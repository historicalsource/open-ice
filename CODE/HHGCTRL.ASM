 	.MLIB	"HHMACS.LIB"
	.FILE	"HHGCTRL.ASM"
	.WIDTH	132
	.OPTION	B,D,L,T
	.MNOLIST

*
*GET THE SYSTEM STUFF
*
	.INCLUDE	"HH.INC"
	.INCLUDE	"HHSTRING.H"
	.INCLUDE	"IMGTBL.GLO"
	.include	"hhgctrl.e"
	.include	"hhram.g"
	.include	"hhmath.g"
	.include	"hhgscr.g"
	.include	"hhpuck.g"
	.include	"hhplayer.g"
	.include	"hhmisc.g"
	.include	"hhproc.g"
	.include	"hhgame.g"
	.include	"hhhigher.g"
	.include	"hhutil.g"
	.include	"hhgscr2.g"
	.include	"hhcontrl.g"
	.include	"hhgscrf.g"
	.include	"hhcontr2.g"
	.include	"hhgdrone.g"
	.include	"hhspeech.g"

; end of include files

;	.BSS	BREAKON4,16		;BREAKAWAY FLAG (ONLY TEMPORARY)
;	.BSS	BREAKON8,16		;BREAKAWAY FLAG (ONLY TEMPORARY)

	.TEXT
	.EVEN

**************************************************************************
*								         *
* GET_TEAMMATE_BLOCKS							 *
* 									 *
* PASS:									 *
* A8 = PLAYER OBJECT							 *
* RETURN:								 *
* A0 = TEAMMATE BLOCK							 *
* A1 = TEAMMATE BLOCK							 *
*								         *
**************************************************************************

GET_TEAMMATE_BLOCKS
	MOVE	*A8(OID),A1,W
	SLL	24,A1
	SRL	28,A1
	SLL	6,A1
	ADDI	TEAMMATE_LIST-(020H*2),A1

	MOVE	*A1+,A0,L
	MOVE	*A1+,A1,L
	RETS

TEAMMATE_LIST
	.long	PLAYER_2_BLOCK,PLAYER_4_BLOCK
	.long	PLAYER_1_BLOCK,PLAYER_4_BLOCK
	.long	0,0
	.long	PLAYER_1_BLOCK,PLAYER_2_BLOCK
	.long	PLAYER_6_BLOCK,PLAYER_8_BLOCK
	.long	PLAYER_5_BLOCK,PLAYER_8_BLOCK
	.long	0,0
	.long	PLAYER_5_BLOCK,PLAYER_6_BLOCK

**************************************************************************
*								         *
* control_goalie - GOALIE CONTROL PROCESS				 *
* 									 *
* PASS:									 *
* A8  = GOALIE OBJECT							 *
* A10 = PLAYER NUMBER							 *
*								         *
**************************************************************************

	.if	DEBUG
	.BSS	GTIME4_PTR,32
	.BSS	GTIME8_PTR,32
	.BSS	GTIME4,8*100
	.BSS	GTIME8,8*100
	.endif

control_goalie
;	P_FORK	WATCH_BREAKAWAY_PROC
	GBLOCK	a10,a10

	CLR	A14
	MOVE	A14,*A10(POF_D_LASTPUCK0),L
	MOVB	A14,*A8(ODT_GFREEZES)
	MOVE	A14,@FIVE_HOLE,W

	.if	DEBUG
	MOVI	GTIME4_PTR,A0
	MOVI	GTIME4,A1

	MOVB	*A10(POF_NUMBER),A14
	SUBK	4,A14
	JRZ	CG_SET_PTR

	MOVI	GTIME8_PTR,A0
	MOVI	GTIME8,A1
CG_SET_PTR
	MOVE	A1,*A0,L
	.endif

CG_LUPE
	.if	DEBUG
	MOVE	@VCOUNT,A14,W
	PUSHP	A14
	.endif

	MOVE	A10,A6

	MOVE	*A6(POF_D_LASTPUCK1),*A6(POF_D_LASTPUCK0),W
	MOVE	*A6(POF_D_PUCK),*A6(POF_D_LASTPUCK1),W

	MOVE	*A6(POF_OBJECT),A8,L

	MOVB	*A6(POF_NUMBER),A14

;	.if	DEBUG
;	CMPI	4,A14
;	JREQ	CG_NAP
;	.endif

	MOVE	@PUCK_OBJECT,A0,L
	MOVE	*A0(OXVAL),A0,L

	DEC	A14
	SRA	2,A14
	JRNZ	CG_CHECK_R
	CMPI	LEFT_BLUELINE_X,A0
	JRLT	CG_GO				;BR = ON THE LEFT
	JRUC	CG_HOME
CG_CHECK_R
	CMPI	RGHT_BLUELINE_X,A0
	JRGT	CG_GO				;BR = ON THE RIGHT
CG_HOME
	CLR	A14
	MOVE	A14,*A8(ODT_VEL),L
	MOVE	A14,*A8(OXVEL),L
	MOVE	A14,*A8(OZVEL),L

	MOVB	*A8(OFLAGS),A14
;	JRNN	CG_NAP				;BR=ON DA SCREEN
	JRN	CG_SET_POS			;BR=OFF DA SCREEN

	MOVE	*A6(POF_FLAGS),A14,W
	BTST	B_PF_HIGHER,A14
;	JRNZ	CG_OFFSCRN_NAP			;BR=UNDER HIGHER CONTROL
	JRNZ	CG_NAP				;BR=UNDER HIGHER CONTROL
	BTST	B_PF_CONTROL,A14
;	JRZ	CG_OFFSCRN_NAP			;BR=UNDER CONTROL
	JRZ	CG_NAP				;BR=UNDER CONTROL
	JRUC	CG_SET_DIR			;SET GSKATE SCRIPT

CG_SET_POS
;	MOVI	RGHT_GOALIE_AT_GOAL,A0
	MOVI	025C0000H,A0
	MOVE	*A8(OXVAL),A14,L
	JRNN	CG_R_SETX			;BR=ON THE RIGHT SIDE
	NEG	A0
CG_R_SETX
	.if	BILL
	.else
	MOVE	A0,*A8(OXVAL),L
	MOVI	CENTER_Z,A14
	MOVE	A14,*A8(OZVAL),L
	.endif

	MOVE	*A6(POF_FLAGS),A14,W
	BTST	B_PF_HIGHER,A14
	JRNZ	CG_OFFSCRN_NAP			;BR=UNDER HIGHER CONTROL
	BTST	B_PF_CONTROL,A14
	JRZ	CG_OFFSCRN_NAP			;BR=UNDER CONTROL

CG_SET_DIR
	MOVK	3,A0
	MOVB	*A6(POF_NUMBER),A14
	SUBK	4,A14
	JRZ	CG_DIR_SET			;BR=LEFT GOALIE
	MOVK	7,A0
CG_DIR_SET
	MOVB	A0,*A6(POF_DIRECTION)
	CALLA	SET_GSKATE_SCRIPT
CG_OFFSCRN_NAP
	JRUC	CG_NAP
;	SLEEP	1
;	JRUC	CG_LUPE
CG_GO
	PUSHP	A10

	MOVE	*A6(POF_FLAGS),A14,W
	BTST	B_PF_HIGHER,A14
	JRNZ	CG_GO2
	BTST	B_PF_CONTROL,A14
	JRZ	CG_GO2

	MOVE	*A8(ODT_GFLAGS),A14,W
	BTST	B_FREEZE,A14
	JRNZ	CG_GO2

	CALLR	FACE_GOALIE
	CALLR	GOALIE_WATCH
	CALLA	PUCK_GOALIE_CHECK0

	MOVE	*A6(POF_FLAGS),A14,W
	BTST	B_PF_HIGHER,A14
	JRNZ	CG_GET_A10
	BTST	B_PF_CONTROL,A14
	JRZ	CG_GET_A10

	MOVE	*A8(ODT_GFLAGS),A14,W
	BTST	B_FREEZE,A14
	JRNZ	CG_GET_A10

	MOVE	*A6(POF_MODE),A14,W
	PUSH	A14

;	CALLA	do_drone_logic_goalie
	CALLA	DO_GOALIE_DRONE_LOGIC

	PULLQ	A14
	CMPI	PM_GCROUCH,A14
	JRNE	CG_SCRIPT			;BR=DON'T RESTORE MODE
	MOVE	*A8(ODT_VEL+010H),A0,W
	JRNZ	CG_SCRIPT			;BR=STILL MOVIN'
	MOVE	A14,*A6(POF_MODE),W
CG_SCRIPT
	PULLPQ	A10
	CALLR	SET_GOALIE_SCRIPT
	JRUC	CG_NAP
CG_GO2
	CALLR	GOALIE_WATCH
	CALLA	PUCK_GOALIE_CHECK0
CG_GET_A10
	PULLPQ	A10

CG_NAP
	.if	DEBUG
	PULLPQ	A14
	MOVE	@VCOUNT,A0,W
	SUB	A14,A0

	MOVI	GTIME4_PTR,A1
	MOVI	GTIME4+(8*100),A2

	MOVB	*A6(POF_NUMBER),A14
	SUBK	4,A14
	JRZ	CG_CHECK_SPACE

	MOVI	GTIME8_PTR,A1
	MOVI	GTIME8+(8*100),A2
CG_CHECK_SPACE
	MOVE	*A1,A14,L
	CMP	A2,A14
	JRLT	CG_TIME_SAVE

	SUBI	8*100,A2
	MOVE	A2,A14
CG_TIME_SAVE
	MOVB	A0,*A14
	ADDK	8,A14
	MOVE	A14,*A1,L
	.endif

;	CALLA	fix_players_trail

	SLEEP	1
	JRUC	CG_LUPE

**************************************************************************
*								         *
* GOALIE_WATCH								 *
* 									 *
* PASS:									 *
* A6 = PLAYER BLOCK							 *
* A8 = GOALIE OBJECT							 *
*								         *
**************************************************************************

GOALIE_WATCH
	MOVE	@PUCK_MODE,A14,W
	CMPI	PUM_ENDPER,A14
	JRNE	GW_CHECK_FACEOFF

	MOVE	*A6(POF_MODE),A14,W
	CMPI	PM_GGOAL,A14
	JREQ	GW_DONE				;BR=ALREADY DOIN' IT

	MOVE	@team1_score,A2,W
	MOVE	@team2_score,A3,W

	CMP	A2,A3
	JREQ	GW_CHECK_FACEOFF		;BR=TIE!

	MOVE	@period,A14,W
	CMPI	3,A14
	JRLT	GW_CHECK_FACEOFF		;BR=NOT END OF GAME

	CLR	A14
	MOVE	A14,*A8(ODT_VEL),L
	MOVE	A14,*A8(OXVEL),L
	MOVE	A14,*A8(OZVEL),L

	MOVI	PM_GGOAL,A14
	MOVE	A14,*A6(POF_MODE),W
	CALLA	take_player_control

	MOVB	*A6(POF_NUMBER),A14
	SUBK	4,A14
	JRZ	GW_EOG_CHECK
	SWAP	A2,A3

GW_EOG_CHECK
	MOVI	SET_GDISGRACE_SCRIPT,A1
	CMP	A2,A3
	JRGT	GW_EOG_SET_ANIM
	MOVI	SET_GCHEER_SCRIPT,A1

GW_EOG_SET_ANIM
	CALL	A1
	JRUC	GW_DONE

GW_CHECK_FACEOFF
	MOVE	@PUCK_CONTROL,A14,W
;	JRNZ	GW_GO				;BR=SOMEBODY HAS CONTROL
	JRZ	GW_CF_GO			;BR=NOBODY HAS CONTROL

	MOVB	*A6(POF_NUMBER),A0
	CMP	A0,A14
	JREQ	GW_GO				;BR=GOALIE GOT DA PUCK

	MOVE	@PUCK_OBJECT,A9,L
	MOVE	*A9(OYVAL),A14,L
	JRN	GW_DONE				;BR=PLAYER WITH PUCK IN AIR
	JRUC	GW_GO

GW_CF_GO

	MOVE	@PUCK_MODE,A14,W
	CMPI	PUM_FACEOFF,A14
	JRNE	GW_CHECK_SCORE

	MOVI	PM_GCROUCH,A14
	MOVE	A14,*A6(POF_MODE),W
	JRUC	GW_DONE

GW_CHECK_SCORE
	CMPI	PUM_SCORE,A14
	JRLT	GW_GO
	CMPI	PUM_SCORE,A14
	JRNE	GW_DONE

	CLR	A14
	MOVE	A14,*A8(ODT_GTSAVES),W

	MOVE	*A6(POF_MODE),A14,W
	CMPI	PM_GSAVE,A14
	JREQ	GW_DONE				;BR=IN-A-SAVE
	CMPI	PM_GGOAL,A14
	JREQ	GW_DONE				;BR=ALREADY DOIN' IT

	CLR	A14
	MOVE	A14,*A8(ODT_VEL),L
	MOVE	A14,*A8(OXVEL),L
	MOVE	A14,*A8(OZVEL),L

	MOVI	PM_GGOAL,A14
	MOVE	A14,*A6(POF_MODE),W
	CALLA	take_player_control

	MOVB	*A6(POF_NUMBER),A14
	SRL	3,A14
	XORI	1,A14
	SLL	6,A14
	ADDI	team1_score,A14
	MOVE	*A14,A14,W

	MOVI	SET_GFRUST2_SCRIPT,A1

	CMPI	9,A14
	JREQ	GW_RESPOND
	CMPI	12,A14
	JREQ	GW_RESPOND
	CMPI	15,A14
	JREQ	GW_RESPOND
	CMPI	19,A14
	JREQ	GW_RESPOND

GW_NORM_UPSET
	MOVI	SET_GFRUST_SCRIPT,A1
	MOVI	511,A0
	CALLA	RANDPER
	JRC	GW_RESPOND			;BR=SHIT OCCURS
	MOVI	SET_GDISGRACE_SCRIPT,A1
GW_RESPOND
	CALL	A1
	JRUC	GW_DONE
GW_GO

	MOVE	*A8(ODT_GLASTSIRQ),A14,L
	MOVE	@WAVEIRQS,A1,L
	SUB	A1,A14

	CMPI	-MAX_SAVE_TTIME,A14
	JRGE	GW_1

	MOVE	*A8(ODT_GTSAVES),A14,W
	JRZ	GW_1

	MOVE	A1,*A8(ODT_GLASTSIRQ),L

	DEC	A14
	MOVE	A14,*A8(ODT_GTSAVES),W
GW_1
	MOVE	*A8(ODT_GPOSSIRQ),A0,L		;IRQ TIME OF POSSESSION
	.if	BILL
	JRUC	GW_2
;	JRZ	GW_2
	.else
	JRZ	GW_2
	.endif

	MOVB	*A6(POF_NUMBER),A1
	MOVE	@PUCK_CONTROL,A14,W
	CMP	A14,A1
	JRNE	GW_1_RESET			;BR=GOALIE DOESN'T HAVE CONTROL
	MOVE	@WAVEIRQS,A14,L
	SUB	A0,A14

	CMPI	FREEZE_TIME/2,A14
	JRLE	GW_2				;BR=NOT HELD LONG ENOUGH YET

	MOVE	*A6(POF_MODE),A1,W
	CMPI	PM_GPASS,A1
	JREQ	GW_2				;BR=PASSING
	CMPI	PM_GHOLDING,A1
	JREQ	GW_1_CHECK_TIME			;BR=STANDING,DON'T FORCE SMOTHER
	CMPI	PM_GSMOTHER,A1
;	JREQ	GW_1_CHECK_TIME			;BR=ALREADY SMOTHERING
	JREQ	GW_2				;BR=ALREADY SMOTHERING
	MOVE	@PUCK_OBJECT,A9,L
	CALLA	A_INBACK			;PUT PUCK BEHIND GOALIE ON SMOTHER
;	CALLA	FIX_GOALIE_DIR
	JRUC	GW_2_SMOTHER			;GET DOWN AND SMOTHER

GW_1_CHECK_TIME
	CMPI	FREEZE_TIME,A14
	JRLE	GW_2				;BR=NOT HELD LONG ENOUGH YET

	MOVB	*A8(ODT_GFREEZES),A14
	CMPI	MAX_FREEZES,A14
	JRLT	GW_1_FREEZE			;BR=FREEZE DA PUCK

	MOVI	FREEZE_PROB,A0
	CALLA	RANDPER
	JRC	GW_1_FREEZE			;BR=FREEZE MF

	CALLA	GOALIE_PASS_PUCK
	JRC	GW_1_FREEZE			;BR=NO PASS
	MOVK	GS_PASSED,A0
	CALLA	GOALIE_SPEECH
	RETS

GW_1_FREEZE
	INC	A14
	MOVB	A14,*A8(ODT_GFREEZES)

	CLR	A14
	MOVE	A14,*A8(ODT_GPOSSIRQ),L		;RESET IRQ TIME OF POSSESSION

	JAUC	PUCK_FROZEN
GW_1_RESET
	CLR	A14
	MOVE	A14,*A8(ODT_GPOSSIRQ),L		;RESET IRQ TIME OF POSSESSION
GW_2
	MOVE	A6,B6
	MOVE	*B6(POF_MODE),B0,W
;	CMPI	PM_GHUGPOST,B0
;	JREQ	GW_ANIM_OK
;	CMPI	PM_GSIDESHUFF,B0
;	JREQ	GW_ANIM_OK
	CMPI	PM_GSTAND,B0
	JREQ	GW_ANIM_OK
	CMPI	PM_GSKATE,B0
	JREQ	GW_ANIM_OK
	CMPI	PM_GSKID,B0
	JREQ	GW_ANIM_OK
	CMPI	PM_GCHECKED,B0
	JREQ	GW_ANIM_OK
	CMPI	PM_GCROSSCHECK,B0
	JRNE	GW_DONE
;	CMPI	PM_GSTANCE,B0
;	JRNE	GW_DONE
GW_ANIM_OK
	MOVE	@PUCK_CONTROL,A1,W
	JRNZ	GW_2_CHECK_TEAM			;BR=SOMEBODY HAS CONTROL

	MOVE	@PUCK_MODE,A14,W
	CMPI	PUM_OPEN,A14
	JREQ	GW_2_CHECK_SAVE			;BR=OPEN SEASON

	MOVE	@PUCK_LAST_CONTROL,A1,W		;CHECK WHO HAS LAST CONTROL
GW_2_CHECK_TEAM
	MOVB	*A6(POF_NUMBER),A0
	CMP	A1,A0
	JREQ	GW_3				;BR=GOALIE HAS PUCK

	EQTEAM	A0,A1
	JRZ	GW_3				;BR=GOALIES'S TEAM HAS PUCK

GW_2_CHECK_SAVE
	MOVE	*A8(ODT_GSAVETIME),A14,W
	JRNZ	GW_3				;BR=SAVIN'

;	MOVE	*A8(ODT_GFLAGS),A14,W
;	BTST	B_MISSSAVE,A14
;	JRNZ	GW_3				;BR=MISSIN' PUCK

	MOVK	1,A7				;SET DRONE FLAG

	MOVE	@PUCK_OBJECT,A9,L
	MOVE	A8,A0
	MOVE	A9,A8
	CALLR	IS_A8_IN_CREASE			;IS PUCK INSIDE CREASE?
	JRNC	GW_2_CHECK			;BR=NOPE.
	MOVE	A0,A8

	MOVE	@PUCK_CONTROL,A1,W
	JRNZ	GW_2_POKE_ALWAYS		;BR=SOMEBODY HAS CONTROL

	MOVE	*A6(POF_D_PUCK),A14,W
	CMPI	048H,A14
	JRGT	GW_3				;BR=TOO FAR AWAY

	MOVE	*A9(OYVAL),A14,L
	CMPI	MAX_GAIN_POSSY,A14
	JRLT	GW_3				;BR=TOO HIGH IN THE AIR

;	MOVI	GOALIE_DIR_TABLE_BEHIND,A11
;	MOVI	GOALIE_DIR_TABLE_REV,A11

	MOVI	GOALIE_DIR_TABLE_REV2,A11

	MOVE	*A9(OXVAL),A0,L
	MOVE	*A9(OZVAL),A1,L
	MOVE	*A8(OXVAL),A2,L
	MOVE	*A8(OZVAL),A3,L
	CALLR	GET_GDIR_TO_POINT_18_T
	MOVE	A11,A11
	JRNN	GW_2_DO_SMOTHER			;BR=PUCK IN FRONT OF GOALIE

	ABS	A11
	MOVB	A11,*A6(POF_DIRECTION)
	JRUC	GW_2_REVERSE_SMOTHER

;	ABS	A11
;	MOVB	A11,*A6(POF_DIRECTION)
;	JRUC	GW_2_REAR_SMOTHER
GW_2_DO_SMOTHER
	MOVB	A11,*A6(POF_DIRECTION)
	JRUC	GW_2_SMOTHER

GW_2_CHECK
	MOVE	A0,A8

	CMPI	PM_GCHECKED,B0
	JREQ	GW_4				;BR=GETTING CHECKED

;	JRUC	GW_3				;SKIP NORMAL SMOTHER

	CALLR	IS_A8_IN_CREASE
	JRNC	GW_3				;BR=NOPE.

	MOVI	M_SW_S|M_SW_P|M_SW_T,A0		;SHOOT AND PASS
	CALLR	CHECK_GOALIE_SWITCHES
	JRZ	GW_2_GO				;BR=HUMAN GOALIE SWITCHES ON
	JRNC	GW_3				;BR=NO DRONE GOALIE

	MOVK	1,A7				;SET DRONE FLAG

;	MOVI	102,A0				;DECIDE IF GOALIE SMOTHER
	MOVI	51,A0				;DECIDE IF GOALIE SMOTHER
	CALLA	RANDPER
	JRC	GW_2_GO				;BR=YEP

	MOVE	@PUCK_CONTROL,A1,W
	JRNZ	GW_2_POKE			;BR=SOMEBODY HAS CONTROL
	JRUC	GW_3
GW_2_GO
	MOVE	@PUCK_CONTROL,A1,W
	JRNZ	GW_2_POKE			;BR=SOMEBODY HAS CONTROL

	CALLR	IS_A8_IN_CREASE
	JRNC	GW_3				;BR=NOPE.

	MOVE	*A6(POF_D_PUCK),A14,W
	CMPI	048H,A14
	JRGT	GW_3				;BR=TOO FAR AWAY

;	MOVE	@PUCK_OBJECT,A9,L
	MOVE	*A9(OYVAL),A14,L
	CMPI	MAX_GAIN_POSSY,A14
	JRLT	GW_3				;BR=TOO HIGH IN THE AIR

	MOVE	*A9(OXVAL),A0,L
	MOVE	*A9(OZVAL),A1,L
	MOVE	*A8(OXVAL),A2,L
	MOVE	*A8(OZVAL),A3,L
	CALLA	find_dir_to_point
	MOVE	A0,A1

	MOVB	*A6(POF_DIRECTION),A0
	CALLA	dir_to_degrees

	MOVE	A1,A14
	SUB	A0,A14
	ABS	A14
	CMPI	XD1800,A14
	JRLE	GW_2_SMOTHER_ANGLE_CHECK	;BR=CHECK ANGLE DIFFERENCE
	SUBI	XD3600,A14
	ABS	A14
GW_2_SMOTHER_ANGLE_CHECK
	CMPI	XD0450,A14
	JRGT	GW_3				;BR=ANGLE TOO WIDE, DON'T

	CLR	A14

	MOVE	A7,A7
	JRNZ	GW_2_SMOTHER			;BR=DRONE SMOTHERING

	MOVE	A14,*A11+,L			;RESET SHOOT SWITCH TIME
	MOVE	A14,*A11+,L			;RESET PASS SWITCH TIME
	MOVE	A14,*A11+,L			;RESET TURBO SWITCH TIME

GW_2_SMOTHER
	CLR	A14
	MOVE	A14,*A8(ODT_VEL),L
	MOVE	A14,*A8(OXVEL),L
	MOVE	A14,*A8(OZVEL),L

	MOVE	A14,*A9(ODT_VEL),L
	MOVE	A14,*A9(OXVEL),L
	MOVE	A14,*A9(OZVEL),L

	MOVE	*A8(ODT_GFLAGS),A14,W
	ANDNI	M_RSMOTH,A14
	MOVE	A14,*A8(ODT_GFLAGS),W

	MOVK	PM_GSMOTHER,A14
	MOVE	A14,*A6(POF_MODE),W
	CALLA	take_player_control
	CALLA	SET_GSMOTHER_SCRIPT
	JRUC	GW_DONE

GW_2_REVERSE_SMOTHER
	CLR	A14
	MOVE	A14,*A8(ODT_VEL),L
	MOVE	A14,*A8(OXVEL),L
	MOVE	A14,*A8(OZVEL),L

	MOVE	A14,*A9(ODT_VEL),L
	MOVE	A14,*A9(OXVEL),L
	MOVE	A14,*A9(OZVEL),L

	MOVE	*A8(ODT_GFLAGS),A14,W
	ORI	M_RSMOTH,A14
	MOVE	A14,*A8(ODT_GFLAGS),W

	MOVK	PM_GSMOTHER,A14
	MOVE	A14,*A6(POF_MODE),W
	CALLA	take_player_control
	CALLA	SET_GSMOTHER_SCRIPT
	JRUC	GW_DONE

GW_2_REAR_SMOTHER
	CLR	A14
	MOVE	A14,*A8(ODT_VEL),L
	MOVE	A14,*A8(OXVEL),L
	MOVE	A14,*A8(OZVEL),L

	MOVE	A14,*A9(ODT_VEL),L
	MOVE	A14,*A9(OXVEL),L
	MOVE	A14,*A9(OZVEL),L

	MOVK	PM_GSMOTHER,A14
	MOVE	A14,*A6(POF_MODE),W
	CALLA	take_player_control
	CALLA	SET_GREARSMOTH_SCRIPT
	JRUC	GW_DONE

GW_2_POKE
;	MOVI	102,A0				;DECIDE IF GOALIE POKE
	MOVI	51,A0				;DECIDE IF GOALIE POKE
	CALLA	RANDPER
	JRNC	GW_3				;BR=NOPE

GW_2_POKE_ALWAYS
;	MOVE	*A8(ODT_PBK),A6,L
;	MOVB	*A6(POF_NUMBER),A0
;	CMP	A1,A0
;	JREQ	GW_3
;
;	EQTEAM	A0,A1
;	JRZ	GW_3				;BR=GOALIES'S TEAM HAS PUCK

	MOVI	0340000H,A4			;DEFAULT NEAR LIMIT
	MOVI	0740000H,A5			;DEFAULT FAR LIMIT

	MOVB	*A6(POF_DIRECTION),A14
	ANDI	3,A14
	SUBK	3,A14
	JRNZ	GW_2P_CHECK_LIMITS		;BR=NOT DIRECTION 3 OR 7

	MOVI	0380000H,A4			;DIR 3(7) NEAR LIMIT
	MOVI	0800000H,A5			;DIR 3(7) FAR LIMIT

GW_2P_CHECK_LIMITS
	MOVE	@PUCK_OBJECT,B9,L
	MOVE	*B9(OXVAL),B0,L
	MOVE	*B9(OXVEL),B4,L
	ADD	B4,B0
	MOVE	B0,A0
	MOVE	*B9(OZVAL),B1,L
	MOVE	*B9(OZVEL),B5,L
	ADD	B5,B1
	MOVE	B1,A1

	MOVE	*A8(OXVAL),A2,L
	MOVE	A2,B2
	MOVE	*A8(OZVAL),A3,L
	MOVE	A3,B3
	CALLA	find_dist_quick2

	CMP	A4,A1
	JRGT	GW_2P_CHECK_FAR

;	MOVI	102,A0				;BR=DECIDE IF GOALIE POKE
;	CALLA	RANDPER
;	JRNC	GW_3				;BR=NOPE

	MOVK	PM_GDEFLECT,A4
	MOVI	SET_GDEFLECT_SCRIPT,A5
	JRUC	GW_2P_CHECK_DIR

GW_2P_CHECK_FAR
;	MOVI	102,A0				;BR=DECIDE IF GOALIE POKE
;	CALLA	RANDPER
;	JRNC	GW_3				;BR=NOPE

	ADD	B4,B0
	ADD	B4,B0
	MOVE	B0,A0
	ADD	B5,B1
	ADD	B5,B1
	MOVE	B1,A1

	MOVE	B2,A2
	MOVE	B3,A3
	CALLA	find_dist_quick2

	CMP	A5,A1
	JRGT	GW_2P_FULLBUTT

	MOVK	PM_GSMOTHER,A4
	MOVI	SET_GPOKECHECK_SCRIPT,A5

GW_2P_CHECK_DIR
	MOVE	B0,A0
	MOVE	B1,A1
	MOVE	B2,A2
	MOVE	B3,A3
	CALLA	find_dir_to_point
	MOVE	A0,A1

	MOVB	*A6(POF_DIRECTION),A0
	CALLA	dir_to_degrees

	MOVE	A1,A14
	SUB	A0,A14
	ABS	A14
	CMPI	XD1800,A14
	JRLE	GW_2P_CD_GO
	SUBI	XD3600,A14
	ABS	A14
GW_2P_CD_GO
	CMPI	XD0450,A14
	JRGT	GW_2P_FULLBUTT

	CLR	A14

	MOVE	A7,A7
	JRNZ	GW_2P_POKE			;BR=DRONE POKING

	MOVE	A14,*A11+,L			;RESET SHOOT SWITCH TIME
	MOVE	A14,*A11+,L			;RESET PASS SWITCH TIME
	MOVE	A14,*A11+,L			;RESET TURBO SWITCH TIME
GW_2P_POKE
;	CLR	A14
	MOVE	A14,*A8(ODT_VEL),L
	MOVE	A14,*A8(OXVEL),L
	MOVE	A14,*A8(OZVEL),L

	MOVE	A4,*A6(POF_MODE),W
	CALLA	take_player_control
	CALL	A5

	JRUC	GW_DONE
GW_2P_FULLBUTT
	MOVE	A7,A7
	JRNZ	GW_3				;BR=DON'T FEEDBACK IF DRONE

	CLR	A14
	MOVE	A14,*A11+,L			;RESET SHOOT SWITCH TIME
	MOVE	A14,*A11+,L			;RESET PASS SWITCH TIME
	MOVE	A14,*A11+,L			;RESET TURBO SWITCH TIME

;	CLR	A14
	MOVE	A14,*A8(ODT_VEL),L
	MOVE	A14,*A8(OXVEL),L
	MOVE	A14,*A8(OZVEL),L

;	MOVE	A14,*A8(ODT_GZIMPACT),L
;	MOVE	A14,*A8(ODT_GXIMPACT),L
;
;	MOVE	*A8(ODT_GFLAGS),A5,W
;	ANDNI	M_TMOVE|M_SAVEMADE|M_MISSSAVE,A5
;	ORI	M_FREEZE|M_PUCKCHECK,A5
;	MOVE	A5,*A8(ODT_GFLAGS),W

	MOVK	PM_GSAVE,A14
	MOVE	A14,*A6(POF_MODE),W
	CALLA	take_player_control
	CALLA	SET_GBUTTERFLY_SCRIPT

	JRUC	GW_DONE
GW_3
	CMPI	PM_GCROSSCHECK,B0
	JREQ	GW_4

	;MAYBE GOALIE ONLY CHECKS INSIDE CREASE.

	CALLR	IS_A8_IN_CREASE
	JRNC	GW_4				;BR=NOPE.

	.if	BILL
;	JRUC	GW_4
	.endif

;	JRUC	GW_3_DRONE			;DRONE GOALIE

	MOVI	M_SW_S|M_SW_T,A0			;SHOOT AND TURBO
	CALLR	CHECK_GOALIE_SWITCHES
;	JRZ	GW_3_GO				;BR=HUMAN GOALIE SWITCHES ON
;	JRNC	GW_4				;BR=NO DRONE GOALIE
	JRC	GW_3_DRONE			;BR=DRONE GOALIE
	JRNZ	GW_4				;BR=HUMAN GOALIE SWITCHES OFF

	MOVE	@PUCK_CONTROL,A1,W
	JRZ	GW_3_HUMAN			;BR=NOBODY HAS THE PUCK
	MOVE	A1,A3				;GET TEAM FROM PLAYER
	DEC	A3
	SRL	2,A3				;TEAM WITH PUCK
	GBLOCK	A1,A1
GW_3_HUMAN
	MOVK	1,B1				;RIGHT GOALIE
	CLR	A7				;LEFT TEAM
	MOVI	POF_D_1P,A0
	MOVI	PLAYER_1_BLOCK,A5
	MOVB	*A6(POF_NUMBER),A14
;	MOVE	A14,B0
	SUBK	4,A14
	JRNZ	GW_3_H_DIST_CHECK		;BR=GOALIE ON RIGHT SIDE
	CLR	B1				;LEFT GOALIE
	MOVK	1,A7				;RIGHT TEAM
	MOVI	POF_D_5P,A0
	MOVI	PLAYER_5_BLOCK,A5
GW_3_H_DIST_CHECK
	XOR	A7,A3				;ZERO IF OFFENSIVE TEAM HAS PUCK
	CLR	A7
	MOVI	040H,A2				;HITTING RANGE
	ADD	A6,A0
	MOVE	*A0+,A14,W			;1ST PLAYER'S DISTANCE
	CMP	A2,A14
	JRGT	GW_3_H_CHECK_2ND		;BR=OUTSIDE RANGE
	MOVE	A5,A7
	CMP	A1,A7
	JREQ	GW_3_H_FACE			;BR=PLAYER HAS PUCK
	MOVE	A3,A3
	JRZ	GW_3_H_CHECK_2ND		;BR=TEAMMATE HAS PUCK
	MOVE	A14,A2
GW_3_H_CHECK_2ND
	ADDI	PLAYER_BLOCK_SIZE,A5
	MOVE	*A0,A14,W			;2ND PLAYER'S DISTANCE
	CMP	A2,A14
	JRGE	GW_3_H_CHECK_3RD		;BR=WITHIN RANGE
	MOVE	A5,A7
	CMP	A1,A7
	JREQ	GW_3_H_FACE			;BR=PLAYER HAS PUCK
	MOVE	A3,A3
	JRZ	GW_3_H_CHECK_3RD		;BR=TEAMMATE HAS PUCK
	MOVE	A14,A2
GW_3_H_CHECK_3RD
	ADDK	020H,A0
	ADDI	PLAYER_BLOCK_SIZE*2,A5
	MOVE	*A0,A14,W			;3RD PLAYER'S (GOALIE) DISTANCE
	CMP	A2,A14
	JRGE	GW_3_H_CLOSEST			;BR=OUTSIDE RANGE
	MOVE	A5,A7
GW_3_H_CLOSEST
	MOVE	A7,A7
;	JRZ	GW_3_GO				;BR=NOBODY CLOSE
	JRZ	GW_4				;BR=NOBODY CLOSE
GW_3_H_FACE
	MOVE	*A7(POF_OBJECT),A9,L
	MOVE	*A9(OXVAL),A0,L
	MOVE	*A9(OZVAL),A1,L
;	CALLR	GOALIE_FACE_XZ
	MOVE	*A8(OXVAL),A2,L
	MOVE	*A8(OZVAL),A3,L
	MOVE	A11,A4				;SAVE SHOOT SWITCH TIME ADDRESS
	CALLA	find_dir_to_point_18
	MOVE	A4,A11				;RESTORE
	MOVE	B1,B1
	JRNZ	GW_3_H_DIR			;BR=RIGHT GOALIE
	INC	A0
	CMPI	2,A0
	JREQ	GW_3_H_DIR			;BR=FIXED DIRECTION 1
	SUBK	2,A0
	CMPI	4,A0
	JREQ	GW_3_H_DIR			;BR=FIXED DIRECTION 5
	INC	A0				;RESTORE ORIGINAL DIRECTION
GW_3_H_DIR
	MOVB	A0,*A6(POF_DIRECTION)

	CLR	A14
	MOVE	A14,*A11+,L			;RESET SHOOT SWITCH TIME
	MOVE	A14,*A11+,L			;RESET PASS SWITCH TIME
	MOVE	A14,*A11+,L			;RESET TURBO SWITCH TIME

	JRUC	GW_3_GO
GW_3_DRONE
;	MOVI	102,A0				;BR=DECIDE IF GOALIE CHECK
	MOVI	51,A0				;BR=DECIDE IF GOALIE CHECK
	CALLA	RANDPER
	JRNC	GW_4				;BR=NOPE
	CLR	A7
	MOVI	POF_D_1P,A0
	MOVI	PLAYER_1_BLOCK,A5
	MOVB	*A6(POF_NUMBER),A14
	MOVE	A14,B0
	SUBK	4,A14
	JRNZ	GW_3_DIST_CHECK			;BR=GOALIE ON RIGHT SIDE
	MOVI	POF_D_5P,A0
	MOVI	PLAYER_5_BLOCK,A5
GW_3_DIST_CHECK
	MOVI	040H,A2				;HITTING RANGE
	ADD	A6,A0
	MOVE	*A0+,A14,W			;1ST PLAYER'S DISTANCE
	CMP	A2,A14
	JRGT	GW_3_CHECK_2ND			;BR=OUTSIDE RANGE
	MOVE	A5,A7
	MOVE	A14,A2
GW_3_CHECK_2ND
	ADDI	PLAYER_BLOCK_SIZE,A5
	MOVE	*A0,A14,W			;2ND PLAYER'S DISTANCE
	CMP	A2,A14
	JRGE	GW_3_CHECK_3RD			;BR=WITHIN RANGE
	MOVE	A5,A7
	MOVE	A14,A2
GW_3_CHECK_3RD
	ADDK	020H,A0
	ADDI	PLAYER_BLOCK_SIZE*2,A5
	MOVE	*A0,A14,W			;3RD PLAYER'S (GOALIE) DISTANCE
	CMP	A2,A14
	JRGE	GW_3_CLOSEST			;BR=OUTSIDE RANGE
	MOVE	A5,A7
GW_3_CLOSEST
	MOVE	A7,A7
	JRZ	GW_4				;BR=NOBODY CLOSE

	MOVE	*A7(POF_OBJECT),A9,L
	MOVE	*A9(OXVAL),A0,L
	MOVE	*A9(OZVAL),A1,L
	MOVE	*A8(OXVAL),A2,L
	MOVE	*A8(OZVAL),A3,L
	CALLA	find_dir_to_point_18
	MOVB	*A6(POF_DIRECTION),A14

	SUB	A14,A0
	JRZ	GW_3_CHECK_PUCK			;BR=SAME DIRECTION
	ABS	A0
	SUBK	1,A0
	JRZ	GW_3_CHECK_PUCK			;BR=ADJACENT DIRECTION
	SUBK	6,A0
	JRNZ	GW_4				;BR=NOT THE 8 TO 1 CASE
GW_3_CHECK_PUCK
	MOVE	@PUCK_CONTROL,A0,W
	JRZ	GW_3_CHECK_PASS			;BR=NO CONTROL

;	MOVE	*A6(POF_MODE),A14,W
;	CMPI	PM_GCHECKED,A14
;	JREQ	GW_4				;BR=GOALIE GETTING CHECKED

	MOVB	*A7(POF_NUMBER),A14
	CMP	A0,A14
	JRNE	GW_3_NOPUCK			;BR=DOESN'T HAVE PUCK
	MOVE	*A7(POF_MODE),A14,W
	CMPI	PM_PASS,A14
	JREQ	GW_4				;BR=PASSING, PREPARE TO SAVE
	CMPI	PM_DUMP,A14
	JREQ	GW_4				;BR=DUMPING, PREPARE TO SAVE
	CMPI	PM_ONETIME,A14
	JREQ	GW_4				;BR=ONE TIMING, PREPARE TO SAVE
	CMPI	PM_SHOOT,A14
	JRNE	GW_3_GO				;BR=NOT SHOOTING

	MOVE	*A7(POF_RELEASE_TIME),A1,L
	MOVE	@WAVEIRQS,A14,L
	SUB	A14,A1
	JRN	GW_4				;BR=BOGUS RELEASE TIME
;	SUBK	12,A1
	SUBK	3,A1
	JRN	GW_4				;BR=NOT ENOUGH TIME TO CHECK

	JRUC	GW_3_GO
GW_3_NOPUCK
	MOVE	B0,A14
	EQTEAM	A0,A14
	JRZ	GW_3_GO				;BR=GOALIES'S TEAM HAS PUCK
	JRUC	GW_4
GW_3_CHECK_PASS
	MOVE	@PUCK_MODE,A14,W
	CMPI	PUM_PASS,A14
	JREQ	GW_3_PASS			;BR=PASSING
	CMPI	PUM_TPASS,A14
	JRNE	GW_4				;BR=NOT PASSING
GW_3_PASS
	MOVE	@PUCK_INTENDED,A0,W
	MOVB	*A7(POF_NUMBER),A14
	CMP	A0,A14
	JRNE	GW_4				;BR=NOT PASSING TO THIS PLAYER
GW_3_GO
	CLR	A14
	MOVE	A14,*A8(ODT_VEL),L
	MOVE	A14,*A8(OXVEL),L
	MOVE	A14,*A8(OZVEL),L

	MOVK	PM_GCROSSCHECK,A14
	MOVE	A14,*A6(POF_MODE),W
	CALLA	take_player_control
	CALLA	SET_GCCHECK_SCRIPT
	JRUC	GW_DONE
GW_4

GW_DONE
	RETS

**************************************************************************
*								         *
* CHECK_GOALIE_SWITCHES							 *
* 									 *
* PASS:									 *
* A0 = SWITCHES TO CHECK						 *
* A6 = PLAYER BLOCK							 *
*								         *
* RETURN:								 *
* A11 = SHOOT GOALIE SWITCH TIME ADDRESS				 *
* IF HUMAN GOALIE, C IS CLR AND Z IS SET IF SWITCHES ON			 *
* IF DRONE GOALIE, C IS SET AND Z IS CLR				 *
*								         *
**************************************************************************

MAX_BUTTON_TIME		EQU	15

GOALIE_SWITCH_TABLE
	.LONG	SWITCH,GTIMES1
	.LONG	SWITCH+8,GTIMES2
	.LONG	SWITCH+16,GTIMES5
	.LONG	SWITCH+24,GTIMES6

CHECK_GOALIE_SWITCHES
	MMTM	SP,A1,A2,A3

	MOVI	GOALIE_CONTROL_4,A3
	MOVB	*A6(POF_NUMBER),A14
	SUBK	4,A14
	JRZ	CGS_CHECK_CONTROL		;BR=LEFT GOALIE #4
	ADDK	010H,A3				;OFFSET FOR RIGHT GOALIE #8
CGS_CHECK_CONTROL
	MOVE	*A3,A3,W
	JRZ	CGS_DRONE_GOALIE		;BR=DRONE GOALIE CONTROL

	GBLOCK	A3,A2				;PLAYER DATA BLOCK

	MOVE	@NPLAYERS,A1,W
	DEC	A1
	JRZ	CGS_1PKIT_P1			;BR=1 PLAYER KIT
	DEC	A1
	JRNZ	CGS_REF_TABLE			;BR=NOT A 2 PLAYER KIT

	MOVE	A14,A14
	JRNZ	CGS_2PKIT_P2			;BR=PLAYER 2
CGS_1PKIT_P1
	MOVI	SWITCH,A1			;PLAYER 1
	MOVI	GTIMES1,A3
	JRUC	CGS_GO

CGS_2PKIT_P2
	MOVI	SWITCH+8,A1			;PLAYER 2
	MOVI	GTIMES2,A3
	JRUC	CGS_GO

CGS_REF_TABLE
	DEC	A3
	SLL	6,A3
	ADDI	GOALIE_SWITCH_TABLE,A3
	MOVE	*A3+,A1,L			;SWITCHES
	MOVE	*A3+,A3,L			;SWITCH IRQ TIMES

CGS_GO
;	MOVE	*A2(POF_FLAGS),A14,W
;	BTST	B_PF_HUMAN,A14
;	JRZ	CGS_DRONE_GOALIE		;BR=DRONE GOALIE

	MOVB	*A1,A1
	AND	A0,A1
	JRNZ	CGS_FAIL			;BR=NO MATCH

	MOVE	@WAVEIRQS,A2,L

	BTST	4,A0
	JRZ	CGS_CHECK_P
	MOVE	*A3,A14,L
	SUB	A2,A14
	ADDK	MAX_BUTTON_TIME,A14
	JRN	CGS_FAIL			;BR=TOO LONG
CGS_CHECK_P
	ADDK	020H,A3
	BTST	5,A0
	JRZ	CGS_CHECK_T
	MOVE	*A3,A14,L
	SUB	A2,A14
	ADDK	MAX_BUTTON_TIME,A14
	JRN	CGS_FAIL			;BR=TOO LONG
CGS_CHECK_T
	ADDK	020H,A3

;READING TURBO AS LEVEL!
;
;	BTST	6,A0
;	JRZ	CGS_PASS
;	MOVE	*A3,A14,L
;	SUB	A2,A14
;	ADDK	MAX_BUTTON_TIME,A14
;	JRN	CGS_FAIL			;BR=TOO LONG

CGS_PASS
;	CLR	A14
;	MOVE	A14,*A3,L
;	SUBK	020H,A3
;	MOVE	A14,*A3,L
;	SUBK	020H,A3
;	MOVE	A14,*A3,L

	SUBI	040H,A3
	MOVE	A3,A11

	CLR	A14
	CLRC
	MMFM	SP,A1,A2,A3
	RETS
CGS_FAIL
	MOVE	A0,A0
	CLRC
	MMFM	SP,A1,A2,A3
	RETS
CGS_DRONE_GOALIE
	MOVE	A0,A0
	SETC
	MMFM	SP,A1,A2,A3
	RETS


**************************************************************************
*								         *
* IS_A8_IN_CREASE - CHECK IF A8 IS INSIDE CREASE RADIUS			 *
* IS_A8_IN_GOALR  - CHECK IF A8 IS INSIDE GOAL RADIUS			 *
* IS_A8_IN_A14R   - CHECK IF A8 IS INSIDE RADIUS A14			 *
* 									 *
* PASS:									 *
* A8  = OBJECT								 *
* A14 = RADIUS FOR IS_A8_IN_A14R					 *
* RETURN:								 *
*  C IF INSIDE RADIUS						 	 *
* NC IF OUTSIDE								 *
*								         *
**************************************************************************

IS_A8_IN_CREASE
	MOVI	CREASE_RADIUS,A14
	JRUC	IS_A8_IN_A14R
IS_A8_IN_GOALR
	MOVI	GOAL_RADIUS,A14
IS_A8_IN_A14R
	MMTM	SP,A0,A1
	MOVE	*A8(OXVAL),A0,L
	ABS	A0

	MOVI	RGHT_GOALLINE_X,A1
	CMP	A1,A0
	JRGT	IA8I_OUTSIDE

	SUB	A14,A1
	CMP	A1,A0
	JRLT	IA8I_OUTSIDE

	MOVE	*A8(OZVAL),A0,L

	MOVI	CENTER_Z,A1
	SUB	A14,A1
	CMP	A1,A0
	JRLT	IA8I_OUTSIDE

	ADD	A14,A1
	ADD	A14,A1
	CMP	A1,A0
	JRHI	IA8I_OUTSIDE			;SETS C IF NO JUMP

	MMFM	SP,A0,A1
	RETS
IA8I_OUTSIDE
	CLRC
	MMFM	SP,A0,A1
	RETS

**************************************************************************
*								         *
* FIND_PLAYER_CLOSER_TO_GOALIE						 *
* 									 *
* PASS:									 *
* A6 = GOALIE PLAYER BLOCK						 *
* RETURN:								 *
* A0 = PLAYER NUMBER CLOSER						 *
*								         *
**************************************************************************

FIND_PLAYER_CLOSER_TO_GOALIE
	MMTM	SP,A1,A2
	MOVB	*A6(POF_NUMBER),A2
	CALLR	FPCG_GO
	MMFM	SP,A1,A2
	RETS
FPCG_GO
	MOVK	1,A0				;GOALIE HAS PUCK
	MOVI	POF_D_1P,A1
	SUBK	4,A2
	JRZ	FPCG_GOT_DOFF			;BR=CHECK PLAYERS 1 AND 2
	MOVK	5,A0
	MOVI	POF_D_5P,A1			;CHECK PLAYERS 5 AND 6
FPCG_GOT_DOFF
	ADD	A6,A1
	MOVE	*A1+,A2,W
	MOVE	*A1,A14,W
	CMP	A2,A14
	JRGE	FPCG_DONE			;BR=PLAYER 1 OR 5 IS CLOSER
	INC	A0				;PLAYER 2 OR 6 IS CLOSER
FPCG_DONE
	RETS

**************************************************************************
*								         *
* FACE_GOALIE - FACE GOALIE IN PROPER DIRECTION				 *
*								         *
* PASS:									 *
* A6 = PLAYER DATA							 *
* A8 = GOALIE OBJECT							 *
* RETURN:								 *
* NUTIN'								 *
*								         *
**************************************************************************

FACE_GOALIE
	MOVE	@PUCK_CONTROL,A1,W
	JRZ	FG_FACE_PUCK			;BR=NOBODY HAS PUCK
	MOVB	*A6(POF_NUMBER),A2
	CMP	A2,A1
	JRNE	FG_FACE_PUCK			;BR=FACE PLAYER WITH PUCK

	CALLR	FPCG_GO				;GOALIE HAS PUCK
	GBLOCK	A0,A5				;FACE CLOSER PLAYER
	MOVE	*A5(POF_OBJECT),A14,L
	MOVE	*A14(OXVAL),A0,L
	MOVE	*A14(OZVAL),A1,L
	JRUC	FG_GET_FACE_DIR

FG_FACE_PUCK
	MOVE	@PUCK_MODE,A14,W
	CMPI	PUM_SCORE,A14
	JREQ	FG_ABORT			;BR=DON'T CHANGE DIR AFTER SCORE

	MOVE	@PUCK_OBJECT,A9,L
	MOVE	*A9(OXVAL),A0,L
	MOVE	*A9(OZVAL),A1,L
FG_GET_FACE_DIR
	MOVE	*A8(OXVAL),A2,L
	MOVE	*A8(OZVAL),A3,L
	CALLR	SET_GDIR_TO_POINT_18H
FG_ABORT
	RETS

;FACE_CENTER
;	MOVK	3,A0
;	CMPI	4,A2
;	JRZ	FC_SET				;BR=LEFT GOALIE
;	MOVK	7,A0
;FC_SET
;	MOVB	A0,*A6(POF_DIRECTION)
;	RETS


**************************************************************************
*								         *
* GET_GDIR_TO_POINT_18_T  - GET GOALIE DIRECTION FACING A [X, Z] POINT	 *
* GET_GDIR_TO_POINT_18H_T - GET GOALIE DIRECTION FACING A [X, Z] POINT	 *
*								         *
* PASS:									 *
* A0  = X TO FACE							 *
* A1  = Z TO FACE							 *
* A2  = GOALIE X							 *
* A3  = GOALIE Z							 *
* A6  = PLAYER BLOCK							 *
* A11 = DIRECTION TABLE							 *
*								         *
* RETURN:								 *
* NUTIN', BUT TRASH A BUNCH						 *
*								         *
**************************************************************************

GET_GDIR_TO_POINT_18_T
	CALLA	find_dir_to_point
GGP_LUPE
	MOVE	*A11,A14,L
	LOCKON	N
	CMP	A0,A14
	JRGT	GGP_CHECK
	ADDI	020H+010H+010H,A11
	JRUC	GGP_LUPE
GGP_CHECK
   	ADDK	020H,A11
	MOVB	*A6(POF_NUMBER),A14
	SUBK	4,A14
	JRZ	GGP_GET_DIR
	ADDK	010H,A11
GGP_GET_DIR
	MOVE	*A11,A11,W
	RETS

GET_GDIR_TO_POINT_18H_T
	CALLA	find_dir_to_point
GGPH_LUPE
	MOVE	*A11,A14,L
	LOCKON	N
	CMP	A0,A14
	JRGT	GGPH_CHECK
	ADDI	020H+010H+010H,A11
	JRUC	GGPH_LUPE
GGPH_CHECK
   	ADDK	020H,A11
	MOVB	*A6(POF_NUMBER),A14
	SUBK	4,A14
	JRZ	GGPH_GET_DIR
	ADDK	010H,A11
GGPH_GET_DIR
	MOVE	*A11,A11,W
	MOVB	*A6(POF_DIRECTION),A2

;	.if	DEBUG
;	LOCKDIR	a2
;	.endif

	CMP	A11,A2
	JREQ	GGPH_SAME

	MOVE	A0,A1
	MOVE	A2,A0
	CMPI	3,A2
	JRNE	GGPH_GET_ANGLE
	CMPI	XD2925,A1
	JRLT	GGPH_GET_ANGLE
	MOVI	XD3600,A0
	JRUC	GGPH_DO_DIFF
GGPH_GET_ANGLE
	CALLA	dir_to_degrees
GGPH_DO_DIFF
	SUB	A1,A0
	ABS	A0
	CMPI	XD0300,A0
	JRLT	GGPH_SAME
;	.if	DEBUG
;	LOCKDIR	a11
;	.endif
	RETS
GGPH_SAME
	MOVE	A2,A11
	RETS

**************************************************************************
*								         *
* SET_GDIR_TO_POINT_18H   - SET GOALIE DIRECTION FACING A [X, Z] POINT	 *
* SET_GDIR_TO_POINT_18H_T - SET GOALIE DIRECTION FACING A [X, Z] POINT	 *
*								         *
* PASS:									 *
* A0  = X TO FACE							 *
* A1  = Z TO FACE							 *
* A2  = GOALIE X							 *
* A3  = GOALIE Z							 *
* A6  = PLAYER BLOCK							 *
* A11 = DIRECTION TABLE (FOR SET_GDIR_TO_POINT_18H_T)		         *
*								         *
* RETURN:								 *
* NUTIN', BUT TRASH A BUNCH						 *
*								         *
**************************************************************************

SET_GDIR_TO_POINT_18H
	MOVI	GOALIE_DIR_TABLE,A11
SET_GDIR_TO_POINT_18H_T
	CALLA	GET_GDIR_TO_POINT_18H_T
	MOVB	A11,*A6(POF_DIRECTION)
	SRL	8,A11
	MOVB	A11,*A6(POF_BDIRECTION)
	RETS

;**************************************************************************
;*								         *
;* GOALIE_FACE_XZ							 *
;* GOALIE_FACE_PUCK							 *
;* 									 *
;* PASS:									 *
;* A0 = OXVAL FOR GOALIE_FACE_XZ						 *
;* A1 = OZVAL FOR GOALIE_FACE_XZ						 *
;* A6 = PLAYER DATA ADDRESS						 *
;* A8 = GOALIE OBJECT ADDRESS						 *
;* RETURN:								 *
;* NUTIN'								 *
;*								         *
;**************************************************************************
;
;GOALIE_FACE_XZ
;	MMTM	SP,A0,A1,A2,A3,A9,A11
;	MOVE	*A8(OXVAL),A2,L
;	MOVE	*A8(OZVAL),A3,L
;	JRUC	GFP_GO
;
;GOALIE_FACE_PUCK
;	MMTM	SP,A0,A1,A2,A3,A9,A11
;	MOVE	@PUCK_OBJECT,A9,L
;	MOVE	*A9(OXVAL),A0,L
;	MOVE	*A9(OZVAL),A1,L
;	MOVE	*A8(OXVAL),A2,L
;	MOVE	*A8(OZVAL),A3,L
;GFP_GO
;	CALLA	find_dir_to_point
;	MOVI	GOALIE_DIR_TABLE,A11
;GFP_LUPE
;	MOVE	*A11,A14,L
;	LOCKON	N
;	CMP	A0,A14
;	JRGT	GFP_CHECK
;	ADDI	020H+010H+010H,A11
;	JRUC	GFP_LUPE
;GFP_CHECK
;	ADDK	020H,A11
;	MOVB	*A6(POF_NUMBER),A14
;	SUBK	4,A14
;	JRZ	GFP_GET_DIR
;	ADDK	010H,A11
;GFP_GET_DIR
;	.if	DEBUG
;	movb	*a11,a11
;;	LOCKDIR	a11
;	movb	a11,*a6(POF_DIRECTION)
;	.else
;	MOVB	*A11(0),*A6(POF_DIRECTION)
;	.endif
;	MMFM	SP,A0,A1,A2,A3,A9,A11
;	RETS

GOALIE_DIR_TABLE
	LWW	XD0225,0003H,0007H	;3
	LWW	XD0675,0004H,0506H	;4
	LWW	XD1125,0504H,0506H	;5
	LWW	XD1575,0504H,0006H	;6
	LWW	XD2025,0003H,0007H	;7
	LWW	XD2475,0102H,0008H	;8
	LWW	XD2925,0102H,0108H	;1
	LWW	XD3375,0002H,0108H	;2
	LWW	XD3825,0003H,0007H	;3
	.long	-1

;GOALIE_DIR_TABLE_REV
;	LWW	XD0225,3,3	;3
;	LWW	XD0675,4,4	;4
;	LWW	XD1125,4,6	;5
;	LWW	XD1575,6,6	;6
;	LWW	XD2025,7,7	;7
;	LWW	XD2475,8,8	;8
;	LWW	XD2925,2,8	;1
;	LWW	XD3375,2,2	;2
;	LWW	XD3825,3,3	;3
;	.long	-1

GOALIE_DIR_TABLE_REV2
	LWW	XD0225, 3,-3	;3
	LWW	XD0675, 4,-4	;4
	LWW	XD1125, 4, 6	;5
	LWW	XD1575,-6, 6	;6
	LWW	XD2025,-7, 7	;7
	LWW	XD2475,-8, 8	;8
	LWW	XD2925, 2, 8	;1
	LWW	XD3375, 2,-2	;2
	LWW	XD3825, 3,-3	;3
	.long	-1

GOALIE_DIR_TABLE_BEHIND
	LWW	XD0225, 3,-7	;3
	LWW	XD0675, 4,-8	;4
	LWW	XD1125, 4, 6	;5
	LWW	XD1575,-2, 6	;6
	LWW	XD2025,-3, 7	;7
	LWW	XD2475,-4, 8	;8
	LWW	XD2925, 2, 8	;1
	LWW	XD3375, 2,-6	;2
	LWW	XD3825, 3,-7	;3
	.long	-1

;**************************************************************************
;*								         *
;* FIX_GOALIE_DIR - FIX GOALIE'S DIRECTION (TRANSLATE DIRECTIONS 1 AND 5) *
;* 									 *
;* PASS:									 *
;* A8 = GOALIE OBJECT							 *
;*								         *
;**************************************************************************
;
;FIX_GOALIE_DIR
;	PUSH	A0
;	MOVB	*A6(POF_DIRECTION),A0
;	DEC	A0
;	SLL	5,A0
;	ADDI	FIX_GOALIE_DIR_TABLE,A0
;	MOVB	*A6(POF_NUMBER),A14
;	SRL	3,A14
;	SLL	4,A14
;	ADD	A14,A0
;	MOVB	*A0(0),*A6(POF_DIRECTION)
;	PULLQ	A0
;	RETS
;
;FIX_GOALIE_DIR_TABLE
;	.word	0102H,0108H	;1
;	.word	0002H,0108H	;2
;	.word	0003H,0007H	;3
;	.word	0004H,0506H	;4
;	.word	0504H,0506H	;5
;	.word	0504H,0006H	;6
;	.word	0003H,0007H	;7
;	.word	0102H,0008H	;8

**************************************************************************
*								         *
* SET_GOALIE_SCRIPT							 *
*								         *
**************************************************************************

SET_GOALIE_SCRIPT
	MOVE	*A6(POF_MODE),A0,W
	SLL	5,A0
	ADDI	GOALIE_SCRIPTS,A0
	MOVE	*A0,A0,L
	JRZ	SGS_NOPE

;	CMPI	SET_GSKATE_SCRIPT,A0
;	JREQ	SGS_SET					;BR=NOT SKATE SCRIPT
;	CLR	A14
;	MOVB	A14,*A6(POF_BDIRECTION)

;	MOVB	*A6(POF_BDIRECTION),A1
;	JRZ	SGS_SET					;BR=DIRECTION OKAY
;	CLR	A14
;	MOVB	A14,*A6(POF_BDIRECTION)
;	CMPI	SET_GSKATE_SCRIPT,A0
;	JRNE	SGS_SET					;BR=NOT SKATE SCRIPT
;	MOVB	A1,*A6(POF_DIRECTION)
SGS_SET
	JUMP	A0
SGS_NOPE
	RETS

GOALIE_SCRIPTS
	.long	SET_GSKATE_SCRIPT		;STAND
	.long	SET_GSKATE_SCRIPT		;SKATE
	.long	0				;PASS
	.long	0				;SHOOT
	.long	SET_GSKATE_SCRIPT		;SKID
	.long	0				;STEAL
	.long	0				;CROSSCHECK
	.long	0				;SHAKE
	.long	0				;DUMP
	.long	0				;STANCE
	.long	0				;FALL
	.long	0				;DEFLECT
	.long	0				;SAVE
	.long	0				;ONETIME
	.long	0				;SMOTHER
	.long	SET_GHOLDING_SCRIPT		;HOLDING
	.long	0				;AFTER GOAL
	.long	SET_GCROUCHF_SCRIPT		;CROUCH AT FACEOFF
	.long	SET_GHUGPOST_SCRIPT		;HUG POST
	.long	SET_GSIDESHUFF_SCRIPT		;SIDE SHUFFLE
	.long	0				;RECEIVING A CHECK
	
	.end

;**************************************************************************
;*								         *
;* WATCH_BREAKAWAY_PROC - ONLY TEMPORARY					 *
;*								         *
;**************************************************************************
;
;WATCH_BREAKAWAY_PROC
;	MOVE	*A8(ODT_PBK),A6,L
;	MOVB	*A6(POF_NUMBER),A14
;
;	MOVK	1,A0			;PLAYERS 1, 2, 4
;	MOVK	1,A8			;RIGHT SIDE FLAG
;	MOVI	BREAKON8,A10
;	CMPI	4,A14
;	JRNE	WBP_RIGHT		;BR=GOALIE ON THE RIGHT
;	MOVK	5,A0			;PLAYERS 5, 6, 8
;	MOVI	-1,A8			;LEFT SIDE FLAG
;	MOVI	BREAKON4,A10
;WBP_RIGHT
;	CLR	A11
;	MOVE	A11,*A10,W		;RESET BREAKAWAY FLAG
;	GBLOCK	a0,a6
;	MOVE	A6,A9
;WBP_LUPE
;	MOVE	A11,A11
;	JRNZ	WBP_BREAK			;BR=WE ARE HAVING A BREAKAWAY
;
;	CALLR	GOALIE_CHECK_ZONE
;	MOVE	A0,A0
;	JRZ	WBP_NAP				;BR=NO BREAKAWAY
;
;	CALLR	BREAKAWAY_CHECK_OFFZONE
;	JRZ	WBP_NAP				;BR=DEFENSIVE PLAYER NEAR
;
;	MOVE	A0,A11
;	MOVK	1,A14
;	MOVE	A14,*A10,W			;BREAKAWAY
;	JRUC	WBP_NAP
;
;WBP_BREAK
;	MOVE	A11,A11
;	JRN	WBP_CHECK_EMPTY_ZONE		;BR=GO CHECK FOR EMPTY ZONE
;
;	CALLR	GOALIE_CHECK_ZONE
;	MOVE	A2,A2
;	JRNZ	WBP_CHECK_GOALLINE		;BR=ZONE NOT EMPTY
;	CLR	A11				;ZONE EMPTY
;	MOVE	A11,*A10,W
;	JRUC	WBP_NAP
;WBP_CHECK_GOALLINE
;	MOVE	*A11(OXVAL),A14,L		;WATCH BREAKAWAY PLAYER'S X
;	ABS	A14
;	CMPI	RGHT_GOALLINE_X,A14
;	JRGT	WBP_NOMORE_BREAK		;BR=PLAYER CROSSED GOAL LINE
;
;;	MOVE	*A11(ODT_PBK),A6,L
;;	MOVE	*A6(POF_MODE),A14,W
;;	CMPI	PM_PASS,A14
;;	JRNE	WBP_NAP				;BR=NOT PASSING
;
;	MOVE	@PUCK_MODE,A14,W
;	CMPI	PUM_PASS,A14
;	JREQ	WBP_PASS			;BR=PASS
;	CMPI	PUM_TPASS,A14
;	JRNE	WBP_CHECK_DEFENSE		;BR=TURBO PASS
;WBP_PASS
;
;	;CHECK FOR ILLEGAL BREAKAWAY PASS (ACROSS THE BLUE LINE)
;
;	MOVE	@PUCK_LAST_CONTROL,A0,W
;	MOVE	*A11(OID),A14,W
;	SLL	24,A14
;	SRL	28,A14
;	CMP	A0,A14
;	JRNE	WBP_CHECK_DEFENSE		;BR=THIS PLAYER DIDN'T HAVE CTRL
;
;	MOVE	@PUCK_INTENDED,A0,W
;	JRZ	WBP_CHECK_DEFENSE		;BR=NOT SET
;	GBLOCK	a0,a6
;	CALLA	PLAYER_LOC
;	CMP	A8,A1
;	JREQ	WBP_NOMORE_BREAK		;BR=PASSING TO PLAYER IN ZONE
;WBP_CHECK_DEFENSE
;	MOVE	A11,A0
;	CALLR	BREAKAWAY_CHECK_OFFZONE
;	JRZ	WBP_NOMORE_BREAK		;BR=DEFENSIVE PLAYER NEAR
;
;	MOVE	*A11(OXVAL),A2,L
;	ABS	A2
;
;	MOVE	*A11(OID),A0,W
;	SLL	24,A0
;	SRL	28,A0
;	SLL	6,A0
;	ADDI	TEAMMATE_LIST-(020H*2),A0
;
;	MOVE	*A0+,A6,L
;	CALLA	PLAYER_LOC
;	CMP	A8,A1
;	JRNE	WBP_OTHER_TEAMMATE		;BR=NOT ON THIS SIDE
;	MOVE	*A6(POF_OBJECT),A1,L
;	MOVE	*A1(OXVAL),A14,L
;	ABS	A14
;	CMP	A2,A14
;	JRGE	WBP_NOMORE_BREAK
;WBP_OTHER_TEAMMATE
;	MOVE	*A0,A6,L
;	CALLA	PLAYER_LOC
;	CMP	A8,A1
;	JRNE	WBP_MORE_CHECKS			;BR=NOT ON THIS SIDE
;	MOVE	*A6(POF_OBJECT),A1,L
;	MOVE	*A1(OXVAL),A14,L
;	ABS	A14
;	CMP	A2,A14
;	JRGE	WBP_NOMORE_BREAK
;
;;WBP_
;;	MOVE	@PUCK_CONTROL,A0,W
;;	MOVE	*A6(POF_NUMBER),A14,L
;;	CMP	A0,A14
;;	JRNE	WBP_NAP				;BR=DOESN'T HAVE THE PUCK
;;	MOVE	A1,A11
;;	JRUC	WBP_NAP
;
;WBP_MORE_CHECKS
;
;	;A11 RETURNS TO DEFENSIVE ZONE (MAYBE HE PASSES TO A TEAMMATE)
;
;	JRUC	WBP_NAP
;WBP_NOMORE_BREAK
;	NEG	A11
;	CLR	A14
;	MOVE	A14,*A10,W			;NO MORE BREAKAWAY POSITION
;	JRUC	WBP_NAP
;
;WBP_CHECK_EMPTY_ZONE
;	CALLR	GOALIE_CHECK_ZONE
;	MOVE	A2,A2
;	JRZ	WBP_EMPTY			;BR=ZONE IS EMPTY
;	DEC	A2
;	JRNZ	WBP_NAP				;TOO MANY OFFENSIVES IN ZONE
;
;	CALLR	BREAKAWAY_CHECK_OFFZONE
;	JRZ	WBP_NAP				;BR=DEFENSIVE PLAYER NEAR
;
;	MOVE	A0,A11				;RETURN TO BREAKAWAY
;	MOVK	1,A14
;	MOVE	A14,*A10,W			;BREAKAWAY
;	JRUC	WBP_NAP
;WBP_EMPTY
;	CLR	A11				;ZONE EMPTY
;WBP_NAP
;	SLEEP	1
;	JRUC	WBP_LUPE
;
;**************************************************************************
;*								         *
;* BREAKAWAY_CHECK_OFFZONE - PLAYER ON BREAKAWAY CHECKS ZONE FOR DEFENSE	 *
;* 									 *
;* PASS:									 *
;* A0 = OFFENSIVE OBJECT TO CHECK					 *
;* RETURN:								 *
;*  Z = DEFENSIVE PLAYER NEAR						 *
;* NZ = NO DEFENSE NEAR							 *
;*								         *
;**************************************************************************
;
;BREAKAWAY_CHECK_OFFZONE
;	MOVE	*A0(ODT_PBK),A6,L
;	MOVB	*A6(POF_NUMBER),A14
;	MOVI	100,A2				;FLAG RADIUS
;
;;	MOVE	*A0(OXVAL),A3,L
;;	ABS	A3				;POSITION IN ZONE
;
;	MOVI	POF_D_5P,A1			;RIGHT DEFENSE
;	SUBK	5,A14
;	JRN	BCO_GO				;BR=CHECK THE RIGHT DEFENSE
;	MOVI	POF_D_1P,A1			;LEFT DEFENSE
;BCO_GO
;	ADD	A1,A6
;	MOVE	*A6+,A14,W
;	CMP	A2,A14
;	JRGT	BCO_CHECK_OTHER			;BR=TOO FAR
;	CLR	A14				;FLAG DEFENSIVE PLAYER NEAR
;	RETS
;BCO_CHECK_OTHER
;	MOVE	*A6,A14,W
;	CMP	A2,A14
;	JRGT	BCO_FAR				;BR=TOO FAR
;	CLR	A14				;FLAG DEFENSIVE PLAYER NEAR
;	RETS
;BCO_FAR
;	CLR	A14				;NO DEFENSE NEAR
;	NOT	A14
;	RETS
;
;**************************************************************************
;*								         *
;* GOALIE_CHECK_ZONE - CHECK FOR PLAYERS IN ZONE				 *
;* 									 *
;* PASS:									 *
;* A8, A9 FROM PROCESS ABOVE						 *
;* RETURN:								 *
;* A0 = PLAYER ON BREAKAWAY						 *
;* A2 = ZONE NOT EMPTY FLAG						 *
;*								         *
;**************************************************************************
;
;GOALIE_CHECK_ZONE
;	CLR	A0				;PLAYER ON BREAKAWAY
;	CLR	A2				;ZONE NOT EMPTY FLAG
;
;	MOVE	A9,A6
;	CALLA	PLAYER_LOC
;	CMP	A8,A1
;	JRNE	GCZ_NEXT0			;BR=NOT ON THIS SIDE
;	INC	A2
;	MOVE	*A6(POF_OBJECT),A0,L
;GCZ_NEXT0
;	ADDI	PLAYER_BLOCK_SIZE,A6
;	CALLA	PLAYER_LOC
;	CMP	A8,A1
;	JRNE	GCZ_NEXT1			;BR=NOT ON THIS SIDE
;	INC	A2
;	MOVE	A0,A0
;	JRNZ	GCZ_CLEAR			;BR=OFFENSIVE TEAMMATE ON SIDE
;	MOVE	*A6(POF_OBJECT),A0,L
;GCZ_NEXT1
;	ADDI	PLAYER_BLOCK_SIZE*2,A6
;	MOVE	*A6(POF_FLAGS),A14,W
;	BTST	B_PF_GOALIE,A14
;	JRZ	GCZ_DONE			;BR=NOT A OFFENSIVE PLAYER
;	CALLA	PLAYER_LOC
;	CMP	A8,A1
;	JRNE	GCZ_DONE			;BR=NOT ON THIS SIDE
;	INC	A2
;	MOVE	A0,A0
;	JRNZ	GCZ_CLEAR			;BR=OFFENSIVE TEAMMATE ON SIDE
;	MOVE	*A6(POF_OBJECT),A0,L
;	JRUC	GCZ_DONE
;GCZ_CLEAR
;	CLR	A0
;GCZ_DONE
;	RETS
