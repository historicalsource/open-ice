 	.MLIB	"HHMACS.LIB"
	.FILE	"HHSTATS.ASM"
	.WIDTH	132
	.OPTION	B,D,L,T
	.MNOLIST

*
*GET THE SYSTEM STUFF
*
	.INCLUDE	"HH.INC"
	.INCLUDE	"HHSTRING.H"
	.INCLUDE	"IMGTBL.GLO"
	.include	"HHSTATS.TBL"
	.include	"HHSTATS2.TBL"
	.include	"hhstats.e"
	.include	"hhpick.g"
	.include	"hhstabl2.g"
	.include	"hhutil.g"
	.include	"hhscore.g"
	.include	"hhd.g"
	.include	"hhtext.g"
	.include	"hhtg.g"
	.include	"hhram.g"
	.include	"hhpall.g"
	.include	"hhfade.g"
	.include	"hhgame.g"
	.include	"hhsnd.g"
	.include	"hhc.g"
	.include	"hhplayer.g"
	.include	"HHPROC.G"
	.include	"hhstable.g"
	.include	"hhmisc.g"

; end of include files

	.BSS	P1_STATS,8*8
	.BSS	P2_STATS,8*8
	.BSS	P3_STATS,8*8
	.BSS	P4_STATS,8*8

	.BSS	HILITE_SOUND_FLAG,16

	.TEXT
	.EVEN

**************************************************************************
*								         *
* DISPLAY_INTERMISSION_PAGE						 *
* 									 *
* PASS:									 *
* NUTIN'								 *
* RETURN:								 *
* NUTIN'								 *
*								         *
**************************************************************************

P1_STAT_X	EQU	11-5
P1_STAT_Y	EQU	55

P4_STAT_X	EQU	211+5
P4_STAT_Y	EQU	154

P2_STAT_X	EQU	P1_STAT_X+10

P3_STAT_X	EQU	P4_STAT_X-10

DISPLAY_INTERMISSION_PAGE
	BLACKON

	MOVI	FONTGRYP,A0
	CALLA	GETFPAL
;	MOVI	PRPTXT1P,A0
	MOVI	ORGTXT1P,A0
	CALLA	GETFPAL
	MOVI	GLDTXT5P,A0
	CALLA	GETFPAL

	MOVI	METAL_ICE_INIT,B0
	CALLA	MULTIMAKE
;	MOVI	BLUMETALP,A0
;	CALLA	CHANGE_PAL_MULTI

	MOVE	@GAME_STATE,A14,W
	CMPI	INEPILOG,A14
	JRNE	DIP_GET_HEADER			;BR=GET HEADER FROM TABLE
	MOVI	END_OF_GAME_STRING,A8
	MOVI	H18FONT,A11			;FONT
	JRUC	DIP_PUT_HEADER

DIP_GET_HEADER
	MOVE	@period,A8,W
	DEC	A8
	SLL	5,A8
	ADDI	INTER_HEADER_TABLE,A8
	MOVE	*A8,A8,L			;POINTER TO STRING
	MOVI	H18TFONT,A11			;FONT

DIP_PUT_HEADER
	MOVI	OID_TEXT,B1			;[SOUND,OID]
	CLR	A0				;NO SLEEP TIL
	MOVI	STRCNCONV,A1			;ROUTINE
	MOVI	HFPAL+0101H,A5			;[COLOR,PALETTE]
	MOVI	[3,199],A9
	MOVI	[0,-1],A10			;[Y,X] SPACING
	JSRP	PRINTF

	MOVE	@team1,A9,W
	SLL	10,A9
	ADDI	team_data,A9

	MOVI	LOGO_INIT,B0
	CALLA	EASYMAKE
	MOVI	[27,47],A1
	MOVK	10,A2
	CALLA	SET_SCRNOBJ
	CALLA	INSERT_OBJ
	MOVE	*A8(OCTRL),A4,W
	MOVE	*A9(TD_TEAMLOGO_IMG),A1,L
	CALLA	ANI
	MOVE	*A1(ICMAP),A0,L
	CALLA	CHANGE_PAL

	MOVI	[30,103],A5
	MOVI	[42,103],A6
	MOVE	@team1,A14,W
	CMPI	team_mighty_ducks,A14
	JRNE	DIP_LEFT_TEAM_NAME		;BR=NO MIGHTY FUDGE
	SWAP	A5,A6
DIP_LEFT_TEAM_NAME	

	MOVI	TEAM_NAME_INIT,B0
	CALLA	EASYMAKE
	MOVE	A5,A1
	CLR	A2
	CALLA	SET_SCRNOBJ
	CALLA	INSERT_OBJ
	MOVE	*A8(OCTRL),A4,W
	MOVE	*A9(TD_CITY_IMG),A1,L
	CALLA	ANI
	MOVI	BLUTXT3P,A0
	CALLA	CHANGE_PAL

	MOVI	TEAM_NAME_INIT,B0
	CALLA	EASYMAKE
	MOVE	A6,A1
	CLR	A2
	CALLA	SET_SCRNOBJ
	CALLA	INSERT_OBJ
	MOVE	*A8(OCTRL),A4,W
	MOVE	*A9(TD_TEAMNAME_IMG),A1,L
	CALLA	ANI
	MOVI	BLUTXT3P,A0
	CALLA	CHANGE_PAL

	MOVE	@team2,A9,W
	SLL	10,A9
	ADDI	team_data,A9

	MOVI	LOGO_INIT,B0
	CALLA	EASYMAKE
	MOVI	[27,352],A1
	MOVK	10,A2
	CALLA	SET_SCRNOBJ
	CALLA	INSERT_OBJ
	MOVE	*A8(OCTRL),A4,W
	MOVE	*A9(TD_TEAMLOGO_IMG),A1,L
	CALLA	ANI
	MOVE	*A1(ICMAP),A0,L
	CALLA	CHANGE_PAL

	MOVI	[30,295],A5
	MOVI	[42,295],A6
	MOVE	@team2,A14,W
	CMPI	team_mighty_ducks,A14
	JRNE	DIP_RIGHT_TEAM_NAME		;BR=NO MIGHTY FUDGE
	SWAP	A5,A6
DIP_RIGHT_TEAM_NAME	

	MOVI	TEAM_NAME_INIT,B0
	CALLA	EASYMAKE
	MOVE	A5,A1
	CLR	A2
	CALLA	SET_SCRNOBJ
	CALLA	INSERT_OBJ
	MOVE	*A8(OCTRL),A4,W
	MOVE	*A9(TD_CITY_IMG),A1,L
	CALLA	ANI
	MOVI	REDTXT3P,A0
	CALLA	CHANGE_PAL

	MOVI	TEAM_NAME_INIT,B0
	CALLA	EASYMAKE
	MOVE	A6,A1
	CLR	A2
	CALLA	SET_SCRNOBJ
	CALLA	INSERT_OBJ
	MOVE	*A8(OCTRL),A4,W
	MOVE	*A9(TD_TEAMNAME_IMG),A1,L
	CALLA	ANI
	MOVI	REDTXT3P,A0
	CALLA	CHANGE_PAL

	MOVI	PLAYER_1_STAT_INIT,B0
	MOVI	PLAYER_1_BLOCK,A9
	MOVI	P1_STATS,A11
	CALLR	DO_PLAYER_STAT

	MOVI	PLAYER_2_STAT_INIT,B0
	MOVI	PLAYER_2_BLOCK,A9
	MOVI	P2_STATS,A11
	CALLR	DO_PLAYER_STAT

	MOVI	PLAYER_3_STAT_INIT,B0
	KITN	4
	JREQ	DIP_3_GO			;BR=4 PLAYER KIT
	MOVI	PLAYER_3_2P_STAT_INIT,B0
DIP_3_GO
	MOVI	PLAYER_5_BLOCK,A9
	MOVI	P3_STATS,A11
	CALLR	DO_PLAYER_STAT

	MOVI	PLAYER_4_STAT_INIT,B0
	KITN	4
	JREQ	DIP_4_GO			;BR=4 PLAYER KIT
	MOVI	PLAYER_4_2P_STAT_INIT,B0
DIP_4_GO
	MOVI	PLAYER_6_BLOCK,A9
	MOVI	P4_STATS,A11
	CALLR	DO_PLAYER_STAT

	BLACKOFF

	MOVI	99,A14
	MOVE	A14,@MAX_PALXFERS,W
	CLR	A0
	CALLA	FADEINF			;FADE DOWN ALL PALETTES
	JSRP	FADE_WAIT
	CALLA	RESET_MAX_PALXFERS

;	MOVI	REDTXT3P,A8
;	MOVK	4,A9
;	MOVK	20,A10
;	MOVK	2,A11
;	CREATE	PID_COLOR,CYCLE16

	JSRP	REVIEW_STATS

	RETP

**************************************************************************
*								         *
* C_CHECK_STAT_2P - CHECK FOR 2 PLAYER KIT				 *
*								         *
**************************************************************************

C_CHECK_STAT_2P
	KITN	4
	JREQ	CCS2P_DONE			;BR=4 PLAYER KIT
	MOVE	*A0(OCTRL),A14,W			
	SRL	4,A14				;TURN OFF OBJECT
	SLL	4,A14
	MOVE	A14,*A0(OCTRL),W
CCS2P_DONE
	RETS

**************************************************************************
*								         *
* REVIEW_STATS								 *
*								         *
* PASS:									 *
* NUTIN'								 *
* RETURN:								 *
* NUTIN'								 *
*								         *
**************************************************************************

REVIEW_STATS
	CLR	A0				;FIRST STAT OFFSET
	CLR	A11				;RESET BUTTON HITS
RS_LUPE
	CALLR	FIND_STAT_LEADERS
	MOVK	25,A9
	MOVE	A11,A11
	JRZ	RS_HILITE			;BR=NO BUTTON YET
	MOVK	10,A9
RS_HILITE
	CALLR	HILITE_LEADERS_STAT

	MOVE	A1,A1
	JRZ	RS_L_NEXT			;BR=NO LEADERS

	MMTM	A12,A0,A1,A9
RS_NAP_LUPE
	SLEEP	1
	CALLA	CHECK_BUTTON_ABORT
	DSJS	A9,RS_NAP_LUPE	
	MMFM	A12,A0,A1,A9
RS_L_NEXT
	ADDK	8,A0
	CMPI	8*7,A0
	JRLT	RS_LUPE

	MOVE	A1,A2
	JRZ	RS_DONE				;BR=NO POINT LEADERS

;	SUBK	8,A0
;	MOVE	A0,A10
;	MOVE	A1,A11
;	CREATE	PID_IND,FLASH_POINTS_STAT_PROC

	CLR	A14
RS_COUNT_LEADERS_LUPE
	BTST	0,A1
	JRZ	RS_CLL_NEXT			;BR=NOT POINT LEADIN'
	INC	A14
RS_CLL_NEXT
	SRL	1,A1
	JRNZ	RS_COUNT_LEADERS_LUPE		;BR=MORE TO COUNT

	DEC	A14
	JRZ	RS_SET_ANIM			;BR=ONLY ONE LEADER

	MOVI	8*4,A0				;GOAL STAT
	CALLR	FIND_STAT_LEADERS
	AND	A1,A2				;GOAL AND POINT LEADER

RS_SET_ANIM
	MOVI	SUPPLSTS-OSLINK+1*020H,A8
	MOVE	@period,A4,W
	SUBK	4,A4
	JRZ	RS_SA_NEXT			;BR=END OF OVERTIME GAME
	INC	A4
	JRNZ	RS_SA_NEXT			;BR=NOT END OF 3RD PERIOD
	MOVE	@team1_score,A0,W
	MOVE	@team2_score,A1,W
	CMP	A0,A1
	JRNE	RS_SA_NEXT			;BR=END OF 3-PERIOD GAME
	MOVK	1,A4				;FLAG NOT END OF GAME
	JRUC	RS_SA_NEXT
RS_SA_LUPE
	MOVE	*A8(OID),A3,W
	ANDI	0F0H,A3
	SRL	4,A3
	DEC	A3
	BTST	A3,A2
	JRZ	RS_SA_NEXT			;BR=NOT POINT LEADIN'

	MOVI	CROWD_FLASH_ANIM,A1
	CALLA	STRT_ANIM

	MOVE	A4,A4
	JRNZ	RS_SA_NEXT			;BR=NO STARS

	CALLR	MAKE_STARS
RS_SA_NEXT
	MOVE	*A8(OSLINK),A8,L
	JRNZ	RS_SA_LUPE

RS_DONE
	RETP


**************************************************************************
*								         *
* MAKE_STARS								 *
* 									 *
* PASS:									 *
* A3 = PLAYER NUMBER - 1						 *
*								         *
**************************************************************************

MAKE_STARS
	MMTM	SP,A2,A3,A4,A8
	
	MOVI	STARS_INIT,B0
	CALLA	EASYMAKE
	JRZ	MS_ABORT

	MOVI	STARS_YX_TABLE,A1
	KITN	4
	JREQ	MS_GET_YXPOS			;BR=4 PLAYER KIT
	MOVI	STARS_YX_2P_TABLE,A1
MS_GET_YXPOS
	SLL	5,A3
	ADD	A3,A1
	MOVE	*A1,A1,L			;[Y,X] POSITION

	MOVI	TEXTZVAL-100,A2
	CALLA	SET_SCRNOBJ
	CALLA	INSERT_OBJ

;	MOVE	A1,A9
;	ADDI	[34,0],A9
;
;	MOVI	TOPPLAYER_INIT,B0
;	CALLA	EASYMAKE
;	JRZ	MS_ABORT
;
;	MOVE	A9,A1
;	MOVI	TEXTZVAL,A2
;	CALLA	SET_SCRNOBJ
;	CALLA	INSERT_OBJ

MS_ABORT
	MMFM	SP,A2,A3,A4,A8
	RETS

STARS_XOFF	EQU	54
STARS_YOFF	EQU	47

STARS_YX_TABLE
	.LONG	[P1_STAT_Y+STARS_YOFF,P1_STAT_X+STARS_XOFF]
	.LONG	[P4_STAT_Y+STARS_YOFF,P2_STAT_X+STARS_XOFF]
	.LONG	[P1_STAT_Y+STARS_YOFF,P3_STAT_X+STARS_XOFF]
	.LONG	[P4_STAT_Y+STARS_YOFF,P4_STAT_X+STARS_XOFF]

STARS_YX_2P_TABLE
	.LONG	[P1_STAT_Y+STARS_YOFF,P1_STAT_X+STARS_XOFF]
	.LONG	[P4_STAT_Y+STARS_YOFF,P2_STAT_X+STARS_XOFF]
	.LONG	[P4_STAT_Y+STARS_YOFF,P4_STAT_X+STARS_XOFF]
	.LONG	[P1_STAT_Y+STARS_YOFF,P3_STAT_X+STARS_XOFF]

STARS_INIT
	.long	STAR301
	.word	DMAWNZ,M_SCRNOBJ|M_NOVEL|M_NOSCALE
	.word	OM_OID|OM_ANIM
	.word	OID_JUNK
	.LONG	STARS_ANIM

;TOPPLAYER_INIT
;	.long	TOPPLAYER
;	.word	DMAWNZ,M_SCRNOBJ|M_NOVEL|M_NOSCALE
;	.word	OM_OID
;	.word	OID_JUNK

STARS_ANIM
	LW	STAR301,5
	LW	STAR302,5
	LW	STAR303,5
	LW	STAR304,5
	LW	STAR305,5
	LW	STAR306,5
	.long	0


**************************************************************************
*								         *
* FLASH_POINTS_STAT_PROC						 *
*								         *
**************************************************************************

FLASH_POINTS_STAT_PROC
	MOVK	2,A8
FPSP_LUPE
	MOVE	A9,A14
	INC	A14
	SLEEPR	A14

	MMTM	A12,A8,A10,A11
	MOVE	A10,A0
	MOVE	A11,A1
	CALLR	HILITE_LEADERS_STAT
	MMFM	A12,A8,A10,A11

	DSJ	A8,FPSP_LUPE

	DIE

**************************************************************************
*								         *
* FIND_STAT_LEADERS							 *
*								         *
* PASS:									 *
* A0 = STAT OFFSET							 *
* RETURN:								 *
* A1 = LEADERS								 *
*								         *
**************************************************************************

FIND_STAT_LEADERS
	PUSH	A2
	MOVI	P1_STATS,A2
	ADD	A0,A2
	MOVB	*A2,A3				;DEFAULT HIGH

	MOVK	3,A4
FSLS_FIND_HIGH_LUPE
	ADDI	8*8,A2
	MOVB	*A2,A14
	CMP	A3,A14
	JRLE	FSLS_FHL_NEXT			;BR=NOT GREATER
	MOVE	A14,A3				;NEW HIGH
FSLS_FHL_NEXT
	DSJS	A4,FSLS_FIND_HIGH_LUPE

	CLR	A1
	MOVE	A3,A3
	JRZ	FSLS_DONE			;BR=NO LEADERS

	MOVI	P1_STATS,A2
	ADD	A0,A2
	MOVK	1,A5
	MOVK	4,A4
FSLS_FIND_LEADERS_LUPE
	MOVB	*A2,A14
	CMP	A3,A14
	JRLT	FSLS_FLL_NEXT			;BR=NOT HIGH
	OR	A5,A1				;SET LEADER
FSLS_FLL_NEXT
	ADDI	8*8,A2
	SLL	1,A5
	DSJS	A4,FSLS_FIND_LEADERS_LUPE
FSLS_DONE
	PULLQ	A2
	RETS

**************************************************************************
*								         *
* HILITE_LEADERS_STAT							 *
*								         *
* PASS:									 *
* A0 = STAT OFFSET							 *
* A1 = LEADERS								 *
* A9 = SLEEP TIME							 *
*								         *
**************************************************************************

HILITE_LEADERS_STAT
	MMTM	SP,A0,A1

	MOVE	A1,A2
	JRZ	HLS_DONE			;BR=NO LEADERS

	CLR	A14
	MOVE	A14,@HILITE_SOUND_FLAG,W	;RESET FLAG TO ENABLE SOUND

	MOVE	A0,A8
	SLL	2,A8
	ADDI	SUPPLSTS-OSLINK+9*020H,A8
	JRUC	HLS_NEXT
HLS_LUPE
	MOVE	*A8(OID),A10,W
	ANDI	07H,A10
	DEC	A10
	BTST	A10,A2
	JRZ	HLS_NEXT			;BR=NOT LEADIN'

	CREATE	PID_IND,HILITE_PROC

HLS_NEXT
	MOVE	*A8(OSLINK),A8,L
	JRNZ	HLS_LUPE			;BR=LIST NOT DONE

HLS_DONE
	MMFM	SP,A0,A1
	RETS


**************************************************************************
*								         *
* HILITE_PROC								 *
* 									 *
* PASS:									 *
* A9  = SLEEP TIME							 *
* A10 = PLAYER NUMBER - 1						 *
*								         *
**************************************************************************

HILITE_PROC
	DEC	A9

	MOVE	*A8(OID),A14,W
	MOVE	A14,A11
	SRL	11,A11
	SUBK	9,A11

;	MOVI	INVERSE_ORANGE_TEXT,A5
;	CMPI	15-9,A11
;	JRNE	HP_INVERT_PAL			;BR=NOT POINTS STAT
;	MOVI	INVERSE_RED_TEXT,A5
;HP_INVERT_PAL
	MOVI	INVERSE_WHITE_TEXT,A5
	MOVE	A5,A0
	CALLA	CHANGE_PAL

	BTST	3,A14
	JRNZ	HP_NAP				;BR=NOT STAT TITLE

	PUSHP	A8
	MOVI	HILITE_BAR_INIT,B0
	CALLA	EASYMAKE

	MOVE	@HILITE_SOUND_FLAG,A14,W
	JRNZ	HP_SET_POS_TABLES		;BR=SOUND ALREADY CALLED

;	MOVI	S_CURSOR_SELECT,A4
	MOVI	S_STAT_HILITE,A4
	CALLA	do_sound

	MOVK	1,A14
	MOVE	A14,@HILITE_SOUND_FLAG,W		;FLAG SOUND FOR THIS STAT

HP_SET_POS_TABLES
	MOVI	HILITE_X_TABLE,A3
	MOVI	HILITE_Y_TABLE,A4
	KITN	4
	JREQ	HP_GET_BAR_POS			;BR=4 PLAYER KIT
	MOVI	HILITE_X_2P_TABLE,A3
	MOVI	HILITE_Y_2P_TABLE,A4
HP_GET_BAR_POS
	SLL	4,A10
	ADD	A10,A3
	MOVE	*A3,A1,W			;X POSITION
	ADD	A10,A4
	MOVE	*A4,A14,W			;Y POSITION
	SLL	16,A14
	MOVY	A14,A1

	MOVK	12,A3				;COMPUTE Y STAT OFFSET
	MPYU	A11,A3
	SLL	16,A3
	ADDXY	A3,A1				;OFFSET Y

	MOVI	-3,A2
	CALLA	SET_SCRNOBJ

	MOVE	A5,A0
	CALLA	CHANGE_PAL

	MOVI	0101H,A0
	MOVE	A0,*A8(OCONST),W

	CALLA	INSERT_OBJ
	SLEEPR	A9
	CALLA	DELETE_OBJ
	PULLPQ	A8
	JRUC	HP_PICK_PAL

HP_NAP
	SLEEPR	A9
HP_PICK_PAL
	MOVI	ORGTXT1P,A0

	SUBK	15-9,A11
	JRNZ	HP_PAL_SET			;BR=NOT POINTS STAT

	MOVI	REDTXT3P,A0
HP_PAL_SET
	CALLA	CHANGE_PAL

	DIE

HILITE_BAR_INIT
	.long	STAT_BAR
;	.word	DMAWNZ,M_SCRNOBJ|M_NOVEL|M_NOSCALE
	.word	DMACAL,M_SCRNOBJ|M_NOVEL|M_NOSCALE
	.word	OM_OID
	.word	OID_JUNK

STAT_BAR
	.word   01000H,78,12
	.long   00H
	.word   -92,-5
;	.long   INVERSE_ORANGE_TEXT
	.LONG	INVERSE_WHITE_TEXT

HILITE_X_TABLE
	.WORD	P1_STAT_X,P2_STAT_X,P3_STAT_X,P4_STAT_X

HILITE_X_2P_TABLE
	.WORD	P1_STAT_X,P2_STAT_X,P4_STAT_X,P3_STAT_X

HILITE_Y_TABLE
	.WORD	P1_STAT_Y,P4_STAT_Y,P1_STAT_Y,P4_STAT_Y

HILITE_Y_2P_TABLE
	.WORD	P1_STAT_Y,P4_STAT_Y,P4_STAT_Y,P1_STAT_Y

INVERSE_WHITE_TEXT
   .word   12 
   .word   07c1fh,07fffh,00000h,00000h,00000h,00000h,00000h,00000h
   .word   00000h,00000h,00000h,00000h
	  
;INVERSE_ORANGE_TEXT
;  .word   12 
;  .word   07c1fh,31 << 10 + 10 << 5 + 2,00000h,00000h,00000h,00000h,00000h,00000h
;  .word   00000h,00000h,00000h,00000h
;
;INVERSE_RED_TEXT
;   .word   12 
;   .word   07c1fh,31 << 10,00000h,00000h,00000h,00000h,00000h,00000h
;   .word   00000h,00000h,00000h,00000h


**************************************************************************
*								         *
* DO_PLAYER_STAT - DISPLAY PLAYER STATISTICS				 *
* 									 *
* PASS:									 *
* A9 = PLAYER BLOCK ADDRESS						 *
* B0 = MULTIMAKE SCRIPT							 *
* RETURN:								 *
* NUTIN'								 *
*								         *
**************************************************************************

DO_PLAYER_STAT
	CALLA	MULTIMAKE
	MOVI	POSITP,A0
	CALLA	CHANGE_PAL

	MOVE	*A8(OPARTS),A8,L
	MOVE	*A9(POF_CHARACTER),A2,W
	SLL	5,A2
	ADDI	player_data,A2
	MOVE	*A2,A2,L
	MOVE	*A2(TP_PLYRMUG_IMG),A1,L
;	MOVE	A1,A4
	SRL	4,A1
	SLL	4,A1
	MOVI	DMAWNZ,A4
;	SLL	28,A4
;	SRL	24,A4
;	ORI	DMAWNZ,A4
	CALLA	ANI
	MOVE	*A1(ICMAP),A0,L
	CALLA	CHANGE_PAL

	MOVE	*A8(OPARTS),A8,L
	MOVE	*A2(TP_PLYRNAME_IMG),A1,L
	MOVI	DMAWNZ,A4
	CALLA	ANI

	MOVE	A9,A6
	MOVB	*A6(POF_NUMBER),A14
	MOVE	A14,B0
	CMPI	5,B0
	JRLT	DSP_GET_FIRST_STAT_OBJ		;BR=PLAYER 1 OR 2
	SUBK	2,B0				;FIX PLAYER 3 OR 4
DSP_GET_FIRST_STAT_OBJ
	MOVE	*A8(OPARTS),A8,L		;OBJECT WITH UPPER LEFT POSITION

	MOVE	*A8(ODAG),A9,L
	MOVI	[6,167],A14
	ADDXY	A14,A9
	MOVI	POF_SHOTS_P1,A0
	MOVI	OID_STAT_1|8,B1
	OR	B0,B1
	CALLR	TOTAL_STAT
	JSRP	PRINT_STAT
	MOVB	A1,*A11
	ADDK	8,A11

	MOVI	[12,0],A10
	ADDXY	A10,A9
	MOVI	POF_CHECKS_P1,A0
	MOVI	OID_STAT_2|8,B1
	OR	B0,B1
	CALLR	TOTAL_STAT
	JSRP	PRINT_STAT
	MOVB	A1,*A11
	ADDK	8,A11

	ADDXY	A10,A9
	MOVI	POF_STEALS_P1,A0
	MOVI	OID_STAT_3|8,B1
	OR	B0,B1
	CALLR	TOTAL_STAT
	JSRP	PRINT_STAT
	MOVB	A1,*A11
	ADDK	8,A11

	ADDXY	A10,A9
	MOVI	POF_PASSES_P1,A0
	MOVI	OID_STAT_4|8,B1
	OR	B0,B1
	CALLR	TOTAL_STAT
	JSRP	PRINT_STAT
	MOVB	A1,*A11
	ADDK	8,A11

	ADDXY	A10,A9
	MOVI	POF_GOALS_P1,A0
	MOVI	OID_STAT_5|8,B1
	OR	B0,B1
	CALLR	TOTAL_STAT
	JSRP	PRINT_STAT
	MOVB	A1,*A11
	ADDK	8,A11
	MOVE	A1,A2

	ADDXY	A10,A9
	MOVI	POF_ASSISTS_P1,A0
	MOVI	OID_STAT_6|8,B1
	OR	B0,B1
	CALLR	TOTAL_STAT
	JSRP	PRINT_STAT
	MOVB	A1,*A11
	ADDK	8,A11

	ADDXY	A10,A9
	ADD	A2,A1
	MOVI	OID_STAT_7|8,B1
	OR	B0,B1
	JSRP	PRINT_STAT			;POINTS = GOAL + ASSISTS
	MOVB	A1,*A11

	RETS

**************************************************************************
*								         *
* TOTAL_STAT - TOTAL A GIVEN PLAYER STATISTIC				 *
* 									 *
* PASS:									 *
* A0 = STATISTIC OFFSET INTO PLAYER BLOCK				 *
* A6 = PLAYER BLOCK ADDRESS						 *
* RETURN:								 *
* A1 = TOTAL								 *
*								         *
**************************************************************************

TOTAL_STAT
	MMTM	SP,A2,A3,A6
	ADD	A0,A6
	CLR	A1				;RESET TOTAL
	CLR	A2				;PERIOD COUNT
	MOVE	@period,A3,W
TS_LUPE
	MOVB	*A6,A14
	ADD	A14,A1
	ADDK	8,A6
	INC	A2
	CMP	A3,A2
	JRNE	TS_LUPE				;BR=STILL MORE PERIODS TO TOTAL
	MMFM	SP,A2,A3,A6
	RETS

**************************************************************************
*								         *
* PRINT_STAT - PRINT SINGLE STATISTIC					 *
* 									 *
* PASS:									 *
* A1 = VALUE								 *
* A9 = [Y, X] POSITION							 *
* 									 *
* RETURN:								 *
* NUTIN'								 *
*								         *
**************************************************************************

PRINT_STAT
	MMTM	SP,A0,A1,A2,A5,A6,A7,A8,A9,A10,A11
	MMTM	SP,B0,B1

	MOVE	A1,@RAMTEMP1,W

	CLR	A0				;NO SLEEP TIL
	MOVI	STRRNCONV,A1			;ROUTINE
	MOVI	HFPAL,A5			;[COLOR,PALETTE]
	MOVI	STAT_VALUE_STRING,A8			;POINTER TO STRING
	MOVI	[0,-1],A10			;[Y,X] SPACING
	MOVI	H10FONT,A11			;FONT
	JSRP	PRINTF

	MMFM	SP,B0,B1
	MMFM	SP,A0,A1,A2,A5,A6,A7,A8,A9,A10,A11
	RETP

INTER_HEADER_TABLE
	.long	END_OF_FIRST_STRING,END_OF_SECOND_STRING
	.long	END_OF_THIRD_STRING

END_OF_FIRST_STRING
	.STRING	"END OF 1\dST\d PERIOD",0
;	.long	H10FONT
	.long	H11WFONT
	.long	H18TFONT

END_OF_SECOND_STRING
	.STRING	"END OF 2\dND\d PERIOD",0
;	.long	H10FONT
	.long	H11WFONT
	.long	H18TFONT

END_OF_THIRD_STRING
	.STRING	"END OF 3\dRD\d PERIOD",0
;	.long	H10FONT
	.long	H11WFONT
	.long	H18TFONT

END_OF_GAME_STRING
	.STRING	"END OF GAME",0

STAT_VALUE_STRING
	.STRING	"%u",0
	.LONG	RAMTEMP1

PLAYER_1_STAT_INIT
	.byte	15, 0, 0, 9	;OBJCNT, PRCCNT, IMGPALCNT, PALCNT
	.long	GNUMS		;IMGPAL
	.long	CHELIOS1P	;IMGPAL
	.long	LSTNME1P	;IMGPAL
	.long	FRAMEA04P	;IMGPAL
	.long	PLAYERP		;IMGPAL
	.long	FLASHA01P	;IMGPAL
	.long	BLUBARA2P	;IMGPAL
	.long	GFONT10CP	;IMGPAL
	.long	BLUFRMP		;IMGPAL
	.word	OID_JUNK, OM_POS|OM_INSERT	;OID, GLOBAL_OPTIONS
	.long	[P1_STAT_X,0],[P1_STAT_Y,0],0
*        .long IMG
*        .word OCTRL, OFLAGS, OZOFF
*        .word OPTIONS

	LWWWW	G01, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -1, OM_OPARTS
	.long	[5,4]
	LWWWW	M_CHELIOS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, 1, OM_OPARTS
	.long	[78,53]
	LWWWW	CHELIOS1, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -1, OM_OPARTS
	.long	[8,53]
	LWWWW	STATCARD1, DMAWNZ, M_SCRNOBJ|M_NOSCALE, 0, 0
	LWWWW	PPLAYER, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -1, OM_OPARTS
	.long	[25,4]
	LWWWW	COMPA00, DMAWNZ, M_SCRNOBJ|M_NOSCALE, 2, OM_OID
	.WORD	OID_PLAYER_1
	LWWWW	BLUBAR1, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -1, 0
	LWWWW	SHOTS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_1|1
	LWWWW	CHECKS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_2|1	
	LWWWW	STEALS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_3|1	
	LWWWW	PASSES, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_4|1	
	LWWWW	GOALS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_5|1	
	LWWWW	ASSISTS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_6|1	
	LWWWW	POINTS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_7|1	
	LWWWW	STATSLH1, DMAWNZ, M_SCRNOBJ|M_NOSCALE, 3, OM_OPARTS|OM_CFUNC
	.long	[94,60]
	.LONG	C_CHANGE_PAL
	.LONG	BLUFRMP

PLAYER_2_STAT_INIT
	.byte	15, 0, 0, 9	;OBJCNT, PRCCNT, IMGPALCNT, PALCNT
	.long	GNUMS		;IMGPAL
	.long	CHELIOS1P	;IMGPAL
	.long	LSTNME1P	;IMGPAL
	.long	FRAMEA04P	;IMGPAL
	.long	PLAYERP		;IMGPAL
	.long	FLASHA01P	;IMGPAL
	.long	BLUBARA2P	;IMGPAL
	.long	GFONT10CP	;IMGPAL
	.long	BLUFRMP		;IMGPAL
	.word	OID_JUNK, OM_POS|OM_INSERT	;OID, GLOBAL_OPTIONS
	.long	[P2_STAT_X,0],[P4_STAT_Y,0],0
*        .long IMG
*        .word OCTRL, OFLAGS, OZOFF
*        .word OPTIONS

	LWWWW	G02, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -1, OM_OPARTS|OM_CFUNC
	.long	[5,4]
	.LONG	C_CHECK_STAT_2P
	LWWWW	M_CHELIOS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, 1, OM_OPARTS
	.long	[78,53]
	LWWWW	CHELIOS1, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -1, OM_OPARTS
	.long	[8,53]
	LWWWW	STATCARD1, DMAWNZ, M_SCRNOBJ|M_NOSCALE, 0, 0
	LWWWW	PPLAYER, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -1, OM_OPARTS|OM_CFUNC
	.long	[25,4]
	.LONG	C_CHECK_STAT_2P
	LWWWW	COMPA00, DMAWNZ, M_SCRNOBJ|M_NOSCALE, 2, OM_OID
	.WORD	OID_PLAYER_2
	LWWWW	BLUBAR1, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -1, 0
	LWWWW	SHOTS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_1|2
	LWWWW	CHECKS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_2|2	
	LWWWW	STEALS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_3|2	
	LWWWW	PASSES, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_4|2	
	LWWWW	GOALS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_5|2	
	LWWWW	ASSISTS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_6|2
	LWWWW	POINTS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_7|2
	LWWWW	STATSLH1, DMAWNZ, M_SCRNOBJ|M_NOSCALE, 3, OM_OPARTS|OM_CFUNC
	.long	[94,82]
	.LONG	C_CHANGE_PAL
	.LONG	BLUFRMP

PLAYER_3_STAT_INIT
	.byte	15, 0, 0, 9	;OBJCNT, PRCCNT, IMGPALCNT, PALCNT
	.long	GNUMS		;IMGPAL
	.long	CHELIOS1P	;IMGPAL
	.long	LSTNME1P	;IMGPAL
	.long	FRAMEA04P	;IMGPAL
	.long	PLAYERP		;IMGPAL
	.long	FLASHA01P	;IMGPAL
	.long	REDBAR2P	;IMGPAL
	.long	GFONT10CP	;IMGPAL
	.long	REDFRMP		;IMGPAL
	.word	OID_JUNK, OM_POS|OM_INSERT	;OID, GLOBAL_OPTIONS
	.long	[P3_STAT_X,0],[P1_STAT_Y,0],0
*        .long IMG
*        .word OCTRL, OFLAGS, OZOFF
*        .word OPTIONS

	LWWWW	G03, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -1, OM_OPARTS
	.long	[5,4]
	LWWWW	M_CHELIOS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, 1, OM_OPARTS
	.long	[78,53]
	LWWWW	CHELIOS1, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -1, OM_OPARTS
	.long	[8,53]
	LWWWW	STATCARD1, DMAWNZ, M_SCRNOBJ|M_NOSCALE, 0, 0
	LWWWW	PPLAYER, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -1, OM_OPARTS
	.long	[25,4]
	LWWWW	COMPA00, DMAWNZ, M_SCRNOBJ|M_NOSCALE, 2, OM_OID
	.WORD	OID_PLAYER_3
	LWWWW	REDBAR1, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -1, 0
	LWWWW	SHOTS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_1|3
	LWWWW	CHECKS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_2|3	
	LWWWW	STEALS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_3|3	
	LWWWW	PASSES, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_4|3	
	LWWWW	GOALS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_5|3	
	LWWWW	ASSISTS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_6|3	
	LWWWW	POINTS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_7|3	
	LWWWW	STATSLH1, DMAWNZ, M_SCRNOBJ|M_NOSCALE, 3, OM_OPARTS|OM_CFUNC
	.long	[94,60]
	.LONG	C_CHANGE_PAL
	.LONG	REDFRMP

PLAYER_3_2P_STAT_INIT
	.byte	15, 0, 0, 9	;OBJCNT, PRCCNT, IMGPALCNT, PALCNT
	.long	GNUMS		;IMGPAL
	.long	CHELIOS1P	;IMGPAL
	.long	LSTNME1P	;IMGPAL
	.long	FRAMEA04P	;IMGPAL
	.long	PLAYERP		;IMGPAL
	.long	FLASHA01P	;IMGPAL
	.long	REDBAR2P	;IMGPAL
	.long	GFONT10CP	;IMGPAL
	.long	REDFRMP		;IMGPAL
	.word	OID_JUNK, OM_POS|OM_INSERT	;OID, GLOBAL_OPTIONS
	.long	[P4_STAT_X,0],[P4_STAT_Y,0],0
*        .long IMG
*        .word OCTRL, OFLAGS, OZOFF
*        .word OPTIONS

	LWWWW	G03, DMAGO, M_SCRNOBJ|M_NOSCALE, -1, OM_OPARTS
	.long	[5,4]
	LWWWW	M_CHELIOS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, 1, OM_OPARTS
	.long	[78,53]
	LWWWW	CHELIOS1, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -1, OM_OPARTS
	.long	[8,53]
	LWWWW	STATCARD1, DMAWNZ, M_SCRNOBJ|M_NOSCALE, 0, 0
	LWWWW	PPLAYER, DMAGO, M_SCRNOBJ|M_NOSCALE, -1, OM_OPARTS
	.long	[25,4]
	LWWWW	COMPA00, DMAWNZ, M_SCRNOBJ|M_NOSCALE, 2, OM_OID
	.WORD	OID_PLAYER_3
	LWWWW	REDBAR1, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -1, 0
	LWWWW	SHOTS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_1|3
	LWWWW	CHECKS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_2|3	
	LWWWW	STEALS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_3|3	
	LWWWW	PASSES, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_4|3	
	LWWWW	GOALS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_5|3	
	LWWWW	ASSISTS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_6|3	
	LWWWW	POINTS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_7|3	
	LWWWW	STATSLH1, DMAWNZ, M_SCRNOBJ|M_NOSCALE, 3, OM_OPARTS|OM_CFUNC
	.long	[94,82]
	.LONG	C_CHANGE_PAL
	.LONG	REDFRMP


PLAYER_4_STAT_INIT
	.byte	15, 0, 0, 9	;OBJCNT, PRCCNT, IMGPALCNT, PALCNT
	.long	GNUMS		;IMGPAL
	.long	CHELIOS1P	;IMGPAL
	.long	LSTNME1P	;IMGPAL
	.long	FRAMEA04P	;IMGPAL
	.long	PLAYERP		;IMGPAL
	.long	FLASHA01P	;IMGPAL
	.long	REDBAR2P	;IMGPAL
	.long	GFONT10CP	;IMGPAL
	.long	REDFRMP		;IMGPAL
	.word	OID_JUNK, OM_POS|OM_INSERT	;OID, GLOBAL_OPTIONS
	.long	[P4_STAT_X,0],[P4_STAT_Y,0],0
*        .long IMG
*        .word OCTRL, OFLAGS, OZOFF
*        .word OPTIONS

	LWWWW	G04, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -1, OM_OPARTS
	.long	[5,4]
	LWWWW	M_CHELIOS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, 1, OM_OPARTS
	.long	[78,53]
	LWWWW	CHELIOS1, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -1, OM_OPARTS
	.long	[8,53]
	LWWWW	STATCARD1, DMAWNZ, M_SCRNOBJ|M_NOSCALE, 0, 0
	LWWWW	PPLAYER, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -1, OM_OPARTS
	.long	[25,4]
	LWWWW	COMPA00, DMAWNZ, M_SCRNOBJ|M_NOSCALE, 2, OM_OID
	.WORD	OID_PLAYER_4
	LWWWW	REDBAR1, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -1, 0
	LWWWW	SHOTS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_1|4
	LWWWW	CHECKS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_2|4	
	LWWWW	STEALS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_3|4	
	LWWWW	PASSES, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_4|4	
	LWWWW	GOALS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_5|4	
	LWWWW	ASSISTS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_6|4	
	LWWWW	POINTS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_7|4	
	LWWWW	STATSLH1, DMAWNZ, M_SCRNOBJ|M_NOSCALE, 3, OM_OPARTS|OM_CFUNC
	.long	[94,82]
	.LONG	C_CHANGE_PAL
	.LONG	REDFRMP

PLAYER_4_2P_STAT_INIT
	.byte	15, 0, 0, 9	;OBJCNT, PRCCNT, IMGPALCNT, PALCNT
	.long	GNUMS		;IMGPAL
	.long	CHELIOS1P	;IMGPAL
	.long	LSTNME1P	;IMGPAL
	.long	FRAMEA04P	;IMGPAL
	.long	PLAYERP		;IMGPAL
	.long	FLASHA01P	;IMGPAL
	.long	REDBAR2P	;IMGPAL
	.long	GFONT10CP	;IMGPAL
	.long	REDFRMP		;IMGPAL
	.word	OID_JUNK, OM_POS|OM_INSERT	;OID, GLOBAL_OPTIONS
	.long	[P3_STAT_X,0],[P1_STAT_Y,0],0
*        .long IMG
*        .word OCTRL, OFLAGS, OZOFF
*        .word OPTIONS

	LWWWW	G02, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -1, OM_OPARTS
	.long	[5,4]
	LWWWW	M_CHELIOS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, 1, OM_OPARTS
	.long	[78,53]
	LWWWW	CHELIOS1, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -1, OM_OPARTS
	.long	[8,53]
	LWWWW	STATCARD1, DMAWNZ, M_SCRNOBJ|M_NOSCALE, 0, 0
	LWWWW	PPLAYER, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -1, OM_OPARTS
	.long	[25,4]
	LWWWW	COMPA00, DMAWNZ, M_SCRNOBJ|M_NOSCALE, 2, OM_OID
	.WORD	OID_PLAYER_4
	LWWWW	REDBAR1, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -1, 0
	LWWWW	SHOTS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_1|4
	LWWWW	CHECKS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_2|4	
	LWWWW	STEALS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_3|4	
	LWWWW	PASSES, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_4|4	
	LWWWW	GOALS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_5|4	
	LWWWW	ASSISTS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_6|4	
	LWWWW	POINTS, DMAWNZ, M_SCRNOBJ|M_NOSCALE, -4, OM_OID
	.WORD	OID_STAT_7|4	
	LWWWW	STATSLH1, DMAWNZ, M_SCRNOBJ|M_NOSCALE, 3, OM_OPARTS|OM_CFUNC
	.long	[94,60]
	.LONG	C_CHANGE_PAL
	.LONG	REDFRMP

**************************************************************************
*								         *
* A_RAND_CROWD_FLASH							 *
*								         *
**************************************************************************

A_RAND_CROWD_FLASH
	CALLA	GETAFARG_LONG
	MOVE	A0,A1
	MOVE	*A1+,A0,W
	CALLA	RAND0
	SLL	5,A0
	ADD	A1,A0
	MOVE	*A0,A0,L
	MOVE	A0,*A8(ANIMFRM),L
	RETS


CROWD_FLASH_ALL_TABLE
	.WORD	7
	.LONG	CF_A_ANIM,CF_B_ANIM,CF_C_ANIM
	.LONG	CF_D_ANIM,CF_E_ANIM
	.LONG	CF_F_ANIM,CF_NONE_ANIM

CROWD_FLASH_1_TABLE
	.WORD	4
	.LONG	CF_A_ANIM,CF_B_ANIM,CF_C_ANIM
	.LONG	CF_NONE_ANIM

CROWD_FLASH_2_TABLE
	.WORD	4
	.LONG	CF_D_ANIM,CF_E_ANIM,CF_F_ANIM
	.LONG	CF_NONE_ANIM

CROWD_FLASH_ANIM
CF_NONE_ANIM
	LWLL	COMPA00,6|AFUNC,A_RAND_CROWD_FLASH+2,CROWD_FLASH_ALL_TABLE

CF_A_ANIM
	LW	COMPA01,2
	LW	COMPA02,2
	LWLL	COMPA03,2|AFUNC,A_RAND_CROWD_FLASH+2,CROWD_FLASH_2_TABLE

CF_B_ANIM
	LW	COMPB01,2
	LW	COMPB02,2
	LWLL	COMPB03,2|AFUNC,A_RAND_CROWD_FLASH+2,CROWD_FLASH_2_TABLE

CF_C_ANIM
	LW	COMPC01,2
	LW	COMPC02,2
	LWLL	COMPC03,2|AFUNC,A_RAND_CROWD_FLASH+2,CROWD_FLASH_2_TABLE

CF_D_ANIM
	LW	COMPD01,2
	LW	COMPD02,2
	LWLL	COMPD03,2|AFUNC,A_RAND_CROWD_FLASH+2,CROWD_FLASH_1_TABLE

CF_E_ANIM
	LW	COMPE01,2
	LW	COMPE02,2
	LWLL	COMPE03,2|AFUNC,A_RAND_CROWD_FLASH+2,CROWD_FLASH_1_TABLE

CF_F_ANIM
	LW	COMPF01,2
	LW	COMPF02,2
	LWLL	COMPF03,2|AFUNC,A_RAND_CROWD_FLASH+2,CROWD_FLASH_1_TABLE


	.end
