 	.MLIB	"HHMACS.LIB"
	.FILE	"HHGSCRF.ASM"
	.WIDTH	132
	.OPTION	B,D,L,T
	.MNOLIST

*
*GET THE SYSTEM STUFF
*
	.INCLUDE	"HH.INC"
	.include	"hhgscrf.e"
	.include	"hhd.g"
	.include	"hhpuck.g"
	.include	"hhplayer.g"
	.include	"hhcontrl.g"
	.include	"hhc.g"
	.include	"hhshow.g"
	.include	"HHMATH.G"
	.include	"hhsnd.g"
	.include	"hhutil.g"
	.include	"hhram.g"
	.include	"hhmisc.g"
	.include	"hhhigher.g"
	.include	"hhgdrone.g"
	.include	"hhafunc.g"
	.include	"hhgame.g"
	.include	"hhgscr.g"
	.include	"hhgscr2.g"
	.include	"hhspeech.g"
	.include	"hhgctrl.g"
	.include	"hhscr_sh.g"
	.include	"hhcmos.g"
	.include	"hhscrsh2.g"
	.include	"hhproc.g"

; end of include files

	.TEXT
	.EVEN

**************************************************************************
*								         *
* A_CHECK_RSMOTH - IF REVERSE SMOTHER, ANIMATE TO REVERSE IMAGE		 *
* 									 *
* PASS:									 *
* A8 = PLAYER OBJECT							 *
* AARG+,L = REVERSE IMAGE						 *
*								         *
**************************************************************************

A_CHECK_RSMOTH
	MOVE	*A8(ODT_GFLAGS),A14,W
	BTST	B_RSMOTH,A14
	JRZ	ACR_DONE			;BR=NOT REVERSE SMOTHER

	CALLA	GETAFARG_LONG			;GET REVERSE IMAGE
	MOVE	A0,A1
	MOVE	*A8(OCTRL),A4,W
	CALLA	ANI
ACR_DONE
	RETS	

**************************************************************************
*								         *
* A_CHECK_ABORT_KO							 *
* 									 *
* PASS:									 *
* A8 = PLAYER OBJECT							 *
*								         *
**************************************************************************

A_CHECK_ABORT_KO
	MOVE	*A8(ODT_GFLAGS),A14,W
	BTST	B_SAVEMADE,A14
	JRNZ	ACAKO_DONE			;BR=OKAY, ALREADY SAVED

	MOVE	@PUCK_CONTROL,A14,W
	JRNZ	A_GSCRIPT_DONE			;BR=ABORT, SOMEBODY GOT DA PUCK

	MOVE	*A8(ODT_GCOMPTIME),A0,L
	MOVE	@PUCK_NEWDIR_TIME,A1,L
	CMP	A0,A1
	JRGT	A_GSCRIPT_DONE			;BR=ABORT, NEW DIRECTION

	MOVE	@PUCK_MODE,A14,W
	CMPI	PUM_SCORE,A14
	JREQ	A_GSCRIPT_DONE			;BR=ABORT, SCORE!
ACAKO_DONE
	RETS		

**************************************************************************
*								         *
* A_SET_KO								 *
* 									 *
* PASS:									 *
* A8 = PLAYER OBJECT							 *
*								         *
**************************************************************************

A_SET_KO
	MOVI	s_puck_to_goalie_mask_1,A4
	CALLA	do_sound

	MOVI	-038000H,A1

	MOVE	*A8(ODT_GFLAGS),A14,W
	BTST	B_MISSSAVE,A14
	JRZ	ASKO_CHECK_SIDE

	MOVI	511,A0
	CALLA	RANDPER
	JRC	ASKO_CHECK_SIDE
	MOVI	-060000H,A1

ASKO_CHECK_SIDE
	MOVE	*A8(OXVAL),A14,L
	JRN	ASKO_SET			;BR=ON THE LEFT
	NEG	A1			
ASKO_SET
	MOVE	A1,*A8(OXVEL),L
	JRUC	A_CHECK_KO_X_LIMIT	

**************************************************************************
*								         *
* A_CHECK_KO_X_LIMIT							 *
* 									 *
* PASS:									 *
* A8 = PLAYER OBJECT							 *
*								         *
**************************************************************************

A_CHECK_KO_X_LIMIT
	MOVE	*A8(OXVEL),A0,L
	ABS	A0
	MOVE	*A8(OXVAL),A1,L
	ABS	A1
	ADD	A0,A1
	CMPI	029E0000H,A1
	JRGE	ACKOS_STOP			;BR=X LIMIT EXCEEDED ON NEXT
	RETS

**************************************************************************
*								         *
* A_CHECK_KO_SLIDE								 *
* 									 *
* PASS:									 *
* A8 = PLAYER OBJECT							 *
*								         *
**************************************************************************

A_CHECK_KO_SLIDE
	MOVE	@PUCK_MODE,A4,W
	CMPI	PUM_FACEOFF,A4
	JREQ	ACKOS_STOP			;BR=ABORT, FACEOFF!

	MOVE	*A8(OXVEL),A0,L
	GETST	A3

	MOVB	*A8(ANIMSLP),A1
	DEC	A1
	JRZ	ACKOS_STOP			;BR=NO MORE LOOPS
	MOVB	A1,*A8(ANIMSLP)

	MOVI	07FFFH,A1
	MOVE	*A8(OXVAL),A2,L
	JRN	ACKOS_CHECK_LIMIT		;BR=LEFT SIDE
	NEG	A1
ACKOS_CHECK_LIMIT
	ABS	A2
	CMPI	029E0000H,A2
	JRLT	ACKOS_SLOW
	MOVE	A0,A1
	NEG	A1
ACKOS_SLOW
	ADD	A1,A0
	GETST	A14
	XOR	A3,A14
	ANDI	0A0000000H,A14
	JRZ	ACKOS_SET			;BR=GOING OTHER DIRECTION

	MOVE	A0,A0
	JRZ	ACKOS_SET

	CMPI	PUM_SCORE,A4
	JRNE	ACKOS_CLR			;BR=NO SCORE

	MOVI	PID_GOAL_SLIDE,A0
	CALLA	EXISTP_ALL
	JRNZ	ACKOS_CLR			;BR=GOAL ALREADY SLIDIN'

	CREATE	PID_GOAL_SLIDE,GOAL_SLIDE_PROC

ACKOS_CLR
	CLR	A0
ACKOS_SET
	MOVE	A0,*A8(OXVEL),L

	MOVE	*A8(ANIMFRM),A14,L		;HOLD FRAME
	SUBI	020H+010H+020H,A14
	MOVE	A14,*A8(ANIMFRM),L
	RETS

ACKOS_STOP
	CLR	A14
	MOVE	A14,*A8(OXVEL),L
	RETS

**************************************************************************
*								         *
* GOAL_SLIDE_PROC							 *
* 									 *
* PASS:									 *
* A8 = GOALIE OBJECT							 *
*								         *
**************************************************************************

GOAL_SLIDE_PROC
	MOVE	*A8(ODT_PBK),A6,L
	MOVB	*A6(POF_NUMBER),A14
	SUBK	4,A14
	JRNZ	GSP_RIGHT

	MOVE	@lgoalF_object,A8,L
	MOVI	-020000H,A9
	MOVI	01000H,A10
	MOVI	-02AD0000H,A11
	JRUC	GSP_LUPE

GSP_RIGHT
	MOVE	@rgoalF_object,A8,L
	MOVI	020000H,A9
	MOVI	-01000H,A10
	MOVI	02AD0000H,A11

GSP_LUPE
	ADD	A10,A9
	MOVE	A9,*A8(OXVEL),L
	JRZ	GSP_WAIT			;BR=STOPPED

	SLEEP	1
	JRUC	GSP_LUPE

GSP_WAIT
	SLEEP	1
	MOVE	@WORLDX+010H,A0,W
	ABS	A0
	CMPI	0163H,A0
	JRGT	GSP_WAIT

	MOVE	A11,*A8(OXVAL),L

	DIE


**************************************************************************
*								         *
* A_CLRVELS - CLEAR X AND Z VELOCITIES ALONG WITH ODT_VEL		 *
* 									 *
* PASS:									 *
* A8 = PLAYER OBJECT							 *
*								         *
**************************************************************************

A_CLRVELS
	CLR	A14
	MOVE	A14,*A8(ODT_VEL),L
	MOVE	A14,*A8(OXVEL),L
	MOVE	A14,*A8(OZVEL),L
	RETS

**************************************************************************
*								         *
* A_CHECK_LOOKFAR  - CHECK IF GOALIE SHOULD LOOK OVER FAR SHOULDER	 *
* A_CHECK_LOOKNEAR - CHECK IF GOALIE SHOULD LOOK OVER NEAR SHOULDER	 *
* 									 *
* PASS:									 *
* A8 = GOALIE OBJECT							 *
*								         *
**************************************************************************

A_CHECK_LOOKNEAR
	MOVE	*A8(OZVAL),A1,L
	MOVE	@PUCK_OBJECT,A9,L
	MOVE	*A9(OZVAL),A0,L
	JRUC	ACL_CHECK
A_CHECK_LOOKFAR
	MOVE	*A8(OZVAL),A0,L
	MOVE	@PUCK_OBJECT,A9,L
	MOVE	*A9(OZVAL),A1,L
ACL_CHECK
	CMP	A0,A1
	JRGT	ACL_GO				;BR=DON'T EVEN LOOK!				

	SUB	A0,A1
	ABS	A1
	CMPI	0200000H,A1
	JRGT	ACL_STAY			;BR=TOO FAR

	MOVK	10,A0
	CALLA	RANDPER
	JRC	ACL_GO				;BR=LOOK OUT!

ACL_STAY
	MOVB	*A8(ANIMLOOP),A1
	DEC	A1
	JRZ	ACL_DONE			;BR=NO MORE LOOPS
	MOVB	A1,*A8(ANIMLOOP)

	MOVE	*A8(ANIMFRM),A14,L		;HOLD FRAME
	SUBI	020H+010H+020H+020H,A14
	MOVE	A14,*A8(ANIMFRM),L
ACL_DONE
	RETS

ACL_GO
	CALLA	GETAFARG_LONG			;JUMP TO TURN HEAD ANIMATION
	MOVE	A0,*A8(ANIMFRM),L
	RETS

**************************************************************************
*								         *
* A_WAIT_FACEOFF - WAIT FOR FACEOFF TO FINISH				 *
*								         *
* PASS:									 *
* A8 = GOALIE OBJECT							 *
*								         *
**************************************************************************
A_WAIT_FACEOFF
	MOVE	@PUCK_CONTROL,A14,W
	JRNZ	A_GSCRIPT_DONE			;BR=UNDER CONTROL

	MOVE	@PUCK_MODE,A14,W
	CMPI	PUM_FACEOFF,A14
	JRNE	A_GSCRIPT_DONE			;BR=FINISH ANIMATION
A_WFA_DONE
	RETS

**************************************************************************
*								         *
* A_CHECK_FACEOFF - CHECK FOR FACEOFF PUCK MODE AND FINISH ANIMATION IF  *
* 		     SET						 *
* PASS:									 *
* A8 = GOALIE OBJECT							 *
*								         *
**************************************************************************
A_CHECK_FACEOFF
	MOVE	@PUCK_CONTROL,A14,W
	JRNZ	A_CFA_DONE			;BR=STILL UNDER CONTROL

	MOVE	@PUCK_MODE,A14,W
	CMPI	PUM_FACEOFF,A14
	JREQ	A_GSCRIPT_DONE			;BR=FINISH ANIMATION
A_CFA_DONE
	RETS

**************************************************************************
*								         *
* A_CHECK_FRUST - CHECK IF FRUSTRATION ANIMATION SHOULD LOOP OR HOLD	 *
* 									 *
* PASS:									 *
* A8 = GOALIE OBJECT							 *
*								         *
**************************************************************************
A_CHECK_FRUST
	MOVE	@PUCK_CONTROL,A14,W
	JRNZ	A_CF_DONE			;BR=UNDER CONTROL

	MOVE	@PUCK_MODE,A14,W
	CMPI	PUM_SCORE,A14
	JRNE	A_CF_DONE			;BR=SCORE OVER

	MOVB	*A8(ANIMLOOP),A1
	JRZ	A_CF_DONE			;BR=DON'T LOOP
;	JRNZ	A_CF_RAND			;BR=CHECK IF WE SHOULD LOOP
;A_CF_HOLD
;	MOVE	*A8(ANIMFRM),A14,L		;HOLD ON STAND
;	SUBI	020H+010H+020H,A14
;	MOVE	A14,*A8(ANIMFRM),L
;	RETS
;A_CF_RAND
	CLR	A1				;FLAG HOLD ON NEXT FUNCTION CALL
	MOVB	A1,*A8(ANIMLOOP)

	MOVI	511,A0
	CALLA	RANDPER
;	JRNC	A_CF_HOLD			;BR=DON'T LOOP, HOLD ON STAND
	JRNC	A_CF_DONE			;BR=DON'T LOOP

	MOVE	*A8(ANIMSCR),*A8(ANIMFRM),L	;LOOP, RESTART ANIMATION
	MOVK	4,A0				;SLOWER THIS TIME
	MOVB	A0,*A8(ANIMSLP)
A_CF_DONE
	RETS

**************************************************************************
*								         *
* A_SET_SKATE_SLEEP - SET ANIMSLP WITH RESPECT TO ODT_VEL		 *
* 									 *
* PASS:									 *
* A8 = GOALIE OBJECT							 *
*								         *
**************************************************************************

A_SET_SKATE_SLEEP
	MOVK	6,A0
	MOVE	*A8(ODT_VEL),A14,L
	CMPI	011000H,A14
	JRLT	A_SSS_SET
	DEC	A0
	CMPI	020000H,A14
	JRLT	A_SSS_SET
	DEC	A0
A_SSS_SET
	MOVB	A0,*A8(ANIMSLP)
	RETS

**************************************************************************
*								         *
* A_INFRONT - PUCK SHOULD APPEAR IN FRONT OF GOALIE			 *
*								         *
* PASS:									 *
* A8 = GOALIE OBJECT							 *
*								         *
**************************************************************************
A_INBACK
	MOVK	M_INBACK,A14
	JRUC	A_INF_GO
A_INFRONT
	MOVK	M_INFRONT,A14
A_INF_GO
	MOVE	@PUCK_CONTROL,A0,W
	MOVE	*A8(ODT_PBK),A6,L
	MOVB	*A6(POF_NUMBER),A1
	CMP	A0,A1
	JRNE	A_INF_ABORT				;BR=NOT IN CONTROL

	MOVE	@PUCK_OBJECT,A9,L
	MOVE	*A9(ODT_PFLAGS),A0,W
	ANDNI	M_INBACK|M_INFRONT,A0			;CLEAR FLAGS
	OR	A14,A0					;SET FLAG
	MOVE	A0,*A9(ODT_PFLAGS),W
A_INF_ABORT
	RETS

**************************************************************************
*								         *
* A_GSMOTHER								 *
* 									 *
* PASS:									 *
* A8 = GOALIE OBJECT							 *
*								         *
**************************************************************************

A_GSMOTHER
	MOVE	*A8(ODT_PBK),A6,L

	CALLA	GETAFARG_WORD
	MOVE	A0,*A6(POF_D_COLL),W
	CALLA	GETAFARG_LONG
	MOVE	A0,*A8(ODT_FOCIOFF0),L
	CALLA	GETAFARG_LONG
	MOVE	A0,*A8(ODT_FOCIOFF1),L

	MOVE	@PUCK_CONTROL,A14,W
	JRZ	A_GS_CHECK_SCORE			;BR=NO CONTROL
	MOVB	*A6(POF_NUMBER),A0
	CMP	A0,A14	
	JREQ	A_GS_GO					;BR=CONTROLLED BY GOALIE
	JRUC	A_GS_GETUP

A_GS_CHECK_SCORE
	MOVE	@PUCK_MODE,A14,W
	CMPI	PUM_SCORE,A14
	JRGE	A_GS_GETUP				;BR=ABORT SMOTHER
A_GS_GO
	MOVI	FREEZE_TIME,A14
	MOVB	A14,*A8(ANIMLOOP)

	CALLA	GET_PUCK_CONTROL

	MOVE	@PUCK_OBJECT,A9,L
	MOVE	*A9(ODT_PFLAGS),A14,W
	ORI	M_INBACK,A14
	MOVE	A14,*A9(ODT_PFLAGS),W

	MOVK	GS_SMOTHERED,A0
	CALLA	GOALIE_SPEECH

	CLR	A14
	MOVE	A14,@whack_goalie_count,W

	RETS
A_GS_GETUP
	CALLA	GETAFARG_LONG
	MOVE	A0,*A8(ANIMFRM),L

	RETS

**************************************************************************
*								         *
* A_SETCOLL								 *
* A_SETFOCIS								 *
* 									 *
* PASS:									 *
* A8 = GOALIE OBJECT							 *
*								         *
**************************************************************************

A_SETCOLL
	CALLA	GETAFARG_WORD
	MOVE	*A8(ODT_PBK),A6,L
	MOVE	A0,*A6(POF_D_COLL),W
A_SETFOCIS
	CALLA	GETAFARG_LONG
	MOVE	A0,*A8(ODT_FOCIOFF0),L
	CALLA	GETAFARG_LONG
	MOVE	A0,*A8(ODT_FOCIOFF1),L
	RETS

**************************************************************************
*								         *
* A_CLRFOFFS								 *
* 									 *
* PASS:									 *
* A8 = GOALIE OBJECT							 *
*								         *
**************************************************************************

A_CLRFOFFS
	MOVE	*A8(ODT_PBK),A6,L
	MOVI	GSTAND_COLLRANGE,A14
	MOVE	A14,*A6(POF_D_COLL),W
	CLR	A14
	MOVE	A14,*A8(ODT_FOCIOFF0),L
	MOVE	A14,*A8(ODT_FOCIOFF1),L
	RETS

**************************************************************************
*								         *
* A_GPASS_DONE_1							 *
* 									 *
* PASS:									 *
* A8 = GOALIE OBJECT							 *
*								         *
**************************************************************************

A_GPASS_DONE_1
	CALLA	pass_done_1_safe
	JRNC	AGPD1_ABORT_PASS		;BR=NOT SUCCESSFUL

	CLR	A14
	MOVE	A14,*A8(ODT_GPOSSIRQ),L		;RESET IRQ TIME OF POSSESSION
	CALLA	clear_all_signals
	RETS
AGPD1_ABORT_PASS
	CLR	A14				;FLAG PASS FAILED
	DEC	A14
	MOVE	A14,*A8(ODT_GPOSSIRQ),L		;RESET IRQ TIME OF POSSESSION

	CALLA	clear_all_signals
	JAUC	pass_script_done_2

**************************************************************************
*								         *
* A_SPASS_DONE_1							 *
* 									 *
* PASS:									 *
* A8 = GOALIE OBJECT							 *
*								         *
**************************************************************************

A_SPASS_DONE_1
	CALLA	pass_done_1_safe
	JRNC	ASPD1_ABORT_PASS		;BR=NOT SUCCESSFUL

	CLR	A14
	MOVE	A14,*A8(ODT_GPOSSIRQ),L		;RESET IRQ TIME OF POSSESSION
	CALLA	clear_all_signals
	RETS
ASPD1_ABORT_PASS
	CALLA	clear_all_signals

	MOVK	PM_GSMOTHER,A14
	MOVE	A14,*A6(POF_MODE),W

	CALLA	GETAFARG_WORD

	MOVE	*A8(ODT_GPOSSIRQ),A14,L
	JRZ	ASPD1_AP_LOOP			;BR=SET ANIMLOOP
	CLR	A14				;FLAG PASS FAILED
	DEC	A14
	MOVE	A14,*A8(ODT_GPOSSIRQ),L		;RESET IRQ TIME OF POSSESSION
	JRUC	ASPD1_AP_JUMP

ASPD1_AP_LOOP
	MOVB	*A8(ANIMLOOP),A1
	SUB	A0,A1
	JRP	ASPD1_AP_SET			;BR=FREEZE TIME OKAY
	CLR	A1				;FLAG PASS FAILED
	DEC	A1
ASPD1_AP_SET
	MOVB	A1,*A8(ANIMLOOP)

ASPD1_AP_JUMP
	CALLA	GETAFARG_LONG
	MOVE	A0,*A8(ANIMFRM),L
	RETS

**************************************************************************
*								         *
* A_LOSE_PUCK								 *
* 									 *
* PASS:									 *
* A8 = GOALIE OBJECT							 *
*								         *
**************************************************************************

A_LOSE_PUCK
	CLR	A14
	MOVE	A14,@whack_goalie_count,W

	CLR	A14				;PUM_OPEN
	calla	set_safe_puck_mode_a14
	MOVE	@PUCK_CONTROL,@PUCK_LAST_CONTROL,W
	CLR	A14
	MOVE	A14,@PUCK_INTENDED,W
	MOVE	A14,@PUCK_CONTROL,W
	CALLA	new_puck_control

	CLR	A14
	MOVE	A14,*A8(ODT_GPOSSIRQ),L		;RESET IRQ TIME OF POSSESSION

	MOVK	30,A0
	CALLR	ALL_IGNORE_PUCK

	MOVE	*A8(ODT_PBK),A6,L

	MOVB	*A6(POF_DIRECTION),A0
	CALLA	dir_to_degrees

	MOVE	*A8(ODT_GFLAGS),A14,W
	BTST	B_RSMOTH,A14
	JRZ	ALP_CHOOSE_ANGLE		;BR=NOT REVERSE SMOTHER
	ADDI	XD1800,A0			;COME OUT BUTT SIDE

ALP_CHOOSE_ANGLE
	MOVE	A0,A1
	ADDI	XD0800,A0
	SUBI	XD0800,A1
	CALLA	RANGERND
	MOVE	A0,A2

	MOVI	040000H,A0
	MOVI	020000H,A1
	CALLA	RANGERND
	MOVE	A0,A1
	THETA_N	A2
	MOVE	A2,A0

	MOVE	@PUCK_OBJECT,A8,L
	CALLA	set_obj_to_polar

	JAUC	puck_slide_sound

**************************************************************************
*								         *
* ALL_IGNORE_PUCK							 *
* 									 *
* PASS:									 *
* A0 = IGNORE TIME							 *
*								         *
**************************************************************************

ALL_IGNORE_PUCK
	PUSH	A6
	MOVI	PLAYER_1_BLOCK,A6
AIP_LUPE
	MOVB	A0,*A6(POF_IGNORE_PUCK)
	ADDI	PLAYER_BLOCK_SIZE,A6
	CMPI	PLAYER_8_BLOCK,A6
	JRLE	AIP_LUPE

	PULLQ	A6
	RETS
**************************************************************************
*								         *
* A_RESET_WACK								 *
* 									 *
* PASS:									 *
* A8 = GOALIE OBJECT							 *
*								         *
**************************************************************************

A_RESET_WACK
	CLR	A14
	MOVE	A14,@whack_goalie_count,W

	CALLA	GETAFARG_LONG
	MOVE	A0,*A8(ANIMFRM),L

	MOVE	*A8(ODT_GPOSSIRQ),A14,L
	JRNZ	ARW_DONE			;BR=SET ANIMLOOP

	MOVB	*A8(ANIMLOOP),A1
	SUBK	6,A1
	JRP	ARW_SET				;BR=FREEZE TIME OKAY
	CLR	A1				;FLAG PASS FAILED
	DEC	A1
ARW_SET
	MOVB	A1,*A8(ANIMLOOP)
ARW_DONE
	RETS

**************************************************************************
*								         *
* A_CHECK_SPASS								 *
* 									 *
* PASS:									 *
* A8 = GOALIE OBJECT							 *
*								         *
**************************************************************************

A_CHECK_SPASS
	MOVE	@PUCK_CONTROL,A14,W
	JRZ	ACSP_GETUP				;BR=NO CONTROL

;IF NO CONTROL, CHECK?
;	MOVE	@PUCK_MODE,A14,W
;	CMPI	PUM_SCORE,A14
;	JRGE	ACSP_GETUP				;BR=ABORT SMOTHER

	MOVE	*A8(ODT_PBK),A6,L
	MOVB	*A6(POF_NUMBER),A0
	CMP	A14,A0
	JRNE	ACSP_GETUP				;BR=DON'T HAVE CONTROL

	CALLA	GET_TEAMMATE_BLOCKS

	MOVE	*A0(POF_FLAGS),A14,W
	BTST	B_PF_SIG_PASS,A14
	JRNZ	ACSP_PASS				;BR=PASS REQUEST

;	MOVE	A1,A0
;	MOVE	*A0(POF_FLAGS),A14,W
	MOVE	*A1(POF_FLAGS),A14,W
	BTST	B_PF_SIG_PASS,A14
	JRZ	ACSP_STAY				;BR=NO PASS REQUEST
ACSP_PASS
;	MOVE	A0,A7
;	CALLA	GOALIE_PASS_PUCK_PA7
	CALLA	GOALIE_PASS_PUCK
	JRC	ACSP_STAY				;BR=NO PASS

	MOVK	GS_PASSED,A0
	CALLA	GOALIE_SPEECH

	RETS
ACSP_STAY
	MOVE	*A8(ODT_GPOSSIRQ),A0,L		;RESET IRQ TIME OF POSSESSION
	JRZ	ACSP_CHECK_LOOP				;BR=NO IRQ
	JRN	ACSP_F_GO				;BR=PASS FAILED, FREEZE

	MOVE	@WAVEIRQS,A14,L
	SUB	A0,A14
	CMPI	FREEZE_TIME,A14
	JRLE	ACSP_CHECK_WACK				;BR=NOT YET
	JRUC	ACSP_FREEZE

ACSP_CHECK_LOOP
	MOVB	*A8(ANIMLOOP),A14
	JRN	ACSP_F_GO				;BR=PASS FAILED, FREEZE
	DEC	A14
	JRZ	ACSP_FREEZE				;BR=FREEZE PUCK
	MOVB	A14,*A8(ANIMLOOP)
ACSP_CHECK_WACK
	MOVE	@whack_goalie_count,A14,W
	JRZ	ACSP_STAY_ON_FRAME
	CALLA	GETAFARG_LONG
	CALLA	GETAFARG_LONG
	MOVE	A0,A1					;WACKED ANIM
	MOVI	512,A0
	CALLA	RANDPER
	JRC	ACSP_CW_SET
	CALLA	GETAFARG_LONG
	MOVE	A0,A1					;LOSE PUCK ANIM
ACSP_CW_SET
	MOVE	A1,*A8(ANIMFRM),L
	CALLA	player_grunt_sound
	RETS

ACSP_STAY_ON_FRAME	
	MOVE	*A8(ANIMFRM),A14,L			;STAY AT THIS FRAME
;	SUBI	020H+010H+020H+020H,A14
	SUBI	020H+010H+020H+020H+020H+020H,A14
	MOVE	A14,*A8(ANIMFRM),L
	RETS

ACSP_FREEZE
	MOVB	*A8(ODT_GFREEZES),A14
	CMPI	MAX_FREEZES,A14
	JRLT	ACSP_F_GO				;BR=FREEZE DA PUCK

	MOVI	FREEZE_PROB,A0
	CALLA	RANDPER
	JRC	ACSP_F_GO				;BR=FREEZE MF

	CALLA	GOALIE_PASS_PUCK
	JRC	ACSP_F_GO				;BR=NO PASS
	MOVK	GS_PASSED,A0
	CALLA	GOALIE_SPEECH
	RETS

ACSP_F_GO
	MOVB	*A8(ODT_GFREEZES),A14
	INC	A14
	MOVB	A14,*A8(ODT_GFREEZES)

	CLR	A14
	MOVE	A14,*A8(ODT_GPOSSIRQ),L		;RESET IRQ TIME OF POSSESSION

	MOVE	*A8(OZVAL),*A8(ODT_GZPOSPOSS),L
	MOVE	*A8(ODT_PBK),A6,L
	CALLA	PUCK_FROZEN
ACSP_GETUP
	CALLA	GETAFARG_LONG
	MOVE	A0,*A8(ANIMFRM),L

	RETS

**************************************************************************
*								         *
* A_FREEZE_PUCK								 *
* 									 *
* PASS:									 *
* A8 = GOALIE OBJECT							 *
*								         *
**************************************************************************

A_FREEZE_PUCK
	MOVK	GS_FROZEN,A0
	CALLA	GOALIE_SPEECH

	MOVE	*A8(OZVAL),*A8(ODT_GZPOSPOSS),L
	MOVE	*A8(ODT_PBK),A6,L
	JAUC	PUCK_FROZEN

**************************************************************************
*								         *
* A_NOPUCKCHECK								 *
* A_SETPUCKCHECK							 *
* 									 *
* PASS:									 *
* A8 = GOALIE OBJECT							 *
*								         *
**************************************************************************

A_NOPUCKCHECK
	MOVE	*A8(ODT_GFLAGS),A14,W
	ANDNI	M_PUCKCHECK,A14
	MOVE	A14,*A8(ODT_GFLAGS)
	RETS

A_SETPUCKCHECK
	MOVE	*A8(ODT_GFLAGS),A14,W
	ORI	M_PUCKCHECK,A14
	MOVE	A14,*A8(ODT_GFLAGS)
	RETS

**************************************************************************
*								         *
* A_GSCRIPT_DONE							 *
* 									 *
* PASS:									 *
* A8 = GOALIE OBJECT							 *
*								         *
**************************************************************************

A_GSCRIPT_DONE
	CLR	A14
	MOVE	A14,*A8(ODT_GSAVETIME),W

	MOVE	*A8(ODT_GFLAGS),A14,W
	ANDNI	M_MISSSAVE|M_SAVEMADE|M_TMOVE|M_FREEZE,A14
	ORI	M_PUCKCHECK,A14
	MOVE	A14,*A8(ODT_GFLAGS)

	MOVE	*A8(ODT_PBK),A6,L			;PLAYER BLOCK
	MOVB	*A6(POF_NUMBER),A0			;PLAYER NUMBER
	CALLA	give_player_control

	MOVE	*A6(POF_MODE),A14,W
	CMPI	PM_GHOLDING,A14
	JREQ	AGD_DONE				;BR=HOLDING THE PUCK
	CLR	A0
	MOVE	A0,*A6(POF_MODE),W
AGD_DONE
	RETS

**************************************************************************
*								         *
* A_CHECK_DEFLECT							 *
* 									 *
* PASS:									 *
* A8 = GOALIE OBJECT							 *
*								         *
**************************************************************************

A_CHECK_DEFLECT
	MOVE	@PUCK_CONTROL,A4,W
;	JANZ	A_GSCRIPT_DONE
;	JRNZ	ACD_POKE
	JRZ	ACD_P_ABORT				;BR=NOBODY HAS PUCK

;	MOVE	@PUCK_OBJECT,A9,L
;	MOVE	*A9(ODT_VEL),A4,L
;	CMPI	GSLOW_PUCK,A4
;	JALE	A_GSCRIPT_DONE
;
;	CALLA	get_players_puck_point_o
;	MOVE	A0,A2
;	MOVE	A1,A3
;;	MOVE	*A8(OXVEL),A14,L
;;	ADD	A14,A2
;;	MOVE	*A8(OZVEL),A14,L
;;	ADD	A14,A3
;
;	MOVE	*A9(OXVAL),A0,L
;	MOVE	*A9(OXVEL),A14,L
;	ADD	A14,A0
;	MOVE	*A9(OZVAL),A1,L
;	MOVE	*A9(OZVEL),A14,L
;	ADD	A14,A1
;
;	CALLA	find_dir_to_point_18		;FROM GOALIE TO PUCK
;	MOVE	*A8(ODT_PBK),A6,L
;	MOVB	*A6(POF_DIRECTION),A14
;	SUB	A14,A0
;	JRZ	ACD_STAY			;BR=SAME DIRECTION
;	ABS	A0
;	SUBK	1,A0
;	JRZ	ACD_STAY			;BR=ADJACENT DIRECTION
;	SUBK	6,A0
;	JRZ	ACD_STAY			;BR=8 TO 1 CASE
;
;	;CHECK PUCK VELOCITY
;	;IF SLOW ENOUGH CATCH ON STICK
;	;IF TOO FAST DEFLECT TO CLOSER PLAYER
;
;	CMPI	[12,0],A4
;	JRGT	ACD_DEFLECT			;BR=GOING TOO FAST TO CONTROL
;	CALLA	A_GSCRIPT_DONE
;;	JAUC	cpip_control
;
;	MOVE	@WAVEIRQS,A14,L
;	MOVE	A14,*A8(ODT_GPOSSIRQ),L		;IRQ TIME OF POSSESSION
;	MOVE	*A8(OZVAL),*A8(ODT_GZPOSPOSS),L	;Z POSITION
;
;	JAUC	GET_PUCK_CONTROL
;ACD_DEFLECT
;	MOVE	@PUCK_MODE,A14,W
;	CMPI	PUM_SCORE,A14
;	JRGE	A_GSCRIPT_DONE			;BR=DON'T FUCK WITH IT
;
;	CLR	A14				;PUM_OPEN
;	calla	set_safe_puck_mode_a14
;
;	CALLA	FIND_PLAYER_CLOSER_TO_GOALIE
;	GBLOCK	A0,A5
;	MOVE	A8,A7
;	MOVE	*A5(POF_OBJECT),A8,L
;	CALLA	get_players_puck_point_o
;	MOVE	A7,A8
;	CLR	A2				;Y POSITION
;
;	MOVE	*A9(ODT_VEL),A7,L
;	MOVE	*A6(POF_S_SHOT),A14,L
;	MOVE	*A14(PS_SH_SHOT_CHANGE),A14,L
;	MOVE	A14,B6
;	MOVE	*A6(POF_S_SHOT),A10,L
;	MOVE	*A10(PS_SH_MAX_YVEL),A10,L
;	CALLA	wrist_shot_sound
;	JAUC	send_puck_to_point_xyz
;ACD_STAY
;	MOVE	*A8(ANIMFRM),A14,L		;STAY AT THIS FRAME
;	SUBI	020H+010H+020H,A14
;	MOVE	A14,*A8(ANIMFRM),L
;	RETS
ACD_POKE
	MOVE	*A8(ODT_PBK),A6,L
	MOVB	*A6(POF_NUMBER),A0
	CMP	A14,A0
	JAEQ	A_GSCRIPT_DONE

	calla	get_players_puck_point_o
	MOVE	@PUCK_OBJECT,A14,L		;GET PUCK LOCATION
	MOVE	*A14(OXVAL),A2,L
	MOVE	*A14(OZVAL),A3,L
	CALLA	find_dsquared_to_point_whole_a1
	CMPI	0640H,A1			;0300H
	JRGT	ACD_P_ABORT

;	MOVE	@PUCK_MODE,A14,W
;	CMPI	PUM_SCORE,A14
;	JRGE	A_GSCRIPT_DONE			;BR=DON'T FUCK WITH IT

	CLR	A14				;PUM_OPEN
	calla	set_safe_puck_mode_a14
	MOVE	@PUCK_CONTROL,@PUCK_LAST_CONTROL,W
	CLR	A14
	MOVE	A14,@PUCK_INTENDED,W
	MOVE	A14,@PUCK_CONTROL,W
	CALLA	new_puck_control

	GBLOCK	A4,A4
	MOVI	60,A14
	MOVB	A14,*A4(POF_IGNORE_PUCK)

	CALLA	FIND_PLAYER_CLOSER_TO_GOALIE
	GBLOCK	A0,A5
	MOVE	A8,A7
	MOVE	*A5(POF_OBJECT),A8,L

	MOVE	*A8(OXVAL),A14,L
	ABS	A14
	CMPI	RGHT_GOALLINE_X,A14
	JRLE	ACD_GET_POINT			;BR=IN FRONT OF GOAL LINE

	MOVI	TOP_SIDEBOARD_Z-1,A0
	CALLA	RANDU
	MOVE	A0,A1				;Z POSITION
	CLR	A0				;X POSITION
	JRUC	ACD_HIT
ACD_GET_POINT
	CALLA	get_players_puck_point_o
ACD_HIT
	MOVE	A7,A8
	CLR	A2				;Y POSITION
	MOVI	[10,0],A7
	MOVE	*A6(POF_S_SHOT),A14,L
	MOVE	*A14(PS_SH_SHOT_CHANGE),A14,L
	MOVE	A14,B6
	MOVE	*A6(POF_S_SHOT),A10,L
	MOVE	*A10(PS_SH_MAX_YVEL),A10,L
	CALLA	wrist_shot_sound
	JAUC	send_puck_to_point_xyz

ACD_P_ABORT
	RETS

**************************************************************************
*								         *
* A_POKE								 *
* 									 *
* PASS:									 *
* A8 = GOALIE OBJECT							 *
*								         *
**************************************************************************

A_POKE
;	CALLA	GETAFARG_WORD
;	CMPI	040H,A0
;	JRLT	AP_DONE
;	MOVE	*A8(ANIMFRM),A14,L
;	SUBI	(020H+010H+020H+010H)+(020H+010H+020H+010H+020H+020H),A14
;	MOVE	A14,*A8(ANIMFRM),L
;	RETS

	MOVE	@PUCK_CONTROL,A4,W
	JRZ	AP_DONE				;BR=NOBODY HAS CONTROL

	MOVE	*A8(ODT_PBK),A6,L
	MOVB	*A6(POF_NUMBER),A0
	CMP	A4,A0
	JREQ	AP_DONE

;	calla	get_players_puck_point_o
;	MOVE	A0,A2
;	MOVE	A1,A3

	MOVE	*A8(OXVAL),A2,L
	MOVE	*A8(OZVAL),A3,L

	MOVE	@PUCK_OBJECT,A9,L		;GET PUCK LOCATION
	MOVE	*A9(OXVAL),A0,L
	MOVE	*A9(OZVAL),A1,L
;	CALLA	find_dsquared_to_point_whole_a1
;	CALLA	find_dirdis_to_point

	CALLA	find_dist_quick2

	CALLA	GETAFARG_WORD
	SLL	16,A0
	CMP	A0,A1
	JRGT	AP_DONE

;	CMPI	020H,A1				;020H^2=0400H
;	JRLE	AP_REJECT
;
;	CALLA	dir_to_18
;	SUBK	4,A0
;	JRP	AP_CHECK_DIR
;	ADDK	8,A0
;AP_CHECK_DIR
;	MOVB	*A6(POF_DIRECTION),A14
;	CMP	A14,A0
;	JRNE	AP_DONE

AP_REJECT
;PUCK CONTROL IS NON-ZERO!
;	MOVE	@PUCK_MODE,A14,W
;	CMPI	PUM_SCORE,A14
;;	JRGE	AP_DONE				;BR=DON'T FUCK WITH IT
;	JREQ	AP_DONE				;BR=DON'T FUCK WITH IT

	MOVK	GS_POKECHECK,A0
	CALLA	GOALIE_SPEECH

	CLR	A14				;PUM_OPEN
	calla	set_safe_puck_mode_a14
	MOVE	@PUCK_CONTROL,@PUCK_LAST_CONTROL,W
	CLR	A14
	MOVE	A14,@PUCK_INTENDED,W
	MOVE	A14,@PUCK_CONTROL,W
	CALLA	new_puck_control

;	MOVI	TOP_SIDEBOARD_Z-1,A0
;	CALLA	RANDU
;	MOVE	A0,A1				;Z POSITION
;	CLR	A0				;X POSITION
;	CLR	A2				;Y POSITION

;	MOVI	[10,0],A7
;	MOVE	*A6(POF_S_SHOT),A14,L
;	MOVE	*A14(PS_SH_SHOT_CHANGE),A14,L
;	MOVE	A14,B6
;	MOVE	*A6(POF_S_SHOT),A10,L
;	MOVE	*A10(PS_SH_MAX_YVEL),A10,L
	CALLA	wrist_shot_sound
;	JAUC	send_puck_to_point_xyz

	GBLOCK	A4,A4
	MOVI	60,A14
	MOVB	A14,*A4(POF_IGNORE_PUCK)

	MOVE	*A9(ODT_VEL),A14,L
	CMPI	[7,0],A14
	JRLT	AP_REDIR			;BR=TOO SLOW, NO FLY!

;	JRUC	ATU_GO

	MOVB	*A6(POF_DIRECTION),A0
	CALLA	dir_to_degrees
	MOVE	A0,A1
	ADDI	XD1800+XD0225,A0
	ADDI	XD1800-XD0225,A1
	CALLA	RANGERND
	MOVE	A0,A2

	MOVI	1024,A0				;DECIDE WHERE PUCK FLIES
	CALLA	RAND0
	CMPI	410,A0				;60% FLY BEHIND NET
	JRGT	AP_FLY_BEHIND_NET		;BR=FLY BEHIND NET
	MOVE	A0,A3

	MOVI	010000H,A0
	MOVI	08000H,A1
	MOVI	-050000H,A5
	MOVI	-068000H,A6

	CALLA	IS_A8_IN_CREASE
	JRC	AP_FLY_IN_FRONT			;BR=INSIDE DA CREASE

	CMPI	205,A3				;50% (OF 60%) FLY BEHIND GOALIE
	JRGT	AP_CHOOSE_VEL			;BR=FLY BEHIND GOALIE
AP_FLY_IN_FRONT
	ADDI	XD1800,A2			;FLY IN FRONT OF GOALIE
	JRUC	AP_CHOOSE_VEL

AP_FLY_BEHIND_NET
	MOVI	060000H,A0
	MOVI	050000H,A1
	MOVI	-060000H,A5
	MOVI	-070000H,A6
AP_CHOOSE_VEL
	CALLA	RANGERND
	MOVE	A0,A1
	THETA_N	A2
	MOVE	A2,A0
	MOVE	@PUCK_OBJECT,A8,L
	CALLA	set_obj_to_polar

	MOVE	A5,A0
	MOVE	A6,A1
	CALLA	RANGERND
	MOVE	A0,*A8(OYVEL),L

	MOVI	NORM_GRAVITY,A14
	MOVE	A14,*A8(OYACCEL),W
	RETS

AP_REDIR
	MOVB	*A6(POF_DIRECTION),A0
	MOVE	A0,A2
	CALLA	dir_to_degrees
	MOVE	A0,A1

	ANDI	3,A2
	SUBK	3,A2
	JRZ	AP_DIR3_RANGE			;BR=DIRECTION 3 OR 7
	INC	A2
	JRNZ	AP_DIR4_RANGE			;BR=DIRECTION 4 OR 8

	ADDI	XD0100,A0			;DIRECTION 2 OR 6
	SUBI	XD0600,A1
	JRUC	AP_GET_DIR
AP_DIR4_RANGE
	ADDI	XD0600,A0
	SUBI	XD0100,A1
	JRUC	AP_GET_DIR
AP_DIR3_RANGE
	ADDI	XD0350,A0
	SUBI	XD0350,A1
AP_GET_DIR
	CALLA	RANGERND
	MOVE	A0,A2

	MOVI	[16,0],A0
	MOVI	[12,0],A1
	CALLA	RANGERND

	MOVE	A2,A0
	MOVE	A9,A8
	CALLA	set_obj_to_polar

	JAUC	puck_slide_sound
AP_DONE
	RETS

**************************************************************************
*								         *
* COUNT_SAVE								 *
* 									 *
* PASS:									 *
* A2 = X IMPACT POINT WITH OFFSET					 *
* A3 = Z IMPACT POINT WITH OFFSET					 *
* A6 = PLAYER BLOCK							 *
* A8 = GOALIE OBJECT							 *
* A9 = PUCK OBJECT							 *
*								         *
**************************************************************************

COUNT_SAVE
	MMTM	SP,A1,A2,A3

	MOVE	*A6(POF_SAVES),A14,W
	INC	A14
	MOVE	A14,*A6(POF_SAVES),W

	MOVE	@GAME_STATE,A0,W
	CMPI	INAMODE,A0
	JREQ	CS_FIRE				;BR=IN ATTRACT MODE
	MOVE	@PUCK_MODE,A0,W
	CMPI	PUM_FACEOFF,A0
	JRHS	CS_FIRE				;BR=FACEOFF OR END OF PERIOD
	MOVI	AUD_GOALIE_SAVES,A0
	CALLA	AUD1
CS_FIRE

	MOVE	@show_puck_on_fire,A14,W
	JRZ	CS_SPEECH			;BR=PUCK NOT ON FIRE

	MMTM	SP,A3,A4,A5,A7,A8,A10,A11

	MOVB	*A6(POF_DIRECTION),A4

	MOVE	*A8(OZVAL),A3,L
	SUBI	01000H,A3

	MOVE	*A9(OXVEL),A5,L
	ABS	A5
	SRL	4,A5
	MOVE	A5,A7
	NEG	A7

	MOVE	*A9(OZVEL),A10,L
	ABS	A10
	SRL	4,A10
	MOVE	A10,A11
	NEG	A11

	MOVE	*A9(ODT_VEL+010H),A1,W

	CMPI	ROUGH_PUCK_MAX >> 16,A1
	JRLE	CS_GET_FIRE_COUNT		;BR=UNDER MAXIMUM SPEED
	MOVI	15,A4				;MAXIMUM FIRE COUNT
	JRUC	CS_FIRE_LUPE

CS_GET_FIRE_COUNT
	MOVK	3,A4

	SUBK	0EH,A1
	JRN	CS_FIRE_LUPE			;BR=UNDER MINIMUM SPEED

	SLL	4,A1
	ADDI	FIRE_COUNT_TABLE,A1
	MOVE	*A1,A4,W

CS_FIRE_LUPE
	MOVI	SAVE_FIRE_INIT,B0
	CALLA	EASYMAKE
	JRZ	CS_FIRE_DONE			;BR=CAN'T MAKE IT

	MOVE	*A9(OYVAL),*A8(OYVAL),L

	MOVE	A2,*A8(OXVAL),L
;	DEC	A3
	MOVE	A3,*A8(OZVAL),L

	CMPI	1,A4
	JREQ	CS_FIRE_ANIM

	MOVE	A5,A0
	MOVE	A7,A1
	CALLA	RANGERND
	MOVE	A0,*A8(OXVEL),L

	MOVI	08000H,A0
	MOVI	-08000H,A1
	CALLA	RANGERND
	MOVE	A0,*A8(OYVEL),L

	MOVE	A10,A0
	MOVE	A11,A1
	CALLA	RANGERND
	MOVE	A0,*A8(OZVEL),L

CS_FIRE_ANIM
	MOVI	SAVE_FIRE_ANIM,A1
	CALLA	STRT_ANIM

	CALLA	INSERT_OBJ
	DSJ	A4,CS_FIRE_LUPE
CS_FIRE_DONE
	MMFM	SP,A3,A4,A5,A7,A8,A10,A11

CS_SPEECH
	MOVI	SAVE_SPEECH_TABLE-020H-010H,A1
	MOVE	*A8(ODT_GSAVE),A2,L
CS_NEXT
	ADDI	020H+010H,A1
	MOVE	*A1,A14,L
	JRZ	CS_GET_SPEECH_INDEX		;BR=SAVE NOT IN LIST
	CMP	A2,A14
	JRNE	CS_NEXT				;BR=NO MATCH
CS_GET_SPEECH_INDEX
	MOVE	*A1(020H),A1,W			;GET SPEECH TYPE INDEX

	;SPEECH TYPE INDEX READY IN A1

	calla	do_goalie_phrase

	CALLA	record_puck_a6
	move	@WAVEIRQS,@PUCK_NEWDIR_TIME,L
	CLR	A14				;PUM_OPEN
	calla	set_safe_puck_mode_a14

	MOVE	@PUCK_CONTROL,A1,W
	JRZ	CS_GO				;BR=NOBODY CONTROLS

	MOVE	A1,A2
	GBLOCK	A2,A2
	MOVI	60,A14
	MOVB	A14,*A2(POF_IGNORE_PUCK)

	MOVE	A1,@PUCK_LAST_CONTROL,W
	CLR	A14
	MOVE	A14,@PUCK_INTENDED,W
	MOVE	A14,@PUCK_CONTROL,W
	CALLA	new_puck_control
CS_GO
	MOVE	*A8(ODT_GFLAGS),A14,W		;FLAG SAVE SUCCESSFUL
	ORI	M_SAVEMADE,A14
	MOVE	A14,*A8(ODT_GFLAGS),W

	MOVE	*A8(ODT_GLASTSIRQ),A14,L
	MOVE	@WAVEIRQS,A1,L

	MOVE	*A8(ODT_GTSAVES),A2,W
	JRZ	CS_COUNT

	SUB	A1,A14
	CMPI	-MAX_SAVE_TTIME,A14
	JRLT	CS_DONE
CS_COUNT
	MOVE	A1,*A8(ODT_GLASTSIRQ),L

	CMPI	4,A2
	JRGE	CS_DONE				;BR=DON'T GO OVER

	INC	A2
	MOVE	A2,*A8(ODT_GTSAVES),W
CS_DONE
	MMFM	SP,A1,A2,A3
	RETS

FIRE_COUNT_TABLE
	;	14  15  16  17  18  19  20  21  22  23  24
	.WORD	 3,  3,  5,  5,  5,  7,  7,  7,  9,  9,  9
	;
	;	25  26  27  28  29  30  31  32  33  34  35
	.WORD	11, 11, 11, 13, 13, 13, 15, 15, 15, 15, 15


SAVE_SPEECH_TABLE
	LW	SET_GFULLBUTT_SCRIPT,GS_BUTTERFLY
	LW	SET_GBUTTERFLY_SCRIPT,GS_BUTTERFLY
	LW	SET_GLPADST_SCRIPT,GS_PADSTACK
	LW	SET_GQLPADST_SCRIPT,GS_PADSTACK
	LW	SET_GRPADST_SCRIPT,GS_PADSTACK
	LW	SET_GQRPADST_SCRIPT,GS_PADSTACK
	LW	SET_GGLOVE_SCRIPT,GS_GLOVE
	LW	SET_GQGLOVE_SCRIPT,GS_GLOVE
	LW	SET_GQHGLOVE_SCRIPT,GS_GLOVE
	LW	SET_GHGLOVE_SCRIPT,GS_GLOVE
	LW	SET_GBLOCKER_SCRIPT,GS_BLOCKER
	LW	SET_GQBLOCKER_SCRIPT,GS_BLOCKER
	LW	SET_GQHBLOCKER_SCRIPT,GS_BLOCKER
	LW	SET_GBODY_SCRIPT,GS_STOMACH
	LW	SET_GSTICK_SCRIPT,GS_STICK
	LW	SET_GSSKATE_SCRIPT,GS_SKATE
	LW	SET_GQSSKATE_SCRIPT,GS_SKATE
	LW	SET_GGSKATE_SCRIPT,GS_SKATE
	LW	SET_GQGSKATE_SCRIPT,GS_SKATE
	LW	0,GS_SAVE

**************************************************************************
*								         *
* CHECK_PUCK_IMPACT							 *
* 									 *
* PASS:									 *
* A0 = X IMPACT OFFSET							 *
* A1 = Z IMPACT OFFSET							 *
* A6 = PLAYER BLOCK							 *
* A8 = GOALIE OBJECT							 *
* A9 = PUCK OBJECT							 *
*								         *
* RETURN:								 *
* Z = NO IMPACT, NZ = IMPACT						 *
* A2 = X IMPACT POINT WITH OFFSET					 *
* A3 = Z IMPACT POINT WITH OFFSET					 *
* TRASHES: A0, A1, A2, A4, A5						 *
*								         *
**************************************************************************

CHECK_PUCK_IMPACT
	MOVB	*A6(POF_DIRECTION),A5
	ANDI	3,A5			;DIRS: 010B=2(6), 011B=3(7), 0B=4(8)

	MOVE	*A8(ODT_GXIMPACT),A2,L
	JRN	CPI_ADDXOFF			;BR=ON THE LEFT
	NEG	A0
	BTST	0,A5
	JRNZ	CPI_ADDXOFF			;BR=DIR 3 OR 7
	XORI	2,A5				;SWAP DIRS 6 AND 8
CPI_ADDXOFF
	ADD	A0,A2
	MOVE	A2,A4

	MOVE	*A9(OXVAL),A0,L
	MOVE	*A9(OXVEL),A14,L
	ADD	A14,A0

	MOVE	*A8(ODT_GZIMPACT),A3,L

	SUBK	3,A5			;NEW DIRS: 011B=2(8), 0B=3(7), 01B=4(6)
	JRZ	CPI_ADDZOFF			;BR=DIR 3 OR 7
	BTST	1,A5
	JRNZ	CPI_ADDZOFF			;BR=DIR 2 OR 8
	NEG	A1				;DIR 4 OR 6
CPI_ADDZOFF
	SUB	A1,A3

	MOVE	A14,B1
	ABS	B1
	MOVE	A9,B9
	MOVE	*B9(OZVEL),B0,L
	ABS	B0
	CMP	B1,B0
	JRGT	CPI_CHECK_Z			;BR=Z VELOCITY GREATER THAN X

	MOVE	A14,A14
;	JRZ	CPI_CHECK_Z			;BR=AIN'T MOVIN' IN X
	JRN	CPI_GOIN_LEFT			;BR=PUCK HEADIN' TO THE LEFT

	CMP	A2,A0
	JRGE	CPI_IMPACT			;BR=IMPACT ON X
	CLR	A14				;FLAG NO IMPACT
	RETS
CPI_GOIN_LEFT
	CMP	A2,A0
	JRLE	CPI_IMPACT			;BR=IMPACT ON X
	CLR	A14				;FLAG NO IMPACT
	RETS

CPI_CHECK_Z
	MOVE	*A9(OZVAL),A0,L
	MOVE	*A9(OZVEL),A14,L
	ADD	A14,A0

;	BTST	1,A5
;	JRNZ	CPI_2OR8			;BR=DIR 2 OR 8

	MOVE	A14,A14
	JRNN	CPI_GOIN_AWAY			;BR=PUCK HEADIN' AWAY

	CMP	A3,A0
	JRLE	CPI_IMPACT			;BR=IMPACT ON Z
	CLR	A14				;FLAG NO IMPACT
	RETS
;CPI_2OR8
CPI_GOIN_AWAY
	CMP	A3,A0
	JRGE	CPI_IMPACT			;BR=IMPACT ON Z
CPI_NOPACT
	CLR	A14				;FLAG NO IMPACT
	RETS
CPI_IMPACT
	MOVE	A4,A2				;ALSO CLRS Z (FLAG IMPACT)
	RETS

**************************************************************************
*								         *
* A_CHECK_BUTTERFLY							 *
* 									 *
* PASS:									 *
* A8 = GOALIE OBJECT							 *
*								         *
**************************************************************************

A_CHECK_BUTTERFLY
	MOVE	*A8(ODT_GFLAGS),A14,W
	ANDI	M_SAVEMADE|M_MISSSAVE,A14
	JRNZ	ACBF_DONE			;BR=DON'T SAVE

	MOVE	@PUCK_CONTROL,A0,W
;	JRNZ	ACBF_DONE			;BR=SOMEONE GOT THE PUCK
	JRZ	ACBF_CHECK_MODE			;BR=NOBODY HAS CONTROL

	GBLOCK	A0,A0
	MOVE	*A0(POF_MODE),A14,W
	SUBK	PM_ONETIME,A14
	JRNZ	ACBF_DONE			;BR=NOT THE ONE TIMER CASE
	JRUC	ACBF_GO
ACBF_CHECK_MODE
	MOVE	@PUCK_MODE,A14,W
	CMPI	PUM_SCORE,A14
;	JRGE	ACBF_DONE			;BR=GOAL JUST GET UP
	JREQ	ACBF_DONE			;BR=GOAL JUST GET UP
ACBF_GO
	MOVE	@PUCK_OBJECT,A9,L
	MOVE	*A9(ODT_VEL),A14,L
	JRZ	ACBF_DONE			;BR=PUCK STOPPED

	MOVE	*A9(OXVEL),A0,L
	MOVE	*A8(ODT_PBK),A6,L
	MOVB	*A6(POF_NUMBER),A14
	SUBK	4,A14
	JRZ	ACBF_CHECK_PUD			;BR=PUCK X DIRECTION OKAY
	NEG	A0
ACBF_CHECK_PUD
	MOVE	A0,A0
	JRP	ACBF_DONE			;BR=PUCK GOING AWAY FROM GOAL

	.if	1

;	MOVI	0200000H,A0
;	CLR	A1

;	MOVI	016A09EH,A0
;	MOVE	A0,A1

	CLR	A0
	MOVE	A0,A1
	CALLR	CHECK_PUCK_IMPACT
	JRNZ	ACGU_SAVE			;BR=PUCK COLLISION

	.else

	MOVB	*A6(POF_DIRECTION),A5

;	MOVE	*A8(OXVAL),A2,L
;	MOVE	*A8(OZVAL),A3,L

	MOVE	*A8(ODT_GXIMPACT),A2,L		;PUCK AT IMPACT POINT
	MOVE	*A8(ODT_GZIMPACT),A3,L

	MOVE	*A9(OXVAL),A0,L
	MOVE	*A9(OXVEL),A14,L
	ADD	A14,A0
	MOVE	*A9(OZVAL),A1,L
	MOVE	*A9(OZVEL),A14,L
	ADD	A14,A1
	CALLA	find_dir_to_point_18		;FROM GOALIE TO PUCK
	SUB	A5,A0
	JRZ	ACBF_DONE			;BR=SAME DIRECTION
	ABS	A0
	SUBK	1,A0
	JRZ	ACBF_DONE			;BR=ADJACENT DIRECTION
	SUBK	6,A0
	JRZ	ACBF_DONE			;BR=8 TO 1 CASE

	JRUC	ACGU_SAVE

	.endif

ACBF_DONE
	RETS

**************************************************************************
*								         *
* A_CHECK_GETUP								 *
* 									 *
* PASS:									 *
* A8 = GOALIE OBJECT							 *
*								         *
**************************************************************************

A_CHECK_GETUP
	MOVE	*A8(ODT_GFLAGS),A14,W
	BTST	B_SAVEMADE,A14
	JRNZ	ACGU_GETUP			;BR=ALREADY SAVED

	BTST	B_MISSSAVE,A14
	JRZ	ACGU_CHECK_CTRL			;BR=SAVE

	MOVE	@PUCK_CONTROL,A14,W
	JRNZ	ACGU_GETUP			;BR=SOMEBODY GOT DA PUCK

	MOVE	*A8(ODT_GCOMPTIME),A0,L		;TIME OF ANIMATION START
	MOVE	@WAVEIRQS,A14,L
	SUB	A0,A14
	SUBI	30,A14
	JRNN	ACGU_GETUP			;BR=TIME OUT!

	MOVE	@PUCK_MODE,A14,W
	CMPI	PUM_SCORE,A14
	JRLT	ACGU_STAY			;BR=NO SCORE YET
	CMPI	PUM_SCORE,A14
	JRNE	ACGU_STAY			;BR=SCORE OVER

	MOVB	*A8(ANIMLOOP),A14
	JRZ	ACGU_GETUP			;BR=HOLD FINISHED
	DEC	A14
	MOVB	A14,*A8(ANIMLOOP)
	JRUC	ACGU_STAY

ACGU_CHECK_CTRL
	MOVE	@PUCK_CONTROL,A0,W
;	JRNZ	ACGU_GETUP			;BR=SOMEONE GOT THE PUCK
	JRZ	ACGU_CHECK_STUFF		;BR=NOBODY HAS CONTROL

	GBLOCK	A0,A0
	MOVE	*A0(POF_MODE),A14,W
	SUBK	PM_ONETIME,A14
	JRNZ	ACGU_GETUP			;BR=NOT THE ONE TIMER CASE
	JRUC	ACGU_GO
ACGU_CHECK_STUFF
	MOVE	@PUCK_MODE,A14,W
	CMPI	PUM_SCORE,A14
;	JRGE	ACGU_GETUP			;BR=GOAL JUST GET UP
	JREQ	ACGU_GETUP			;BR=GOAL JUST GET UP

	MOVE	@PUCK_OBJECT,A14,L
	MOVE	*A14(ODT_VEL),A14,L
	JRZ	ACGU_GETUP			;BR=PUCK STOPPED

;	MOVE	*A8(OZVAL),*A8(ODT_GZPOSPOSS),L
;	MOVE	*A8(ODT_PBK),A6,L
;	CALLA	PUCK_FROZEN
;ACGU_CHECK_TIME

	MOVE	*A8(ODT_GCOMPTIME),A0,L		;TIME OF ANIMATION START
	MOVE	@PUCK_NEWDIR_TIME,A14,L
	CMP	A0,A14
	JRGT	ACGU_GETUP			;BR=NEW PUCK DIRECTION

	MOVE	@WAVEIRQS,A14,L
	SUB	A0,A14
	SUBK	30,A14
	JRNN	ACGU_GETUP			;BR=TIME OUT!
ACGU_GO
	MOVE	@PUCK_OBJECT,A9,L
	MOVE	*A9(ODT_VEL),A14,L
	JRZ	ACGU_GETUP			;BR=PUCK STOPPED

	MOVE	*A9(OXVEL),A0,L
	MOVE	*A8(ODT_PBK),A6,L
	MOVB	*A6(POF_NUMBER),A14
	SUBK	4,A14
	JRZ	ACGU_CHECK_PUD			;BR=PUCK X DIRECTION OKAY
	NEG	A0
ACGU_CHECK_PUD
	MOVE	A0,A0
	JRP	ACGU_GETUP			;BR=PUCK GOING AWAY FROM GOAL

	.if	1

;	MOVI	0200000H,A0
;	CLR	A1

;;	MOVI	016A09EH,A0
;	MOVI	0100000H,A0
;	MOVE	A0,A1

	CLR	A0
	MOVE	A0,A1
	CALLR	CHECK_PUCK_IMPACT
	JRZ	ACGU_STAY			;BR=NO PUCK COLLISION

	.else

	MOVB	*A6(POF_DIRECTION),A5
;	MOVE	A5,A0				;WILL THE PUCK CHANGE DIR?
;	CALLA	dir_to_degrees
;
;	MOVE	*A9(ODT_DIR),A14,L
;	THETA_N	A14
;	SUB	A14,A0
;	ABS	A0
;	CMPI	XD1800,A0
;	JRLE	ACGU_ANGLE_OKAY
;	SUBI	XD3600,A0
;	NEG	A0
;ACGU_ANGLE_OKAY
;	CMPI	XD0900,A0
;	JRLT	ACGU_GETUP			;BR=PUCK IS GOING AWAY

;	calla	get_players_puck_point_o
;	MOVE	A0,A2				;X POSITION
;	MOVE	A1,A3				;Z POSITION

	MOVE	*A8(ODT_GXIMPACT),A2,L		;PUCK AT IMPACT POINT
	MOVE	*A8(ODT_GZIMPACT),A3,L

;	MOVE	A5,A14
;	ANDI	3,A14
;	CMPI	3,A14
;	JRNE	ACGU_GO				;BR=NOT DIRECTION 3 OR 7
;
;	MOVE	*A9(OYPOS),A14,W
;	JRZ	ACGU_GO				;BR=NOT ON GROUND
;	ABS	A2
;	GETST	A14
;	ADDI	0100000H,A2
;	BTST	31,A14
;	JRNZ	ACGU_GO				;BR=WASN'T NEGATIVE
;	NEG	A2
;ACGU_GO
	MOVE	*A9(OXVAL),A0,L
	MOVE	*A9(OXVEL),A14,L
	ADD	A14,A0
	MOVE	*A9(OZVAL),A1,L
	MOVE	*A9(OZVEL),A14,L
	ADD	A14,A1
	CALLA	find_dir_to_point_18		;FROM GOALIE TO PUCK
	SUB	A5,A0
	JRZ	ACGU_STAY			;BR=SAME DIRECTION
	ABS	A0
	SUBK	1,A0
	JRZ	ACGU_STAY			;BR=ADJACENT DIRECTION
	SUBK	6,A0
	JRZ	ACGU_STAY			;BR=8 TO 1 CASE

	.endif

ACGU_SAVE
	CALLR	COUNT_SAVE

	MOVE	A2,*A9(OXVAL),L			;JUST PUT IT AT PUCK POINT
	MOVE	A3,*A9(OZVAL),L			; FOR NOW

;	MOVE	*A8(ODT_GXIMPACT),*A9(OXVAL),L	;PUCK PUCK AT IMPACT POINT
;	MOVE	*A8(ODT_GZIMPACT),*A9(OZVAL),L

	MOVE	*A9(OFLAGS),A14,W
	ORI	M_FVELADD,A14
	MOVE	A14,*A9(OFLAGS),W

	MOVI	s_goalie_stick_4,A4
	MOVE	*A9(OYPOS),A14,W
	JRZ	ACGU_REBOUND			;BR=NOT ON GROUND
	MOVI	s_goalie_pad_1,A4
ACGU_REBOUND
	CALLA	do_sound

	MOVE	*A8(ODT_GSAVE),A2,L
	CMPI	SET_GBSPLIT_SCRIPT,A2
	JREQ	ACGU_SPLIT			;BR=BLOCKER SPLIT
	CMPI	SET_GGSPLIT_SCRIPT,A2
	JRNE	ACGU_PUT_OTHER			;BR=NOT GLOVE SPLIT
ACGU_SPLIT
	MOVE	A9,A8
	MOVI	NORM_GRAVITY,A14
	MOVE	A14,*A9(OYACCEL),W

	MOVE	*A9(ODT_VEL),A0,L
	MOVE	A0,A1
	SRA	1,A1
	CALLA	RANGERND
;	CMPI	MAX_SAVE_PUCK_SPEED,A0
;	JRLE	ACGU_SPEED_SET			;BR=UNDER OR AT SPEED LIMIT
;	MOVI	MAX_SAVE_PUCK_SPEED,A0
;ACGU_SPEED_SET
	MOVE	A0,A3

	MOVI	XD0600,A1			;LEFT BLOCKER SPLIT PARAMETERS
	MOVI	XD0900,A0
	CMPI	SET_GGSPLIT_SCRIPT,A2
	JRNE	ACGU_GET_DIR			;BR=NOT GLOVE SPLIT
	NEG	A1				;LEFT GLOVE SPLIT PARAMETERS
	NEG	A0
ACGU_GET_DIR
	MOVE	*A9(ODT_DIR),A2,L
	ADDI	XD1800,A2

	MOVB	*A6(POF_NUMBER),A14
	SUBK	4,A14
	JRZ	ACGU_CHOOSE_ANGLE		;BR=LEFT GOALIE
	NEG	A1				;RIGHT PARAMETERS
	NEG	A0
ACGU_CHOOSE_ANGLE
	CALLA	RANGERND
	ADD	A2,A0

	MOVE	A3,A1
	CALLA	set_obj_to_polar

	MOVE	*A9(OYPOS),A14,W
	JAZ	puck_slide_sound
	RETS

ACGU_PUT_OTHER
	MOVE	A9,A8
	MOVE	*A8(ODT_VEL),A1,L
	SRA	3,A1
	JAUC	put_puck_in_other_direction
ACGU_STAY
	MOVE	*A8(ANIMFRM),A14,L		;STAY AT THIS FRAME
	SUBI	020H+010H+020H,A14
	MOVE	A14,*A8(ANIMFRM),L
	RETS
ACGU_GETUP
;	.if	BILL
;	MOVE	*A8(ANIMFRM),A14,L		;STAY AT THIS FRAME
;	SUBI	020H+010H+020H,A14
;	MOVE	A14,*A8(ANIMFRM),L
;	.endif
	RETS

**************************************************************************
*								         *
* A_CHECK_STICKBLOCK							 *
* 									 *
* PASS:									 *
* A8 = GOALIE OBJECT							 *
*								         *
**************************************************************************

A_CHECK_STICKBLOCK
	MOVE	*A8(ODT_GFLAGS),A14,W
	ANDI	M_SAVEMADE|M_MISSSAVE,A14
	JRNZ	ACSTB_DONE			;BR=DON'T SAVE

	MOVE	@PUCK_OBJECT,A9,L

	MOVE	*A8(ODT_PBK),A6,L

	CLR	A0
	MOVE	A0,A1
	CALLR	CHECK_PUCK_IMPACT
	JRZ	ACSTB_DONE			;BR=NO PUCK COLLISION

	CALLR	COUNT_SAVE

	MOVE	A2,*A9(OXVAL),L			;JUST PUT IT AT PUCK POINT
	MOVE	A3,*A9(OZVAL),L			; FOR NOW

;	MOVE	*A8(ODT_GXIMPACT),*A9(OXVAL),L	;PUCK PUCK AT IMPACT POINT
;	MOVE	*A8(ODT_GZIMPACT),*A9(OZVAL),L

	MOVE	*A9(OFLAGS),A14,W
	ORI	M_FVELADD,A14
	MOVE	A14,*A9(OFLAGS),W

	MOVI	s_goalie_stick_4,A4
	CALLA	do_sound

	MOVE	A9,A8
	MOVE	*A8(ODT_VEL),A1,L
	SRA	2,A1
	JAUC	put_puck_in_other_direction
ACSTB_DONE
	RETS

**************************************************************************
*								         *
* A_CHECK_HIBLOCK							 *
* 									 *
* PASS:									 *
* A8 = GOALIE OBJECT							 *
*								         *
**************************************************************************

ODT_GLOVE	.EQU	ODT_GZIMPACT		;UHW GLOVE EVENT CHOOSER

;GL_CATCH_PUCK	.EQU	614			;60% OF 1024
;GL_KEEP_PUCK	.EQU	614-368			;HIGH 60% OF CATCH_PUCK
;GL_HOOK_PUCK	.EQU	25			;LOW 10% OF CATCH PUCK

GL_CATCH_PUCK	.EQU	768			;75% OF 1024
GL_KEEP_PUCK	.EQU	768-461			;HIGH 60% OF CATCH_PUCK
GL_HOOK_PUCK	.EQU	31			;LOW 10% OF CATCH PUCK

;BL_CORNER	.EQU	205			;20% OF 1024
;BL_POPUP	.EQU	102			;10% OF 1024
;BL_POPUP_BEHIND.EQU	51			;50% OF BL_POPUP

BL_CORNER	.EQU	307			;30% OF 1024
BL_POPUP	.EQU	102			;LOW 10% OF 1024
BL_POPUP_BEHIND	.EQU	51			;50% OF BL_POPUP

A_CHECK_HIBLOCK
	MOVE	*A8(ODT_GFLAGS),A10,W
	MOVE	A10,A14
	ANDI	M_SAVEMADE|M_MISSSAVE,A14
	JRNZ	ACHB_DONE			;BR=DON'T SAVE

	MOVE	@PUCK_OBJECT,A9,L

	MOVE	*A8(ODT_PBK),A6,L

	CLR	A0
	MOVE	A0,A1
	CALLR	CHECK_PUCK_IMPACT
	JRZ	ACHB_DONE			;BR=NO PUCK COLLISION

;	MOVB	*A6(POF_DIRECTION),A5
;
;;	MOVE	*A8(OXVAL),A2,L
;;	MOVE	*A8(OZVAL),A3,L
;
;	MOVE	*A8(ODT_GXIMPACT),A2,L		;PUCK AT IMPACT POINT
;	MOVE	*A8(ODT_GZIMPACT),A3,L
;
;	MOVE	*A9(OXVAL),A0,L
;	MOVE	*A9(OXVEL),A14,L
;	ADD	A14,A0
;	MOVE	*A9(OZVAL),A1,L
;	MOVE	*A9(OZVEL),A14,L
;	ADD	A14,A1
;	CALLA	find_dir_to_point_18		;FROM GOALIE TO PUCK
;	SUB	A5,A0
;	JRZ	ACHB_DONE			;BR=SAME DIRECTION
;	ABS	A0
;	SUBK	1,A0
;	JRZ	ACHB_DONE			;BR=ADJACENT DIRECTION
;	SUBK	6,A0
;	JRZ	ACHB_DONE			;BR=8 TO 1 CASE

	CALLR	COUNT_SAVE

	MOVI	1024,A0				;DECIDE IF GOALIE CATCHES PUCK
	CALLA	RAND0
	CMPI	GL_CATCH_PUCK,A0
	JRGT	ACHB_NOCATCH			;BR=DON'T CATCH IT!

	MOVE	*A8(ODT_GSAVE),A14,L
	CMPI	SET_GGLOVE_SCRIPT,A14
	JREQ	ACHB_CATCH
	CMPI	SET_GQGLOVE_SCRIPT,A14
	JREQ	ACHB_CATCH
	CMPI	SET_GQHGLOVE_SCRIPT,A14
	JREQ	ACHB_CATCH
	CMPI	SET_GHGLOVE_SCRIPT,A14
	JRNE	ACHB_NOCATCH
ACHB_CATCH
	MOVI	s_goalie_mitt_5,A4
	CALLA	do_sound

	MOVE	A0,*A8(ODT_GLOVE),L

	MOVK	PM_GHOLDING,A14
	MOVE	A14,*A6(POF_MODE),W

	CLR	A14
	MOVE	A14,*A8(ODT_VEL),L
	MOVE	A14,*A8(OXVEL),L
	MOVE	A14,*A8(OZVEL),L

	MOVE	@WAVEIRQS,A14,L
	MOVE	A14,*A8(ODT_GPOSSIRQ),L		;IRQ TIME OF POSSESSION
	MOVE	*A8(OZVAL),*A8(ODT_GZPOSPOSS),L	;Z POSITION

	CALLA	GET_PUCK_CONTROL

	MOVE	@PUCK_OBJECT,A9,L
	MOVE	*A9(ODT_PFLAGS),A14,W
	ORI	M_INBACK,A14
;	ORI	M_INFRONT,A14
	MOVE	A14,*A9(ODT_PFLAGS),W

	RETS
ACHB_NOCATCH
	MOVI	s_goalie_pad_2,A4
	CALLA	do_sound

	MOVE	A2,*A9(OXVAL),L			;JUST PUT IT AT PUCK POINT
	MOVE	A3,*A9(OZVAL),L			; FOR NOW

;	MOVE	*A8(ODT_GXIMPACT),*A9(OXVAL),L	;PUCK PUCK AT IMPACT POINT
;	MOVE	*A8(ODT_GZIMPACT),*A9(OZVAL),L

	MOVE	*A9(OFLAGS),A14,W
	ORI	M_FVELADD,A14
	MOVE	A14,*A9(OFLAGS),W

	MOVE	*A9(OYVEL),A1,L

	MOVE	*A8(ODT_GSAVE),A14,L
	CMPI	SET_GBHOP_SCRIPT,A14
	JREQ	ACHB_FLY_HIGH			;BR=A QUICK HOP SAVE
	CMPI	SET_GGHOP_SCRIPT,A14
	JREQ	ACHB_FLY_HIGH			;BR=A QUICK HOP SAVE
	CMPI	SET_GKO_SCRIPT,A14
	JREQ	ACHB_FLY_HIGH			;BR=A KO SAVE
	CMPI	SET_GQBLOCKER_SCRIPT,A14
	JREQ	ACHB_FLY_HIGH			;BR=A QUICK BLOCKER SAVE
	CMPI	SET_GQHBLOCKER_SCRIPT,A14
	JRNE	ACHB_FLY_NORM			;BR=NOT A HIGH QUICK BLOCKER
ACHB_FLY_HIGH
	MOVI	1024,A0				;DECIDE WHERE PUCK FLIES
	CALLA	RAND0
	MOVE	A0,A4

	MOVE	A1,A0
	ADDI	018000H,A0
	SUBI	048000H,A1

	MOVI	XD1350,A2
	MOVI	-(XD1350-XD0450),A3

	CMPI	BL_CORNER,A4
	JRGT	ACHB_CHECK_LEFT			;BR=FLY BEHIND GOALIE

	CMPI	BL_POPUP,A4
	JRLT	ACHB_POP_FLY			;BR=POP UP

	MOVI	XD0900,A2			;FLY IN FRONT OF GOALIE
	MOVI	-(XD0900-XD0450),A3
ACHB_CHECK_LEFT
	MOVB	*A6(POF_NUMBER),A14
	SUBK	4,A14
;	JRZ	ACHB_CHOOSE_YVEL		;BR=LEFT GOALIE
	JRNZ	ACHB_CHECK_RIGHT		;BR=RIGHT GOALIE

	BTST	B_GLOVESIDE,A10
	JRNZ	ACHB_OTHER_SIDE			;BR=ON GLOVE SIDE
	JRUC	ACHB_CHOOSE_YVEL
	
ACHB_CHECK_RIGHT
	BTST	B_GLOVESIDE,A10
	JRNZ	ACHB_CHOOSE_YVEL		;BR=ON GLOVE SIDE
ACHB_OTHER_SIDE
	NEG	A2				;RIGHT GOALIE BLOCKER ANGLE OFFS
	NEG	A3
	JRUC	ACHB_CHOOSE_YVEL

ACHB_POP_FLY
	MOVB	*A6(POF_DIRECTION),A0
	CALLA	dir_to_degrees
	MOVE	A0,A1
	ADDI	XD1800+XD0225,A0
	ADDI	XD1800-XD0225,A1
	CALLA	RANGERND
	MOVE	A0,A2

	MOVI	010000H,A0
	MOVI	08000H,A1
	MOVI	-050000H,A5
	MOVI	-058000H,A6

	CALLA	IS_A8_IN_CREASE
	JRC	ACHB_POP_IN_FRONT		;BR=INSIDE DA CREASE

	CMPI	BL_POPUP_BEHIND,A4
	JRGT	ATU_CHOOSE_VEL			;BR=POP UP BEHIND GOALIE
ACHB_POP_IN_FRONT
	ADDI	XD1800,A2			;POP UP IN FRONT OF GOALIE
	JRUC	ATU_CHOOSE_VEL

ACHB_FLY_NORM
	MOVI	XD0225,A2
	MOVE	A2,A3
	MOVE	A1,A0
	ADDI	018000H,A0
	SUBI	018000H,A1
ACHB_CHOOSE_YVEL
	CALLA	RANGERND
	MOVE	A0,*A9(OYVEL),L

	MOVI	NORM_GRAVITY,A14
	MOVE	A14,*A9(OYACCEL),W

	MOVB	*A6(POF_DIRECTION),A0
	CALLA	dir_to_degrees
	MOVE	A0,A1
	ADD	A2,A0
	SUB	A3,A1
	CALLA	RANGERND
	THETA_N	A0

	MOVE	*A9(ODT_VEL),A1,L
	SRA	2,A1

	MOVE	A9,A8
	JAUC	set_obj_to_polar

;	MOVE	A9,A8
;	MOVE	*A8(ODT_VEL),A1,L
;	SRA	2,A1
;	JAUC	put_puck_in_other_direction
ACHB_DONE
	RETS

**************************************************************************
*								         *
* A_THROW_UP								 *
*								         *
* PASS:									 *
* A8 = GOALIE OBJECT							 *
*								         *
**************************************************************************

A_THROW_UP
	MOVE	*A8(ODT_GLOVE),A14,L
	CMPI	GL_KEEP_PUCK,A14
	JRGT	ATU_DONE			;BR=KEEP PUCK
	CMPI	GL_HOOK_PUCK,A14
	JRLT	ATU_DONE			;BR=HOOK PUCK

	MOVE	@PUCK_CONTROL,A14,W
	JRZ	ATU_DONE			;BR=NOBODY HAS CONTROL
	MOVE	*A8(ODT_PBK),A6,L
	MOVB	*A6(POF_NUMBER),A0
	CMP	A0,A14
	JRNE	ATU_DONE			;BR=GOALIE DOESN'T HAVE CONTROL

	CLR	A14				;PUM_OPEN
	calla	set_safe_puck_mode_a14
	MOVE	@PUCK_CONTROL,@PUCK_LAST_CONTROL,W
	CLR	A14
	MOVE	A14,@PUCK_INTENDED,W
	MOVE	A14,@PUCK_CONTROL,W
	CALLA	new_puck_control
ATU_GO
	MOVB	*A6(POF_DIRECTION),A0

	MOVE	*A8(OIMG),A7,L
	CMPI	GSK12F06,A7
	JRNE	ATU_NO_KLUDGE			;BR=DON'T KLUDGE THIS ANIMATION

	CMPI	2,A0
	JREQ	ATU_2_KLUDGE			;BR=DO THE LEFT KLUDGE
	MOVI	XD0450,A0			;RIGHT KLUDGE
	CLR	A1
	JRUC	ATU_CHOOSE_DIR
ATU_2_KLUDGE
	MOVI	XD1800,A0			;LEFT KLUDGE
	MOVI	XD1800-XD0450,A1
	JRUC	ATU_CHOOSE_DIR

ATU_NO_KLUDGE
	CALLA	dir_to_degrees
	MOVE	A0,A1
	ADDI	XD1800+XD0225,A0
	ADDI	XD1800-XD0225,A1
ATU_CHOOSE_DIR
	CALLA	RANGERND
	MOVE	A0,A2

	MOVI	1024,A0				;DECIDE WHERE PUCK FLIES
	CALLA	RAND0
	CMPI	410,A0				;60% FLY BEHIND NET
	JRGT	ATU_FLY_BEHIND_NET		;BR=FLY BEHIND NET
	MOVE	A0,A3

	MOVI	010000H,A0
	MOVI	08000H,A1
	MOVI	-050000H,A5
	MOVI	-058000H,A6

	CALLA	IS_A8_IN_CREASE
	JRC	ATU_FLY_IN_FRONT		;BR=INSIDE DA CREASE

	CMPI	205,A3				;50% (OF 60%) FLY BEHIND GOALIE
	JRGT	ATU_CHOOSE_VEL			;BR=FLY BEHIND GOALIE
ATU_FLY_IN_FRONT
	ADDI	XD1800,A2			;FLY IN FRONT OF GOALIE
	JRUC	ATU_CHOOSE_VEL

ATU_FLY_BEHIND_NET
	MOVI	040000H,A0
	MOVI	028000H,A1
	MOVI	-040000H,A5
	MOVI	-058000H,A6
	CMPI	GSK14F05,A7
	JRNE	ATU_CHOOSE_VEL			;BR=MINIMUM VELOCITY OKAY
	MOVI	038000H,A1
ATU_CHOOSE_VEL
	CALLA	RANGERND
	MOVE	A0,A1
	THETA_N	A2
	MOVE	A2,A0
	MOVE	@PUCK_OBJECT,A8,L
	CALLA	set_obj_to_polar

	MOVE	A5,A0
	MOVE	A6,A1
	CALLA	RANGERND
	MOVE	A0,*A8(OYVEL),L

	MOVI	NORM_GRAVITY,A14
	MOVE	A14,*A8(OYACCEL),W
ATU_DONE
	RETS

**************************************************************************
*								         *
* A_HOOK_DOWN								 *
*								         *
* PASS:									 *
* A8 = GOALIE OBJECT							 *
*								         *
**************************************************************************

A_HOOK_DOWN
	MOVE	*A8(ODT_GLOVE),A14,L
	CMPI	GL_KEEP_PUCK,A14
	JRGT	AHD_DONE			;BR=KEEP PUCK

	MOVE	@PUCK_CONTROL,A14,W
	JRZ	AHD_DONE			;BR=NOBODY HAS CONTROL
	MOVE	*A8(ODT_PBK),A6,L
	MOVB	*A6(POF_NUMBER),A0
	CMP	A0,A14
	JRNE	AHD_DONE			;BR=GOALIE DOESN'T HAVE CONTROL

	CLR	A14				;PUM_OPEN
	calla	set_safe_puck_mode_a14
	MOVE	@PUCK_CONTROL,@PUCK_LAST_CONTROL,W
	CLR	A14
	MOVE	A14,@PUCK_INTENDED,W
	MOVE	A14,@PUCK_CONTROL,W
	CALLA	new_puck_control

	CLR	A1				;DIRECTION 3(7) OFFSET
	MOVE	*A8(OIMG),A14,L
	CMPI	GSK23F08,A14
	JREQ	AHD_GET_ANGLE			;BR=DIRECTION 3(7)
	MOVI	XD0450,A1			;DIRECTION 4(6) OFFSET
	CMPI	GSK14F08,A14
	JREQ	AHD_CHECK_SIDE			;BR=DIRECTION 4(6)
	MOVI	XD0900,A1			;DIRECTION 2(8) OFFSET
AHD_CHECK_SIDE
	SUBK	4,A0
	JRZ	AHD_GET_ANGLE			;BR=LEFT GOALIE
	NEG	A1
AHD_GET_ANGLE
	MOVB	*A6(POF_DIRECTION),A0
	CALLA	dir_to_degrees
	ADD	A1,A0
	MOVE	A0,A1
	ADDI	XD0450,A0
	CALLA	RANGERND
	THETA_N	A0
	MOVE	A0,A2

	MOVE	@PUCK_OBJECT,A8,L
	MOVI	060000H,A0
	MOVI	050000H,A1
	CALLA	RANGERND
	MOVE	A0,*A8(OYVEL),L

	MOVI	NORM_GRAVITY,A14
	MOVE	A14,*A8(OYACCEL),W

	MOVI	030000H,A0
	MOVI	010000H,A1
	CALLA	RANGERND
	MOVE	A0,A1
	MOVE	A2,A0
	CALLA	set_obj_to_polar
AHD_DONE
	RETS

**************************************************************************
*								         *
* A_CHECK_SKATEBLOCK							 *
* 									 *
* PASS:									 *
* A8 = GOALIE OBJECT							 *
*								         *
**************************************************************************

A_CHECK_SKATEBLOCK
	MOVE	*A8(ODT_GFLAGS),A14,W
	ANDI	M_SAVEMADE|M_MISSSAVE,A14
	JRNZ	ACSB_DONE			;BR=DON'T SAVE

	MOVE	@PUCK_OBJECT,A9,L

	MOVE	*A8(ODT_PBK),A6,L

	CLR	A0
	MOVE	A0,A1
	CALLR	CHECK_PUCK_IMPACT
	JRZ	ACSB_DONE			;BR=NO PUCK COLLISION

;	MOVB	*A6(POF_DIRECTION),A5
;
;;	calla	get_players_puck_point_o
;;	MOVE	A0,A2				;X POSITION
;;	MOVE	A1,A3				;Z POSITION
;
;;	MOVE	*A8(OXVAL),A2,L
;;	MOVE	*A8(OZVAL),A3,L
;
;	MOVE	*A8(ODT_GXIMPACT),A2,L		;PUCK AT IMPACT POINT
;	MOVE	*A8(ODT_GZIMPACT),A3,L
;
;	MOVE	*A9(OXVAL),A0,L
;	MOVE	*A9(OXVEL),A14,L
;	ADD	A14,A0
;	MOVE	*A9(OZVAL),A1,L
;	MOVE	*A9(OZVEL),A14,L
;	ADD	A14,A1
;	CALLA	find_dir_to_point_18		;FROM GOALIE TO PUCK
;	SUB	A5,A0
;	JRZ	ACSB_DONE			;BR=SAME DIRECTION
;	ABS	A0
;	SUBK	1,A0
;	JRZ	ACSB_DONE			;BR=ADJACENT DIRECTION
;	SUBK	6,A0
;	JRZ	ACSB_DONE			;BR=8 TO 1 CASE

	CALLR	COUNT_SAVE

	MOVE	A2,*A9(OXVAL),L			;JUST PUT IT AT PUCK POINT
	MOVE	A3,*A9(OZVAL),L			; FOR NOW

;	MOVE	*A8(ODT_GXIMPACT),*A9(OXVAL),L	;PUCK PUCK AT IMPACT POINT
;	MOVE	*A8(ODT_GZIMPACT),*A9(OZVAL),L

	MOVE	*A9(OFLAGS),A14,W
	ORI	M_FVELADD,A14
	MOVE	A14,*A9(OFLAGS),W

	MOVI	s_goalie_stick_1,A4
	CALLA	do_sound

	MOVI	XD0150,A0
	CLR	A1
	MOVB	*A6(POF_DIRECTION),A14
	ANDI	3,A14
	SUBK	3,A14
	JRNZ	ACSB_NO37			;BR=NOT DIRECTION 3 OR 7
	MOVI	-XD0300,A1
ACSB_NO37
	MOVI	XD0900,A2
	MOVE	*A8(ODT_GSAVE),A14,L
	CMPI	SET_GGSKATE_SCRIPT,A14
	JREQ	ACSB_CHECK_FLIP			;BR=GLOVE SIDE
	CMPI	SET_GQGSKATE_SCRIPT,A14
	JREQ	ACSB_CHECK_FLIP			;BR=GLOVE SIDE
	NEG	A0
	NEG	A1
	NEG	A2				;STICK SIDE, ROTATE OTHER WAY
ACSB_CHECK_FLIP
	MOVB	*A6(POF_NUMBER),A14
	SUBK	4,A14
	JRZ	ACSB_UPDATE_PUCK		;BR=LEFT GOALIE
	NEG	A0
	NEG	A1
	NEG	A2				;RIGHT GOALIE, ROTATE OTHER WAY
ACSB_UPDATE_PUCK
	CALLA	RANGERND
	MOVE	*A9(ODT_VEL),A1,L
	MOVE	A1,A3
	SRA	3,A3

	MOVE	A2,A2
	JRN	ACSB_OTHER_MA_CHECK		;BR=DO THE OTHER ONE
	MOVE	A0,A0
	JRP	ACSB_MODIFY_ANGLE		;BR=NOT GOING BEHIND
	JRUC	ACSB_QUICKER
ACSB_OTHER_MA_CHECK
	MOVE	A0,A0
	JRN	ACSB_MODIFY_ANGLE		;BR=NOT GOING BEHIND
ACSB_QUICKER
	MOVE	*A9(OYPOS),A14,W
	JRNZ	ACSB_MODIFY_ANGLE		;BR=HIT THE PAD
	SRA	1,A3
ACSB_MODIFY_ANGLE
	ADD	A0,A2

	MOVE	*A9(ODT_DIR),A0,L
	ADD	A2,A0
	SUB	A3,A1
	MOVE	A9,A8

	CMPI	MAX_SAVE_PUCK_SPEED,A1
	JALE	SEND_PUCK_POLAR			;BR=UNDER OR AT THE SPEED LIM

	MOVI	MAX_SAVE_PUCK_SPEED,A1
	JAUC	SEND_PUCK_POLAR
ACSB_DONE
	RETS

**************************************************************************
*								         *
* A_QPADST - QUICK PAD STACK SETUP					 *
* 									 *
* PASS:									 *
* A8 = GOALIE OBJECT							 *
*								         *
**************************************************************************

A_QPADST
	CALLR	A_NOPUCKCHECK
	CALLA	A_SET_OXZVEL
	CALLR	A_SETFOCIS
	CALLA	A_ANIMFRM
	JAUC	A_CHECK_BUTTERFLY




	.end
