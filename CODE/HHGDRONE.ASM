 	.MLIB	"HHMACS.LIB"
	.FILE	"HHGDRONE.ASM"
	.WIDTH	132
	.OPTION	B,D,L,T
	.MNOLIST

*
*GET THE SYSTEM STUFF
*

	.INCLUDE	"HH.INC"
	.INCLUDE	"HHSTRING.H"
	.INCLUDE	"IMGTBL.GLO"
	.include	"hhgdrone.e"
	.include	"hhplayer.g"
	.include	"hhmath.g"
	.include	"hhcontrl.g"
	.include	"hhpuck.g"
	.include	"hhgscr.g"
	.include	"hhram.g"
	.include	"hhmisc.g"
	.include	"hhgctrl.g"
	.include	"hhhigher.g"
	.include	"hhspeech.g"

; end of include files

	.TEXT
	.EVEN

**************************************************************************
*								         *
* DO_GOALIE_DRONE_LOGIC							 *
*								         *
**************************************************************************

DO_GOALIE_DRONE_LOGIC
	MMTM	SP,A6,A8

	MOVI	DRG_FETCH_PUCK,A0
	MOVE	@PUCK_CONTROL,A1,W
	JRZ	DGDL_CALL			;BR=NOBODY HAS CONTROL

	MOVI	DRG_POSITION_TO_SAVE,A0
	MOVB	*A6(POF_NUMBER),A14
	CMP	A1,A14
	JRNE	DGDL_CALL			;BR=GOALIE DOESN'T HAVE PUCK

	MOVI	DRG_PASS,A0
	MOVI	PLAYER_1_BLOCK,A1
	MOVI	PLAYER_2_BLOCK,A2
	SUBK	4,A14
	JRZ	DGDL_CHECK_PASS			;BR=LEFT GOALIE
	MOVI	PLAYER_5_BLOCK,A1
	MOVI	PLAYER_6_BLOCK,A2
DGDL_CHECK_PASS
	MOVE	*A1(POF_FLAGS),A14,W
	BTST	B_PF_SIG_PASS,A14
	JRNZ	DGDL_CALL			;BR=PASS REQUEST
	MOVE	*A2(POF_FLAGS),A14,W
	BTST	B_PF_SIG_PASS,A14
	JRNZ	DGDL_CALL			;BR=PASS REQUEST

	MOVI	DRG_GO_HOME,A0
DGDL_CALL
	CALL	A0

	MMFM	SP,A6,A8
	RETS

**************************************************************************
*								         *
* PUCK (POSSESSION)							 *
* TIME (LEFT)								 *
* LOCATION (OF DRONE)							 *
* TEAM POSITION (OF TEAM MATE CLOSEST TO THE OPPOSING GOAL)		 *
* PUCK LOCATION								 *
* SIGNAL (FROM TEAM MATE)						 *
* SCORE (DIFFERENCE)							 *
*								         *
*								         *
* PUCK 1 us     TIME 1 plenty     LOCATION 1 our side   TEAM POS. 1 our	 *
*      2 team        2 not much	           2 center ice		  2 cntr *
*      3 opponent    3 very little	   3 other side		  3 othr *
*      4 nobody								 *
* 									 *
* PUCK LOC. 1 our side    SIGNAL 1 nothing	SCORE  1 ahead more	 *
* 	    2 center ice	 2 pass		       2 ahead one	 *
* 	    3 other side	 3 shoot	       3 tied		 *
* 			         		       4 behind one	 *
* 			         		       5 behind more	 *
* 									 *
*  WWWWWWWL PUCK,TIME,LOCATION,TEAM_POS,PUCK_LOC,SIGNAL,SCORE,address	 *
*								         *
**************************************************************************
;		 P T L T P S S
;		 U I O E L I C
;		 C M C A O G O
;		 K E   M C   R
;DRONE_GOALIE_TOP_LEVEL
;;	WWWWWWWL 0,0,0,0,3,0,0,DRG_REST
;	WWWWWWWL 4,0,0,0,1,0,0,DRG_FETCH_PUCK
;	WWWWWWWL 1,0,0,0,0,2,0,DRG_PASS
;	WWWWWWWL 1,0,0,0,0,0,0,DRG_GO_HOME
;	WWWWWWWL 0,0,0,0,1,0,0,DRG_POSITION_TO_SAVE
;;	WWWWWWWL 0,0,0,0,2,0,0,DRG_POSITION_TO_SAVE
;	WWWWWWWL 0,0,0,0,0,0,0,DRG_NONE

;**************************************************************************
;*								         *
;* DRG_NONE - GOALIE DO NUTIN'						 *
;* 									 *
;* PASS:									 *
;* A8 = GOALIE OBJECT							 *
;* RETURN:								 *
;* NUTIN'								 *
;*								         *
;**************************************************************************
;
;DRG_NONE
;	RETS

;**************************************************************************
;*								         *
;* PLAYER_LOC - FIND LOCATION OF PLAYER OBJECT				 *
;* OBJECT_LOC - FIND LOCATION OF OBJECT					 *
;* 									 *
;* PASS:									 *
;* A6 = PLAYER DATA (PLAYER_LOC ONLY)					 *
;* A8 = OBJECT	   (OBJECT_LOC ONLY)					 *
;* RETURN:								 *
;* A1 = LOCATION ( -1 = LEFT SIDE, 0 = CENTER, 1 = RIGHT SIDE)		 *
;*								         *
;**************************************************************************
;
;PLAYER_LOC
;	PUSH	A8
;	MOVE	*A6(POF_OBJECT),A8,L
;	JRUC	OL_GO
;OBJECT_LOC
;	PUSH	A8
;OL_GO
;	MOVE	*A8(OXVAL),A14,L
;	MOVI	-1,A1
;	CMPI	LEFT_BLUELINE_X,A14
;	JRLT	OL_DONE				;BR = ON THE LEFT
;	INC	A1
;	CMPI	RGHT_BLUELINE_X,A14
;	JRLE	OL_DONE				;BR = IN THE CENTER
;	INC	A1				;ON THE RIGHT
;OL_DONE
;	PULLQ	A8
;	RETS

**************************************************************************
*								         *
* DRG_FETCH_PUCK - GOALIE FETCH A STRAY PUCK				 *
* 									 *
* PASS:									 *
* A6 = PLAYER DATA							 *
* A8 = GOALIE OBJECT							 *
* RETURN:								 *
* NUTIN'								 *
*								         *
**************************************************************************

DRG_FETCH_PUCK
	MOVE	@PUCK_MODE,A14,W
	SUBI	PUM_SCORE,A14
	JRN	DRG_FP_CHECK_FACEOFF		;BR=NOT SPECIAL
	JRNZ	DRG_POSITION_TO_SAVE		;BR=NO SCORE

	CLR	A14
	MOVE	A14,*A8(ODT_VEL),L
	MOVE	A14,*A8(OXVEL),L
	MOVE	A14,*A8(OZVEL),L

	RETS

DRG_FP_CHECK_FACEOFF
	MOVE	@faceoff_time,A0,L
	MOVE	@WAVEIRQS,A14,L
	SUB	A0,A14
	SUBK	10,A14
	JRN	DRG_POSITION_TO_SAVE		;BR=STILL AT FACEOFF

	MOVE	@PUCK_OBJECT,A7,L

	MOVE	*A7(ODT_VEL),A14,L
	CMPI	GSLOW_PUCK,A14
	JRGT	DRG_POSITION_TO_SAVE		;BR=MOVING TOO FAST

	MOVE	*A7(OYVAL),A14,L
	CMPI	MAX_GAIN_POSSY,A14
	JRLT	DRG_POSITION_TO_SAVE		;BR=TOO HIGH IN THE AIR

	MOVI	CREASE_RADIUS+0200000H,A14

	MOVE	*A7(OXVAL),A0,L
	ABS	A0

	CMPI	RGHT_GOALLINE_X+GOAL_DEPTH_X,A0
	JRGT	DRG_POSITION_TO_SAVE		;BR=TOO FAR

	MOVI	RGHT_GOALLINE_X,A1
	SUB	A14,A1
	CMP	A1,A0
	JRLT	DRG_POSITION_TO_SAVE		;BR=TOO FAR

	MOVE	*A7(OZVAL),A0,L

	MOVI	CENTER_Z,A1
	SUB	A14,A1
	CMP	A1,A0
	JRLT	DRG_POSITION_TO_SAVE		;BR=TOO FAR

	ADD	A14,A1
	ADD	A14,A1
	CMP	A1,A0
	JRHI	DRG_POSITION_TO_SAVE		;BR=TOO FAR

	MOVE	*A8(ODT_GFLAGS),A14,W
	BTST	B_FETCHING,A14
	JRNZ	DRG_FP_CONTINUE			;BR=ALREADY STARTED GOING HOME

	ANDNI	M_GOHOME,A14
	ORI	M_FETCHING,A14
	MOVE	A14,*A8(ODT_GFLAGS),W

	CLR	A14
	MOVE	A14,*A8(ODT_VEL),L
	MOVE	A14,*A8(OXVEL),L
	MOVE	A14,*A8(OZVEL),L

DRG_FP_CONTINUE
	CLR	A4				;TURBO OFF

;	MOVE	B0,A0
;	MOVE	B1,A1

	MOVE	*A7(OXVAL),A0,L
	MOVE	*A7(OZVAL),A1,L

;	MOVE	*A8(ODT_PBK),A6,L

	MOVE	A0,A14
	ABS	A14
	CMPI	RGHT_GOALLINE_X,A14
	JRLE	DRG_FP_GETIT			;BR=PUCK IN FRONT OF GOAL LINE

	CMPI	BOT_GOALPOST_Z,A1
	JRGE	DRG_FP_TOP			;BR=PUCK BEHIND OR ABOVE GOAL

	MOVE	*A8(OZVAL),A14,L
	CMPI	BOT_GOALPOST_Z,A14
	JRLT	DRG_FP_GETIT			;BR=GOALIE BELOW THEE GOAL

	MOVI	XD2700,A0
;	MOVI	XD3500,A0
	JRUC	DRG_PTS_DIR_SET			;MOVE STRAIGHT DOWN
DRG_FP_TOP
	CMPI	TOP_GOALPOST_Z,A1
	JRLE	DRG_POSITION_TO_SAVE		;BR=PUCK BEHIND THE GOAL

	MOVE	*A8(OZVAL),A14,L
	CMPI	TOP_GOALPOST_Z,A14
	JRGT	DRG_FP_GETIT			;BR=GOALIE ABOVE THEE GOAL

	MOVI	XD0900,A0
;	MOVI	XD0100,A0
	JRUC	DRG_PTS_DIR_SET			;MOVE STRAIGHT UP

;	RETS

DRG_FP_GETIT
	MOVE	A0,A9
	MOVE	A1,A10
	CALLA	get_players_puck_point_o
	MOVE	A0,A2
	MOVE	A1,A3
	MOVE	A9,A0
	MOVE	A10,A1

	CALLA	find_dirdis_to_point		;FROM CURRENT POSITION TO NEW

;	MOVE	*A7(OYVAL),A14,L
;	CMPI	MAX_GAIN_POSSY,A14
;	JRLT	DRG_FP_CHECK_STOP		;BR=TOO HIGH IN THE AIR

;	CMPI	[8,0],A1
	CMPI	[16,0],A1
;	JALE	cpip_control
	JRGT	DRG_FP_CHECK_STOP

	MOVB	*A6(POF_IGNORE_PUCK),A14
	JRNZ	DRG_POSITION_TO_SAVE		;BR=IGNORE PUCK
	MOVE	@PUCK_OPEN_CONTROL_TIME,A0,W
	JRZ	DRG_FP_CTRL			;BR=DON'T CARE ABOUT LAST CONTRL
	MOVE	@PUCK_LAST_CONTROL,A0,W
	CMP	A0,A3
	JREQ	DRG_POSITION_TO_SAVE		;BR=GOALIE CONTROLLED PUCK LAST
DRG_FP_CTRL

	MOVK	GS_GAINS_POSS,A0
	CALLA	GOALIE_SPEECH

	MOVE	@WAVEIRQS,A14,L
	MOVE	A14,*A8(ODT_GPOSSIRQ),L		;IRQ TIME OF POSSESSION
	MOVE	A10,*A8(ODT_GZPOSPOSS),L	;Z POSITION
;	JAUC	cpip_control
	JAUC	GET_PUCK_CONTROL

DRG_FP_CHECK_STOP
;	MOVE	*A8(ODT_VEL),A14,L
;	CMP	A1,A14
;	JAGT	stop_player			;BR=NOT FAR FROM STOPPING POINT
;
;	JRUC	DRG_PTS_DIR_SET

	MOVE	A1,A1
	JRZ	DRG_PTS_ZERO_VELS		;BR=STOP!

	JRUC	DRG_PTS_CHECK_MIN


**************************************************************************
*								         *
* DRG_GO_HOME - GOALIE HAS PUCK AND SHOULD GO HOME			 *
* 									 *
* PASS:									 *
* A6 = PLAYER DATA							 *
* A8 = GOALIE OBJECT							 *
* RETURN:								 *
* NUTIN'								 *
*								         *
**************************************************************************

DRG_GO_HOME
;	MOVE	*A8(ODT_PBK),A6,L
;	CLR	A14				;MOVK	PM_GSTAND,A14
;	MOVE	A14,*A6(POF_MODE),W

	MOVE	*A6(POF_MODE),A14,W
	CMPI	PM_GHOLDING,A14
	JREQ	DRG_GH_DONE			;BR=HOLDING PUCK

	MOVE	*A8(ODT_GFLAGS),A14,W
	BTST	B_GOHOME,A14
	JRNZ	DRG_GH_CONTINUE			;BR=ALREADY STARTED GOING HOME

	ANDNI	M_FETCHING,A14
	ORI	M_GOHOME,A14
	MOVE	A14,*A8(ODT_GFLAGS),W

	CLR	A14
	MOVE	A14,*A8(ODT_VEL),L
	MOVE	A14,*A8(OXVEL),L
	MOVE	A14,*A8(OZVEL),L

DRG_GH_CONTINUE
;	CLR	A4				;TURBO OFF
	MOVK	1,A4				;TURBO ON

	MOVE	*A8(OXVAL),A2,L
	MOVE	A2,A14
	ABS	A14
;;	CMPI	LEFT_GOALLINE_X,A14
;	CMPI	-RGHT_GOALLINE_X,A14
	CMPI	RGHT_GOALLINE_X,A14
	JRLT	DRG_GH_NONET			;BR=NOT BEHIND GOAL LINE

;	MOVE	*A8(ODT_PBK),A6,L

	CLR	A0
	MOVE	A2,A2
	JRN	DRG_PTS_DIR_SET			;BR=GO LEFT
	MOVI	XD1800,A0
	JRUC	DRG_PTS_DIR_SET			;GO RIGHT
DRG_GH_NONET
	MOVI	CENTER_Z,A1
	MOVI	RGHT_GOALLINE_X-[53,0],A0
	MOVE	A2,A2
	JRNN	DRG_PTS_SET_VELS		;BR=ON THE RIGHT SIDE
	NEG	A0
	JRUC	DRG_PTS_SET_VELS
DRG_GH_DONE
	RETS

**************************************************************************
*								         *
* DRG_POSITION_TO_SAVE - GOALIE MOVE INTO POSITION TO SAVE (BLOCK)	 *
* 									 *
* PASS:									 *
* A6 = PLAYER DATA							 *
* A8 = GOALIE OBJECT							 *
* RETURN:								 *
* NUTIN'								 *
*								         *
**************************************************************************

DRG_POSITION_TO_SAVE
;	MOVE	*A8(ODT_PBK),A6,L
;	MOVK	PM_GSTANCE,A14
;	MOVE	A14,*A6(POF_MODE),W

	.if	DEBUG

	JRUC	DRG_PTS_NO_FREEZE
	CLR	A14
	MOVE	A14,*A8(ODT_VEL),L
	MOVE	A14,*A8(OXVEL),L
	MOVE	A14,*A8(OZVEL),L
	RETS
DRG_PTS_NO_FREEZE

	.endif

	MOVB	*A6(POF_NUMBER),A14
	MOVE	A14,B2

	MOVE	*A8(ODT_GFLAGS),A4,W
	ANDNI	M_GOHOME|M_FETCHING,A4
	MOVE	A4,*A8(ODT_GFLAGS),W
	
	SLL	30,A4
	SRL	31,A4				;TURBO FLAG

	MOVE	@PUCK_OBJECT,A7,L

	MOVE	@PUCK_CONTROL,A14,W
	JRZ	DRG_PTS_TARGET			;BR=TARGET PUCK
			
	GBLOCK	A14,A14
	MOVE	*A14(POF_OBJECT),A0,L
	MOVE	*A0(ODT_VEL),A14,L
	JRNZ	DRG_PTS_TARGET			;BR=MOVIN'
	MOVE	A0,A7				;TARGET PLAYER

DRG_PTS_TARGET
	MOVE	*A7(OXVAL),A0,L
	MOVE	A0,B0
	MOVE	*A7(OZVAL),A1,L
	MOVE	A1,B1

	MOVE	A0,A14
	ABS	A14
	CMPI	RGHT_GOALLINE_X,A14
	JRLT	DRG_PTS_ARC			;BR=NOT BEHIND GOAL LINE

	MOVE	@PUCK_OBJECT,A7,L
	MOVE	*A7(OZVAL),A2,L

	MOVK	1,A4				;TURBO ON
	MOVI	RGHT_GOALIE_AT_GOAL+050000H,A0  ;070000H
;	MOVI	BOT_GOALPOST_Z-1,A1		;HUG BOTTOM GOALPOST
	MOVI	01220001H,A1
;	CMPI	CENTER_Z,B1
;	JRLE	DRG_PTS_X_TO_B			;BR=IN BOTTOM HALF
	CMPI	CENTER_Z,A2
	JRLE	DRG_PTS_CHECK_X			;BR=IN BOTTOM HALF
	MOVI	RGHT_GOALIE_AT_GOAL+0B0000H,A0
;	MOVI	TOP_GOALPOST_Z+1,A1		;HUG TOP GOALPOST
	MOVI	017FFFFFH,A1
;DRG_PTS_X_TO_B
;	MOVE	A1,B1
DRG_PTS_CHECK_X

;	MOVI	RGHT_GOALIE_AT_GOAL+050000H,A0  ;070000H
	MOVE	*A8(OXVAL),A14,L
	JRNN	DRG_PTS_CHECK_HUG		;BR=ON THE RIGHT SIDE
	NEG	A0

DRG_PTS_CHECK_HUG
	MOVE	A0,A9
	MOVE	A1,A10
	MOVE	*A8(OXVAL),A2,L
	MOVE	*A8(OZVAL),A3,L
	CALLA	find_dirdis_to_point		;FROM CURRENT POSITION TO NEW

;	MOVE	*A8(ODT_PBK),A6,L

	MOVE	A1,A1
	JRZ	DRG_PTS_HUG			;BR=NOT MOVIN'

	MOVE	*A6(POF_MODE),A14,W
	CMPI	PM_GHUGPOST,A14
	JREQ	DRG_PTS_SHUFFLE			;BR=HUGGIN' POST
	SUBK	PM_GSIDESHUFF,A14
	JRNZ	DRG_PTS_CHECK_MIN		;BR=NOT SHUFFLN'

DRG_PTS_SHUFFLE
	CALLR	DRG_PTS_CHECK_MIN
	MOVK	PM_GSIDESHUFF,A5
	MOVE	A5,*A6(POF_MODE),W
	RETS

DRG_PTS_HUG
	MOVK	PM_GHUGPOST,A5
	MOVE	A5,*A6(POF_MODE),W

	MOVE	A9,*A8(OXVAL),L
	MOVE	A10,*A8(OZVAL),L
	JRUC	DRG_PTS_ZERO_VELS

DRG_PTS_ARC
;	MOVI	LEFT_GOALLINE_X,A2
	MOVI	RGHT_GOALLINE_X,A2
	MOVE	*A8(OXVAL),A14,L
	JRNN	DRG_PTS_LEFT			;BR=ON THE RIGHT SIDE
	NEG	A2
DRG_PTS_LEFT
	MOVI	CENTER_Z,A3
	CALLA	find_dirdis_to_point		;FROM CENTER OF GOAL TO PUCK

	;A2 AND A3 STILL INTACT

	;A1=RADIUS 53 TO 166

;	SUBI	[75,0],A1
;	MOVI	[166,0],A14
;	CMP	A14,A1
;	JRLE	DRG_PTS_GETOFFS
;	MOVE	A14,A1
;DRG_PTS_GETOFFS

	CMPI	CREASE_RADIUS,A1
	JRGT	DRG_OUTSIDE			;BR=OUTSIDE RADIUS

	JRUC	DRG_PTS_CLOSE

;	MOVE	@PUCK_CONTROL,A2,W
;	JRZ	DRG_PTS_CLOSE			;BR=NOBODY HAS THE PUCK
;
;	MOVE	A0,B10
;	MOVE	A2,A0
;	GBLOCK	a0,a5
;	MOVE	B10,A0
;	MOVE	*A5(POF_MODE),A14,W
;	CMPI	PM_ONETIME,A14
;	JREQ	DRG_PTS_CLOSE			;BR=ONE TIMER CASE
;;	JREQ	DRG_OUTSIDE			;BR=ONE TIMER CASE
;
;	MOVE	B2,A1
;	EQTEAM	A2,A1
;	JREQ	DRG_PTS_CLOSE			;BR=SAME TEAM
;
;;	MOVE	A0,B10
;;TRASHING A0,A1
;	calla	get_players_puck_point_o
;	MOVE	B0,A2
;	MOVE	B1,A3
;	CALLA	find_dsquared_to_point_whole_a1
;	MOVE	B10,A0
;	CMPI	0300H,A1
;	JRGT	DRG_PTS_CLOSE			;BR=TOO FAR TO STEAL
;
;	CLR	A14
;	MOVE	A14,*A8(ODT_VEL),L
;	MOVE	A14,*A8(OXVEL),L
;	MOVE	A14,*A8(OZVEL),L
;
;	MOVK	PM_GDEFLECT,A14
;	MOVE	A14,*A6(POF_MODE),W
;	CALLA	take_player_control
;	JAUC	SET_GDEFLECT_SCRIPT
;DRG_NOSTEAL
;	MOVE	B10,A0
;	JRUC	DRG_PTS_CLOSE

DRG_OUTSIDE
	MOVE	A1,A6
	MOVI	POSITION_RADIUS,A1

	MOVE	B0,B0
	JRNN	DRG_CHECK_RIGHT_ANGLE		;BR=ON THE RIGHT SIDE

	THETA_H	A0

	CMPI	XD0700,A0
	JRLE	DRG_CHECK_LEFT_BOTTOM_EXTREME	;BR=WITHIN TOP EXTREME ANGLE
	MOVI	XD0700,A0
	JRUC	DRG_ANGLE_OKAY

DRG_CHECK_LEFT_BOTTOM_EXTREME
	CMPI	-XD0700,A0
	JRGE	DRG_ANGLE_OKAY			;BR=WITHIN BOTTOM EXTREME ANGLE
	MOVI	-XD0700,A0
	JRUC	DRG_ANGLE_OKAY

DRG_CHECK_RIGHT_ANGLE
	CMPI	XD1100,A0
	JRGE	DRG_CHECK_RIGHT_BOTTOM_EXTREME	;BR=WITHIN TOP EXTREME ANGLE
	MOVI	XD1100,A0
	JRUC	DRG_ANGLE_OKAY

DRG_CHECK_RIGHT_BOTTOM_EXTREME
	CMPI	XD2500,A0
	JRLE	DRG_ANGLE_OKAY			;BR=WITHIN BOTTOM EXTREME ANGLE
	MOVI	XD2500,A0

DRG_ANGLE_OKAY

	JRUC	DRG_PTS_GET_DEST		;DISABLE BREAKAWAY

;	MOVI	BREAKON4,A7
	MOVE	*A8(OID),A14
	CMPI	OID_PLAYER_4,A14
	JREQ	DRG_PTS_GET_FLAG		;BR=THIS IS PLAYER 4
	ADDK	16,A7				;OFFSET TO PLAYER 8 FLAG
DRG_PTS_GET_FLAG
	MOVE	*A7,A14,W
	JRZ	DRG_PTS_GET_DEST		;BR=NO BREAKAWAY

	MOVE	A0,B0
	CALLA	GETCOS
	ABS	A0

	SUBI	[75,0],A6
	MOVI	[166,0],A14
	CMP	A14,A6
	JRLE	DRG_PTS_COMOFF			;BR=BACKUP TO THE GOAL BY [75,0]
	MOVE	A14,A6
DRG_PTS_COMOFF
	SUBI	GOAL_RADIUS,A6			;MAX IS OFF [166,0]-GOAL_RADIUS
	MPYS	A0,A6
	SLL	16,A6
	SRL	16,A7
	MOVX	A7,A6
;	ABS	A6
	ADD	A6,A1
	MOVE	B0,A0
DRG_PTS_GET_DEST
	CALLA	polar_to_rect
	ADD	A2,A0
	ADD	A3,A1

	JRUC	DRG_PTS_SET_VELS
DRG_PTS_CLOSE
	MOVE	B1,A1
;	MOVE	A1,B3				;PUCK Z

	CALLA	IS_A8_IN_CREASE
	JRC	DRG_PTS_SET_CLOSE_X		;BR=YES
	MOVK	1,A4				;TURBO ON

DRG_PTS_SET_CLOSE_X
	MOVI	RGHT_GOALIE_AT_GOAL,A0
	MOVE	*A8(OXVAL),A14,L
	JRNN	DRG_PTS_SET_VELS		;BR=ON THE RIGHT SIDE
	NEG	A0

DRG_PTS_SET_VELS
;NEEDS A0 = DESTINATION OXVAL
;      A1 = DESTINATION OZVAL
;      A4 = TURBO FLAG
	MOVE	*A8(OXVAL),A2,L
	MOVE	*A8(OZVAL),A3,L
	CALLA	find_dirdis_to_point		;FROM CURRENT POSITION TO NEW

	MOVE	*A8(ODT_PBK),A6,L

	MOVE	A1,A1
	JRNZ	DRG_PTS_CHECK_MIN

DRG_PTS_ZERO_VELS
	CLR	A14
	MOVE	A14,*A8(ODT_VEL),L
	MOVE	A14,*A8(OXVEL),L
	MOVE	A14,*A8(OZVEL),L
	RETS

DRG_PTS_CHECK_MIN
	MOVE	*A8(ODT_VEL),A14,L
;	CMP	A1,A14
;	JAGT	stop_player			;BR=NOT FAR FROM STOPPING POINT
	CMP	A1,A14
	JRLE	DRG_PTS_DIR_SET

	PUSH	A1
	MOVE	A4,A1				;TURBO FLAG
	CALLA	move_player_dir
	PULLQ	A1

	MOVE	*A8(ODT_VEL),A14,L
	CMP	A1,A14
	JRLE	DRG_ABORT
	MOVE	A1,*A8(ODT_VEL),L
	RETS

DRG_PTS_DIR_SET
;NEEDS A0 = DIRECTION
;      A4 = TURBO FLAG
;      A6 = PLAYER DATA
	MOVE	A4,A1				;TURBO FLAG
	JAUC	move_player_dir

;	MOVE	A1,A14
;	SRA	3,A1
;	JRNZ	DRG_PTS_SET_OBJ
;	MOVE	A14,A1
;
;DRG_PTS_SET_OBJ
;	CALLA	set_obj_to_polar
DRG_ABORT
	RETS

;**************************************************************************
;*								         *
;* DRG_REST - GOALIE REST IN FRONT OF GOAL				 *
;* 									 *
;* PASS:									 *
;* A8 = GOALIE OBJECT							 *
;* RETURN:								 *
;* NUTIN'								 *
;*								         *
;**************************************************************************
;
;DRG_REST
;;	MOVE	*A8(ODT_PBK),A6,L
;;	CLR	A14				;MOVK	PM_GSTAND,A14
;;	MOVE	A14,*A6(POF_MODE),W
;
;	MOVI	RGHT_GOALIE_AT_GOAL,A2
;	MOVE	*A8(OXVAL),A14,L
;	JRNN	DRG_R_SETX			;BR=ON THE RIGHT SIDE
;	NEG	A2
;DRG_R_SETX
;	MOVE	A2,*A8(OXVAL),L
;	MOVI	CENTER_Z,A14
;	MOVE	A14,*A8(OZVAL),L
;	RETS

**************************************************************************
*								         *
* DRG_PASS - GOALIE PASS TO PLAYER					 *
* 									 *
* PASS:									 *
* A6 = PLAYER DATA							 *
* A8 = GOALIE OBJECT							 *
* RETURN:								 *
* NUTIN'								 *
*								         *
**************************************************************************

**************************************************************************
*								         *
* GOALIE_PASS_PUCK - GOALIE PASS TO PLAYER				 *
* 									 *
* PASS:									 *
* A6 = GOALIE BLOCK							 *
* A8 = GOALIE OBJECT							 *
* RETURN:								 *
* C SET, IF NO PASS, OTHERWISE CLR					 *
*								         *
**************************************************************************

DRG_PASS
;	MOVE	*A8(ODT_PBK),A6,L
GOALIE_PASS_PUCK
	MOVB	*A6(POF_NUMBER),A0
	CALLA	get_pass_receiver
	GBLOCK	a0,a7
;GOALIE_PASS_PUCK_PA7
	CLR	A14
	MOVE	A14,*A8(ODT_VEL),L
	MOVE	A14,*A8(OXVEL),L
	MOVE	A14,*A8(OZVEL),L

	MOVE	A8,A2	
	MOVE	*A7(POF_OBJECT),A8,L

;	MOVE	*A8(OXVAL),A0,L
;	ABS	A0
;	CMPI	RGHT_GOALLINE_X,A0
;	JRGT	DRG_P_NOPASS			;BR=RECEIVER BEHIND GOAL LINE

	CALLA	get_players_puck_point_o
	MOVE	A0,A14
	ABS	A14
	CMPI	RGHT_GOALLINE_X,A14
	JRGT	DRG_P_NOPASS			;BR=PUCK POINT BEHIND GOAL LINE

	MOVE	A2,A8
	MOVE	*A8(OXVAL),A2,L
	MOVE	*A8(OZVAL),A3,L
	MOVI	GOALIE_DIR_TABLE,A11
	CALLA	GET_GDIR_TO_POINT_18_T
	MOVB	A11,*A6(POF_DIRECTION)

;	CLR	A14
;	MOVE	A14,*A8(ODT_GPOSSIRQ),L		;RESET IRQ TIME OF POSSESSION

	MOVK	1,A1				;turbo on
	CALLA	pass_puck
	CLRC					;FLAG PASS
	RETS
DRG_P_NOPASS
	MOVE	A2,A8

	CALLA	clear_all_signals

;	MOVE	*A7(POF_FLAGS),A14,W		;CLEAR PASS SIGNAL
;	ANDNI	M_PF_SIG_PASS,A14
;	MOVE	A14,*A7(POF_FLAGS),W

	SETC					;FLAG NO PASS
	RETS

	.end


;	JRUC	DRG_FP_SETDIR
;
;
;	MOVE	@PUCK_MODE,A14,W
;	JRZ	DRG_FP_CHECK_PUCK_SPEED		;BR=OPEN PUCK
;	CMPI	15,A14
;	JRNE	DRG_POSITION_TO_SAVE		;BR=DUMPED
;DRG_FP_CHECK_PUCK_SPEED
;	MOVE	@PUCK_OBJECT,A7,L		;GET PUCK LOCATION
;	MOVE	*A7(ODT_VEL),A14,L
;	CMPI	GSLOW_PUCK,A14
;	JRGT	DRG_POSITION_TO_SAVE		;BR=MOVING TOO FAST
;
;	MOVE	*A7(OYPOS),A14,W
;	JRNZ	DRG_POSITION_TO_SAVE		;BR=PUCK IN THE AIR
;
;	MOVE	*A8(ODT_PBK),A5,L
;
;	MOVB	*A5(POF_NUMBER),A14
;
;;	MOVB	*A5(POF_IGNORE_PUCK),A0
;;	CLR	A14
;;	MOVB	A14,*A5(POF_IGNORE_PUCK)	;DON'T IGNORE PUCK
;;
;;	CALLA	find_closest_player_to_puck_r
;;
;;	MOVB	A0,*A5(POF_IGNORE_PUCK)
;;
;;	MOVE	A3,A3
;;	JRN	DRG_POSITION_TO_SAVE		;BR=NOBODY
;;
;;	MOVB	*A5(POF_NUMBER),A14
;;	CMP	A3,A14				;DON'T TRASH A14!
;;	JRNE	DRG_POSITION_TO_SAVE		;BR=NOT THE CLOSEST
;;
;;	MOVE	*A7(OXVAL),A0,L			;CHECK IF PUCK IN CLOSE BOX
;;	MOVE	A0,B0
;;	ABS	A0				;PUCK AND GOALIE SAME SIDE
;;	MOVE	*A7(OZVAL),A1,L
;;	MOVE	A1,B1
;;
;;	CMPI	RGHT_GOALLINE_X+GOAL_DEPTH_X,A0
;;	JRGT	DRG_FP_FAR_CHECK		;BR=OUTSIDE CLOSE BOX
;;
;;	CMPI	RGHT_GOALLINE_X-(CREASE_RADIUS+[48,0]),A0
;;	JRLT	DRG_FP_FAR_CHECK		;BR=OUTSIDE CLOSE BOX
;;
;;	CMPI	CENTER_Z-(CREASE_RADIUS+[32,0]),A1
;;	JRLT	DRG_FP_FAR_CHECK		;BR=OUTSIDE CLOSE BOX
;;
;;	CMPI	CENTER_Z+(CREASE_RADIUS+[32,0]),A1
;;	JRLT	DRG_FP_SETDIR			;BR=IN CLOSE BOX
;
;DRG_FP_FAR_CHECK
;;	CLR	A9				;# OF OFFENSIVE PLAYERS IN ZONE
;	MOVE	*A5(POF_D_PUCK),A4,W		;DISTANCE FROM GOALIE TO PUCK
;
;	CMPI	16,A4
;	JRLE	DRG_FP_SETDIR			;BR=CLOSE ENOUGH
;
;	ADDI	64,A4
;	MOVK	1,A0				;CHECK LEFT SIDE OFFENSE
;	MOVK	5,A2
;	MOVK	1,A3				;RIGHT SIDE FLAG
;	SUBK	4,A14
;	JRNZ	DRG_FP_OTHER_SIDE		;BR=GOALIE ON OTHER SIDE BUB
;	MOVK	5,A0				;CHECK RIGHT SIDE OFFENSE
;	MOVK	1,A2
;	NEG	A3				;LEFT SIDE FLAG
;DRG_FP_OTHER_SIDE
;	GBLOCK	a0,a6
;
;;	MOVE	*A6(POF_OBJECT),A14,L		;OPPOSING OFFENSE
;;	MOVE	*A14(OFLAGS),A14,W
;;	BTST	B_OFSCRN,A14
;;	JRZ	DRG_POSITION_TO_SAVE		;BR=ON SCREEN, DON'T GO
;
;	MOVE	*A6(POF_D_PUCK),A14,W		;OPPOSING OFFENSE
;	CMP	A4,A14
;	JRLE	DRG_POSITION_TO_SAVE		;BR=CLOSE ENOUGH, DON'T GO
;
;;	CALLR	PLAYER_LOC
;;	CMP	A3,A1
;;	JRNE	DRG_FP_NEXT0			;BR=NOT ON THIS SIDE
;;	INC	A9
;DRG_FP_NEXT0
;	ADDI	PLAYER_BLOCK_SIZE,A6
;
;;	MOVE	*A6(POF_OBJECT),A14,L		;OPPOSING OFFENSE
;;	MOVE	*A14(OFLAGS),A14,W
;;	BTST	B_OFSCRN,A14
;;	JRZ	DRG_POSITION_TO_SAVE		;BR=ON SCREEN, DON'T GO
;
;	MOVE	*A6(POF_D_PUCK),A14,W		;OPPOSING OFFENSE
;	CMP	A4,A14
;	JRLE	DRG_POSITION_TO_SAVE		;BR=CLOSE ENOUGH, DON'T GO
;
;;	CALLR	PLAYER_LOC
;;	CMP	A3,A1
;;	JRNE	DRG_FP_NEXT1			;BR=NOT ON THIS SIDE
;;	MOVE	A9,A9
;;	JRNZ	DRG_POSITION_TO_SAVE		;BR=OFFENSIVE TEAMMATE ON SIDE
;;	INC	A9
;DRG_FP_NEXT1
;	ADDI	PLAYER_BLOCK_SIZE*2,A6
;
;;	MOVE	*A6(POF_OBJECT),A14,L		;OPPOSING OFFENSE
;;	MOVE	*A14(OFLAGS),A14,W
;;	BTST	B_OFSCRN,A14
;;	JRZ	DRG_POSITION_TO_SAVE		;BR=ON SCREEN, DON'T GO
;
;	MOVE	*A6(POF_D_PUCK),A14,W		;OPPOSING OFFENSE (GOALIE)
;	CMP	A4,A14
;	JRLE	DRG_POSITION_TO_SAVE		;BR=CLOSE ENOUGH, DON'T GO
;
;;	CALLR	PLAYER_LOC
;;	CMP	A3,A1
;;	JRNE	DRG_FP_NEXT2			;BR=NOT ON THIS SIDE
;;	MOVE	A9,A9
;;	JRNZ	DRG_POSITION_TO_SAVE		;BR=OFFENSIVE TEAMMATE ON SIDE
;DRG_FP_NEXT2
;	SUBK	32,A4
;
;	MOVE	A2,A0
;	GBLOCK	a0,a6
;	MOVE	*A6(POF_D_PUCK),A14,W		;YOUR OFFENSE
;	CMP	A4,A14
;	JRLE	DRG_POSITION_TO_SAVE		;BR=CLOSE ENOUGH
;
;	ADDI	PLAYER_BLOCK_SIZE,A6
;	MOVE	*A6(POF_D_PUCK),A14,W		;YOUR OFFENSE
;	CMP	A4,A14
;	JRLE	DRG_POSITION_TO_SAVE		;BR=CLOSE ENOUGH
;
;;	CLR	A14				;MOVK	PM_GSTAND,A14
;;	MOVE	A14,*A6(POF_MODE),W
;
;;	MOVE	*A7(OXVAL),A0,L
;;	MOVE	*A7(OZVAL),A1,L
;
;DRG_FP_SETDIR
